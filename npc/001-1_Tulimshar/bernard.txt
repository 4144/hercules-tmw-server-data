//

001-1.gat,37,44,0	script	Bernard	117,{
	if(TMW_Quest >= 4) goto L_Done2;
	if(TMW_Quest == 3) goto L_Progress2;
	if(TMW_Quest == 2) goto L_Done1;
	if(TMW_Quest == 1) goto L_Progress;

	set @TEMP, rand(2);
	if(@TEMP == 0) goto L_Opening0;
	if(@TEMP == 1) goto L_Opening1;

L_Opening0:
	mes "[Bernard]";
	mes "\"In Tulimshar, there are a lot of maggots. I need a favor, please help me. Will you?\"";
	next;
	goto L_Ask;

L_Opening1:
	mes "[Bernard	]";
	mes "\"The taste of maggots in soup is... unforgettable, taste so good. Will you help me kill some?\"";
	next;
	goto L_Ask;

L_Ask:
	menu
		"Yes", L_Yes,
		"No", L_No;

L_Yes:
	set @TEMP, rand(2);
	if(@TEMP == 0) goto L_Req0;
	if(@TEMP == 1) goto L_Req1;

L_Req0:
	mes "[Bernard]";
	mes "\"I need a [Roasted Maggot] for my soup.\"";
	next;
	goto L_Set;

L_Req1:
	mes "[Bernard]";
	mes "\"Bring me a [Roasted Maggot]. I'll give you something if you do.\"";
	next;
	goto L_Set;

L_Set:
	set TMW_Quest,1;
	mes "[Bernard]";
	mes "\"Please bring me it!\"";
	close;

L_Progress:
	if (countitem("RoastedMaggot") < 1) goto L_NotEnough;
	mes "[Bernard]";
	mes "\"Perfect! It's perfect !";
	mes "You brought me my Roasted Maggot!  Here, have some cake.\"";
	getinventorylist;
	if (@inventorylist_count - (countitem("RoastedMaggot") == 1) > 99 - (countitem("CherryCake") == 0) ) goto L_TooMany;
	delitem "RoastedMaggot", 1;
        getexp 100, 0;
	getitem "CherryCake", 5;
	set TMW_Quest, 2;
	close;

L_Progress2:
        if (countitem("MaggotSlime") < 3) goto L_NotEnough1;
        mes "[Bernard]";
        mes "\"Nice ! It'll perfect, so perfect !";           
        mes "You brought me the 3 maggots slimes! Here, have some beer.\"";
        getinventorylist;             
        if ((@inventorylist_count - (countitem("MaggotSlime") == 3) > 99  - (countitem("Beer") == 0) )) goto L_TooMany;
        delitem "MaggotSlime", 3;
        getexp 100, 0;     
        getitem "Beer", 3;       
        set TMW_Quest, 4;                
        close;


L_NotEnough:
	mes "[Bernard]";
	mes "\"Please hurry and bring me a [Roasted Maggot].\"";
	close;

L_NotEnough1:
        mes "[Bernard]";
        mes "\"Please hurry and bring me 3 [Maggot Slime]s.\"";
        close;


L_Done1:
	mes "[Bernard]";
	mes "\"Thank you so much !\"";
	mes "\"But something is missing to make the soup creamy!\"";
	next;
	mes "\"I need 3 [Maggot Slime]s for the soup.\"";
	mes "\"Bring me them. I'll give you something if you do.\"";
	set TMW_Quest,3;
	close;

L_Done2:
        mes "[Bernard]";
        mes "\"I didn't tell you, I also put beer in my soup.  I hope you like beer as much as I do because..\"";
        next;
        mes "\"Beer is life!\"";
        close;


L_No:
	close;

L_TooMany:
	next;
	mes "[Bernard]";
	mes "\"You don't have room for my reward. I'll wait until you do.\"";
	close;
}
