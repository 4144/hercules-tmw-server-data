//

002-4.gat,32,94,6	script	Sema	109,{

	if (TMW_Quest >= 40) goto L_Sema_Done;
	if (TMW_Quest == 39) goto L_Sema_Happy;
	if (TMW_Quest == 38) goto L_Sema_GCM;

	mes "[Sema the Miner]";
	mes "\"This place is odd.  There is a chest somewhere in this palace, but I don't know where.  I think the archants have eaten all the spare keys for the chest, too.\"";
	close;

L_Sema_GCM:
	mes "[Sema the Miner]";
	mes "\"I saw a giant cave maggot eat one of the other miners and it might eat me!  Can you kill it?\"";
	menu
		"Yes.", L_Sema_Yes,
		"No.", L_Sema_No;
	close;

L_Sema_No:
	mes "[Sema the Miner]";
	mes "\"Ahh!  I am very scared now!\"";
	close;

L_Sema_Yes:
	set TMW_Quest, 39;
	monster "002-4.gat", 70, 90, "", 1059, 1;
	mes "[Sema the Miner]";
	mes "\"Good luck.  To prove you killed it, please bring the [Iten] it swallowed when it ate the miner.  Be careful, there could be more than one.\"";
	close;

L_Sema_Happy:
	if (countitem ("Iten") < 1) goto L_Sema_Not_Happy;
	getinventorylist;
	if (@inventorylist_count - (countitem ("Iten") == 1) > 99) goto L_Sema_TooMany;
	delitem "Iten", 1;
	getitem "DesertShirt", 1;
	set TMW_Quest, 40;
	mes "[Sema the Miner]";
	mes "\"You killed it!  What a relief!  Please accept this [Desert Shirt] as reward.\"";
	close;

L_Sema_TooMany:
	mes "[Sema the Miner]";
	mes "\"You have too many items for me to give you a reward.  Please drop or sell something so I can give it to you.\"";
	close;

L_Sema_Not_Happy:
	set TMW_Quest, 38;
	itemheal -Hp, 0;
	mes "A stone falls on your head.";
	close;

L_Sema_Done:
	mes "[Sema the Miner]";
	mes "\"Thank you for all your help.\"";
	close;

}
