009-2.gat,123,45,0	script	Olana	140,{ 

  if (FLAGS & FLAG_ROSSI_COMPLETED) goto L_FinalEnd;
  if (Rossy_Quest == 17 || Rossy_Quest == 18) goto L_End;
  if (Rossy_Quest >= 14 && Rossy_Quest <= 16) goto L_Julia;
  if (Rossy_Quest == 13) goto L_Best;
  if (Rossy_Quest == 12) goto L_Give;
  if (Rossy_Quest == 11) goto L_Allergic;
  if (Rossy_Quest == 10) goto L_BringRose;
  if (Rossy_Quest == 9) goto L_RC;
  if (Rossy_Quest == 8) goto L_RL;
  if (Rossy_Quest >= 6 && Rossy_Quest < 8) goto L_BL;
  if (Rossy_Quest == 5) goto L_Bring;
  if (Rossy_Quest == 4) goto L_Cont;
  if (Rossy_Quest == 3) goto L_See;
  if (Rossy_Quest == 2) goto L_Daughter;

  mes "[Olana]";
  mes "\"I come from Tulimshar to Hurnscald for vacation with my two young daughters, Rossy and Julia. I already have been to Hurnscald once and felt quite safe in ths area.\"";
  next;
  mes "\"I let my two lovely girls play in the woods near by but they still didn't came back!\"";
  next;
  mes "\"Please go and have a look in the forest near the swamps and come back to see me when you'll find them.\"";
  close;

L_Daughter:
  mes "[Olana]";
  mes "\"I come from Tulimshar to Hurnscald for vacation with my two young daughters, Rossy and Julia. I already have been to Hurnscald once and felt quite safe in ths area.\"";
  mes "\"I let my two lovely girls play in the woods near by but they still didn't came back!\"";
  next;
  mes "*Olana begins to sobb...*";
  next;
  menu
    "Wait, I saw Rossy, she is fine.", L_Help,
    "Wait a bit, I'm sure they'll both get back soon.", L_No;

L_No:
  close;

L_Help:
  mes "[Olana]";
  mes "\"Oh yes? Is Rossy alright? That's good. I'm relieved. I hope she'll find Julia soon.\"";
  next;
  mes "\"Oh by the way, I would need your help myself too. I think Rossy colected some cherries while she was in the woods, could you bring me them please?.\"";
  menu
    "Sure, I'll be back with cherries.", L_Set,
    "Sorry, I'm too busy.", L_No;

L_Set:
  mes "[Olana]";
  set Rossy_Quest, 3;
  mes "\"I'd like 10 Cherries.\"";
  close;

L_Get:
  mes "[Olana]";
  mes "\"Please, get 10 cherries for me.\"";
  close;

L_See:
  if(countitem("cherry") < 10) goto L_Get;
  mes "[Olana]";
  set Rossy_Quest, 4;
  mes "\"You got them for me! Thank you! Here you are... a small reward from me for your precious help!\"";
  delitem "cherry", 10;
  getexp 1000, 0;
  set zeny, zeny + 1000;
  goto L_Task;

L_Task:
  mes "[Olana]";
  mes "\"It seems I have another task for you, would you like to help me again?\"";
  menu
    "Sure, tell me about it.", L_Cont,
    "No, sorry, I'm busy.", L_No;

L_Cont:
  mes "[Olana]";
  mes "\"Can you give this letter to my little daughter please? Her teacher asked me to do it, but I can't walk, my knees...\"";
  menu
    "Of course I can. Hand me the letter, I'll give it to Rossy as soon as I see her.", L_Let,
    "No, sorry, I'm not interested in that sort of task. Hand it to her yourself.", L_No;
    
L_Let:
  mes "[Olana]";
  set Rossy_Quest, 5;
  mes "\"Please bring it to her as fast as you can, she really needs to know this news!\"";
  close;
  
L_Bring:
  mes "[Olana]";
  mes "\"Bring this letter to my daughter as fast as you can, please!\"";
  close;
  
L_BL:
  mes "[Olana]";
  mes "\"Good, now she remembers that she needs to craft a potion! Please, "+ strcharinfo(0) +", help Rossy, she will tell you what to do.\"";
  close;
  
L_Done:
  mes "[Olana]";
  mes "\"Thanks you "+ strcharinfo(0) +", I and Rossy will never forget what you did for us!\"";
  goto L_Rose;
  
L_Rose:
  mes "[Olana]";
  mes "\"I would like congratulate my daughter, Rossy, would you help me?\"";
  menu
    "Just tell me what to do.", L_RL,
    "No, I'm busy, maybe later.", L_No;
    
L_RL:
  mes "[Olana]";
  set Rossy_Quest, 9;
  mes "\"Can you bring me 15 [Red Rose]s? I would like to give them to my daughter because she succesfully made her potion!\"";
  menu
    "Ok, I will be back with these.", L_RG,
    "I'm allergic to roses, can't even touch them. Sorry.", L_No;
    
L_RG:
  mes "[Olana]";
  mes "\"Please, bring me 15 [Red Rose]s as soon as you can.\"";
  close;

L_RC:
  if(countitem("redrose") < 15) goto L_RG;
  mes "[Olana]";
  set Rossy_Quest, 10;
  mes "\"Great job! Please hand them to my brilliant daughter, Rossy.\"";
  close;
  
L_BringRose:
  mes "[Olana]";
  mes "\"Now please hand them to my brilliant daughter, Rossy.\"";
  close;
  
L_Allergic:
  mes "[Olana]";
  set Rossy_Quest, 12;
  mes "\"Did she like my present?\"";
  menu
    "Sure she did...", L_No,
    "Well, you should of known that your daughter is allergic to roses.", -;
  mes "[Olana]";
  mes "\"How stupid I am! Here, take some of my money and buy [Red Tulip]s instead, about the same amount. Keep the flowers.\"";
  set zeny, zeny + 1500;
  next;
  mes "\"I want Rossy to be happy and to forgive me for my stupidity.\"";
  menu
    "I will give her the tulips when I'll see her.", -;
  mes "[Olana]";
  mes "\"And don't forget to tell her that I'm really sorry.\"";
  close;
  
L_Give:
  mes "[Olana]";
  mes "\""+ strcharinfo(0) +", please, get 15 [Red Tulip]s and hand them to Rossy. Don't forget to tell her that I'm really sorry for the mistake I did.\"";
  close;
  
L_Best:
  mes "[Olana]";
  mes "\"Thank you for your help "+ strcharinfo(0) +"!\"";
  close;

L_Julia:
  mes "[Olana]";
  mes "\"Where is Julia? I want to know where she is! Go and find her please "+ strcharinfo(0) +"!\"";
  close;

L_End:
  mes "[Olana]";
  mes "\"Where is Julia? I want to know where she is! Go and find her please "+ strcharinfo(0) +"!\"";
  menu
    "She'll be back soon, trust me, I saved her from an evil spirit.", L_Found,
    "I didn't saw her yet, sorry.", L_No;

L_Found:
  mes "[Olana]";
  if (Rossy_Quest == 17) set Rossy_Quest, 18;
  if (Rossy_Quest == 18) set Rossy_Quest, 19;
  mes "\"Thank you "+ strcharinfo(0) +", thank you! Here is a reward for you.\"";
  getexp 5000, 0;
  set zeny, zeny + 5000;
  if (Rossy_Quest == 19) goto L_Clear;
  close;

L_Clear:
  set Rossy_Quest, 0;
  set cavefights, 0;
  set FLAGS, FLAGS | FLAG_ROSSI_COMPLETED;
  close;

L_FinalEnd:
  mes "[Olana]";
  mes "*The mother is calmly waiting her daughters to come back for diner.*";
  close;
  
}
