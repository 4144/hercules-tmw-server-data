010-2.gat,33,25,0	script	Chef	148,{
	mes "[Chef]";
	mes "\"I'm too busy right now to talk.\"";

	callfunc "KadiyaSubquestConsts";
        if ((@Q_kadiya_status == @Q_STATUS_WANTS_CHOCOCAKE) && (countitem ("MopoxCurePotion") > 0))
        	goto L_lace_chococake;

        if ((@Q_kadiya_status == @Q_STATUS_WANTS_ORANGECUPCAKE) && (countitem ("MopoxCurePotion") > 0))
        	goto L_lace_cupcake;
        close;

L_lace_chococake:
        next;
        menu
		"Sorry.", L_end,
                "I need your help with a sick young girl!", -;

	mes "[Chef]";
        mes "You explain Kadiya's situation to him.";
        mes "\"Hmrmf.  I can't help you; I'm a chef, not a baker!\"";
        close;

L_lace_cupcake:
        next;
        menu
		"Sorry.", L_end,
                "I need your help with a sick young girl!", -;

	mes "[Chef]";
        mes "You explain Kadiya's situation to him.";
        mes "\"Hmrmf.  I was about to make another batch of orange cupcakes.  Do you have that potion with you?\"";
        next;

        menu
		"No, sorry.", L_bringit,
                "Yes, here you are.", -;

        if (countitem ("MopoxCurePotion") < 1)
        	goto L_bringit;

	mes "[Chef]";
        mes "He shudders as he smells the bottle.";
        mes "\"That is disgusting!  How do you expect me to mask this?\"";
        mes "He ponders his own question for a moment.";
        next;

        mes "[Chef]";
        mes "\"Ah yes.  We need to bind the smell in gingerbread.  Bring me three pieces of gingerbread, one orange, and 500 GP for the other ingredients, and I will make you your cupcake.\"";
        next;

        menu
                "I'll go and get it.", L_end,
		"Here you are!", -,
		"Not now.", L_end;

	if (countitem ("GingerBreadMan") < 3) goto L_no_gingerbread;
	if (countitem ("Orange") < 1) goto L_no_orange;
	if (countitem ("MopoxCurePotion") < 1) goto L_no_potion;
	if (zeny < 500) goto L_no_zeny;

        set zeny, zeny - 500;
        delitem "GingerBreadMan", 3;
        delitem "Orange", 1;
        delitem "MopoxCurePotion", 1;

        getitem "LacedOrangeCupcake", 1;

        mes "[Chef]";
        mes "\"Good, good.  This should work.\"";
        mes "He crumbles the gingerbread, mixes it with the potion and some spices, and finally adds some cupcake batter and the orange pieces.";
        next;

        mes "[Chef]";
        mes "\"Take a seat.\"";
        mes "He fills the mixture into a cupcake form and puts it into the oven.  You watch the cupcake grow for a while.";
        mes "Finally, he removes the form and carefully extracts the cupcake.";
        next;

        mes "[Chef]";
        mes "\"Here you are.  It's a bit bigger than a regular cupcake and will taste a bit like ginger, I think, but otherwise it should be fine.\"";
        close;

L_no_gingerbread:
        mes "[Chef]";
        mes "\"I will need three pieces of gingerbread.\"";
        close;

L_no_orange:
        mes "[Chef]";
        mes "\"Please bring me an orange for these cupcakes.\"";
        close;

L_no_zeny:
        mes "[Chef]";
        mes "\"No.  I need extra ingredients worth 500 GP, and I expect you to pay for those.\"";
        close;

L_no_potion:
        mes "[Chef]";
        mes "\"Where did your Mopox potion go?\"";
        close;

L_bringit:
	mes "[Chef]";
        mes "\"Bring it here when you do.\"";
        next;

L_end:
	close;
}
