// Auldsbel the Wizard
// Transmutation Magic expert

011-1.gat,50,68,0	script	Auldsbel#_M	168,{

 	set @RED, 683;
	set @YELLOW, 682;
	set @BLUE, 681;
	set @MAUVE, 680;
	set @PETAL, 565;
	set @PEARL, 700;
	set @BOTTLE_WATER, 541;
        set @MANA_POTION, 705;
        set @SMALL_HEALING_POTION, 685;
        set @MEDIUM_HEALING_POTION, 686;
        set @IRON_POTION, 567;
        set @CONCENTRATION_POTION, 568;
        set @COCOON, 718;
        set @STINGER, 507;
        set @RED_STINGER, 517;
        set @BLACK_STINGER, 709;
        set @SNAKE_TONGUE, 710;
        set @CAVE_SNAKE_TONGUE, 713;
        set @MOUNTAIN_SNAKE_TONGUE, 711;
        set @GRASS_SNAKE_TONGUE, 712;
        set @MAGGOT_SLIME, 505;

        set @Q_STATUS_INITIAL, 0;
        set @Q_STATUS_POSTINTRO, 1;

        set @Q_MASK, NIBBLE_0_MASK | NIBBLE_1_MASK;
        set @Q_SHIFT, NIBBLE_0_SHIFT;

        set @Q_status, (QUEST_MAGIC & @Q_MASK) >> @Q_SHIFT;

        set @Q_main_status, @Q_status & 31;
        set @Q_component_quest, @Q_status >> 5;

	set @has_magic, getskilllv(SKILL_MAGIC);

      	set @address$, "chap";
        if (Sex == 0)
        	set @address$, "girl";

        if (@Q_status >= @Q_STATUS_POSTINTRO)
        	goto L_short_intro;

        mes "[Robed Man]";
        mes "You notice a middle-aged man in expensive-looking robes.";
        mes "\"Ah, splendid, you finally came!\"";
        mes "He motions you to come closer.";
        next;

        mes "[Robed Man]";
        mes "The man is visibly excited.";
        mes "\"Here, I found a silk cocoon.  Now if you can just give me the obsidian salt, we can see whether it works!\"";
        next;

        menu "Whether what works?", L_intro_explain,
             "What are you talking about?", L_intro_explain,
             "I don't have any obsidian salt.", L_intro_nopowder,
             "Do I know you?", L_intro_identity,
             "Goodbye.", -;
        close;

        set @name_complaint, 0;

L_intro_explain:
        mes "[Robed Man]";
        mes "He frowns.";
        mes "\"The transmutation experiment, of course!  You can't have forgotten already...?\"";
        next;
        goto L_intro_identity;

L_intro_nopowder:
        mes "[Robed Man]";
        mes "\"What!?  You traveled all this way without any obsidian...\"";
        next;
        goto L_intro_identity;

L_intro_identity:
        mes "[Robed Man]";
        mes "He eyes you more carefully.";
        if (Sex == 0)
           mes "\"How odd.  I could have sworn that you were a man the last time we met.  Not that I mind...\"";

        mes "\"Wait.  You're not Padric.\"";
        next;

        set @xmsg$, "Right... my name is " + strcharinfo(0) + ".";
        if (strcharinfo(0) == "Padric")
           set @xmsg$, "Actually, I am, but I don't know you..?";

        menu @xmsg$, L_intro_wrongperson,
             "You're not very good with faces, are you?", L_intro_nogood,
             "Who are you?", L_intro_who_are_you,
             "Goodbye.", -;
        close;

L_intro_wrongperson:
        mes "[Robed Man]";
        mes "He laughs.";
        mes "\"Ah, I knew it... you're not the first one today, either.  I should apologize, I am horrible with faces.  Well, if you don't mind, please hurry along, I should go back to my experiments.\"";
        next;

L_intro_primary_menu:
        menu "Who are you?", L_intro_who_are_you,
             "Goodbye.", -;
        close;

L_intro_nogood:
        mes "[Robed Man]";
        mes "He laughs.";
        mes "\"Yes, you could say that.  Well, I shall get back to my experiments, then; I think I shall manage something that requires no obsidian salt instead.\"";
        next;

        menu "Who are you?", L_intro_who_are_you,
             "What is obsidian salt, anyway?", L_intro_obsidian_salt,
             "Goodbye.", -;
        close;

L_intro_obsidian_salt:
        mes "[Robed Man]";
        mes "\"Oh, obsidian salt is a catalyst... or rather a theoretical catalyst.  We know that it has to have an application somewhere, and I do have the strong suspicion that it may be linked to natural transmogrification...\"";
        next;
        goto L_intro_primary_menu;

L_intro_who_are_you:
        mes "[Robed Man]";
        mes "\"Oh, oh my...  of course you wouldn't know me, being from the countryside and all.\"";
        mes "He laughs.";
        mes "\"Well, my young friend, I am none other than Auldsbel the Mostly Grayish, of the Council of Transmuters!\"";
        next;

        set @Q_main_status, @Q_STATUS_POSTINTRO;
        callsub S_update_var;
        set MAGIC_FLAGS, MAGIC_FLAGS | MFLAG_KNOWS_AULDSBEL;

        mes "[Auldsbel the Wizard]";
        mes "\"That means that I'm a wizard, in case you were wondering.\"";
        next;
        goto L_main_menu;

L_short_intro:
        mes "[Auldsbel the Wizard]";
        mes "\"Welcome back, Padric!\"";
        next;
        goto L_main_menu;

L_main_menu:
        if (@has_magic)
           goto L_main_menu_magic;

        menu "How does this 'magic' work?", L_about_magic,
             "I want to become a wizard!", L_learn_magic,
             "Where are you from?", L_about_auldsbel,
             "Do you need help with your experiments?", L_quest,
             "What do you know about...", L_question,
             "Goodbye.", -;
        close;

L_main_menu_magic:
        menu "How does magic work?", L_about_magic,
             "Can you teach me a spell?", L_learn_spell,
             "Where are you from?", L_about_auldsbel,
             "Do you need help with your experiments?", L_quest,
             "What do you know about...", L_question,
             "Goodbye.", -;
        close;

L_about_magic:
        mes "[Auldsbel the Wizard]";
        mes "\"Magic is a universal force that comes from within; only few individuals have the power to channel and manipulate it.  Most magic users resort to spells-- prefabricated invocations-- to access and control their magical power.\"";
        next;
        mes "[Auldsbel the Wizard]";
        mes "\"Most spells are claimed by one of the five schools of magic.  To properly use them, a magic user need not only have sufficient prowess in magic overall, but also in the specifics of that particular school.\"";
        next;

L_about_magic_minimenu:
        menu
		"What are the five schools?", L_about_magic_schools,
		"How can I advance in magic?", L_about_magic_advance,
		"How do spells work?", L_about_magic_spells,
		"Where can I learn spells?", L_about_magic_learn,
		"Never mind.", -;
        goto L_main_menu;

L_about_magic_schools:
        mes "[Auldsbel the Wizard]";
        mes "\"With few exceptions, all spells belong to one of the five schools of magic:  Transmutation, War, Astral, Life, and Nature.\"";
        next;

L_about_schools_minimenu:
        menu
		"What's Transmutation magic?", L_about_transmutation,
		"What's War magic?", L_about_war,
		"What's Astral magic?", L_about_astral,
		"What's Life magic?", L_about_life,
		"What's Nature magic?", L_about_nature,
		"Are there other spells?", L_about_other_spells,
		"Thank you.", -;
        goto L_about_magic_minimenu;

L_about_transmutation:
        mes "[Auldsbel the Wizard]";
        mes "\"Transmutation magic deals with forming matter into a new shape.  Some advanced transmutation magic can also expose special properties of the material in question.\"";
        next;
        mes "[Auldsbel the Wizard]";
        mes "He smiles.";
        mes "\"Transmutation magic is the engine of human civilization.  By allowing us to shape buildings, tools, and other items according to the power of our imagination, it gives us mastery over nature.\"";
        next;
        goto L_about_schools_minimenu;

L_about_astral:
        mes "[Auldsbel the Wizard]";
        mes "\"Astral magic comprises a family of spells that connect the caster to the Astral World.  This connection can be used to pull the caster 'through'-- effectively teleporting them-- or to 'pull others through'-- summoning creatures.\"";
        next;
        mes "[Auldsbel the Wizard]";
        mes "\"There are also some more direct uses of powers of the Astral World; mainly spells that affect other spells.\"";
        next;
        goto L_about_schools_minimenu;

L_about_war:
        mes "[Auldsbel the Wizard]";
        mes "\"War magic deals with the inevitable necessity of struggle against other creatures, and sometimes even against other humans.  War magic exclusively focusses on dealing damage and destruction.\"";
        next;
        mes "[Auldsbel the Wizard]";
        mes "\"If you use War magic, You better have a Transmuter around for the aftermath, to clear up your collateral damage.\"";
        next;
        goto L_about_schools_minimenu;

L_about_life:
        mes "[Auldsbel the Wizard]";
        mes "\"Life magic deals with healing.  Not much of a surprise there.\"";
        next;
        goto L_about_schools_minimenu;

L_about_nature:
        mes "[Auldsbel the Wizard]";
        mes "\"Nature magic is a rather subtle (and, in my eyes, rather weak) kind of magic that deals with manipulating nature as it is.  Think of it as Transmutation magic without being able to actually shape things the way you want.\"";
        next;
        goto L_about_schools_minimenu;

L_about_other_spells:
        mes "[Auldsbel the Wizard]";
        mes "\"A few spells are not claimed by any particular school of magic.  In practice, this means that anyone can cast them if they just have sufficient magical power.  The most prominent example is the 'detect magic' spell, '" + getspellinvocation("detect-magic") + "'.\"";
        next;
        goto L_about_schools_minimenu;



L_about_magic_advance:
        mes "[Auldsbel the Wizard]";
        mes "\"Advancing in your magical powers must come from two sources:  from within and from a person who can guide you in whichever school of magic you wish to advance.\"";
        next;
        mes "[Auldsbel the Wizard]";
        mes "\"For advancing in your general magic power, you must practice magical spells.  Make sure to vary them; you will learn little if you cast the same spell over and over.  Also, spells that consume no components seem not to be very instructive in practice.\"";
        next;
        mes "[Auldsbel the Wizard]";
        mes "\"Once you have gathered enough spellcasting experience, you should be able to advance to the next level of magic.  If you received your magic from a sponsor, you may have to seek out the sponsor again to advance.\"";
        next;
        mes "[Auldsbel the Wizard]";
        mes "\"Similarly, to advance in a particular school of magic, you should seek out someone sufficiently competent in that school.  Each school has a different rite for advancing its students, so make sure to talk to the right person.\"";
        next;
        mes "[Auldsbel the Wizard]";
        mes "\"I myself am a Transmutation Wizard.  Of course I know some spells from the other schools, but my focus is on Transmutation.  Theoretically speaking, I can advance you in this school.\"";
        next;
        goto L_about_magic_minimenu;

L_about_magic_learn:
        mes "[Auldsbel the Wizard]";
        mes "\"Finding and learning new spells is of course important in a magic user's quest towards becoming a full-fledged wizard.\"";
        next;
        mes "[Auldsbel the Wizard]";
        mes "\"Some wizards will be willing to share their knowledge, usually for a price.  But they are not the only sources of magical spells:  many magical books contain spells, and you can occasionally find them written down in the most unusual of places.\"";
        next;
        mes "[Auldsbel the Wizard]";
        mes "\"Some are even hiding as part of folklore or gossip.  Of course, for those it can sometimes be hard to determine just what their prerequisites are...\"";
        next;
        mes "[Auldsbel the Wizard]";
        mes "\"If you decide to hunt for spells, make sure to keep a journal with you.  Some spell invocations may only cross your path once in your lifetime; you must not allow them to get away!\"";
        next;
        goto L_about_magic_minimenu;

L_about_magic_spells:
        mes "[Auldsbel the Wizard]";
        mes "\"Magical spells are shortcuts, true magic bound to a word.  No-one today remembers how they were created at the beginning of time, though many have tried to find it out, and failed...\"";
        next;
        mes "[Auldsbel the Wizard]";
        mes "\"These magical words are the spells' invocations.  Spoken by someone who can't use magic, or by someone who doesn't satisfy the prerequisites, the word stands just for itself.\"";
        next;
        mes "[Auldsbel the Wizard]";
        mes "\"But when spoken by a competent magic user, the word may unleash its effect-- consuming any components it may require, draining the caster's mana, changing the world around it.\"";
        next;
        mes "[Auldsbel the Wizard]";
        mes "\"Some spells require catalysts on top of components, others vary in power depending on whom they are cast on or under what conditions.  However, all spells are affected by the caster's astral power.\"";
        next;

L_about_spells_minimenu:
        menu
		"What is this 'astral power'?", L_about_astral_power,
		"What is a catalyst?", L_about_catalysts,
		"What is a component?", L_about_components,
		"What other prerequisites are there?", L_about_other_prerequisites,
		"Where can I learn spells?", L_about_magic_learn,
		"How often can I cast spells?", L_about_speed,
                "Never mind.", L_about_magic_minimenu;
        close;

L_about_astral_power:
        mes "[Auldsbel the Wizard]";
        mes "\"A person's astral power is determined by several factors:  overall experience, intelligence, and any and all equipment the person may be wearing at a given time.\"";
        next;
        mes "[Auldsbel the Wizard]";
        mes "\"Greater astral power mans more powerful spells.  Since equipment can greatly decrease astral power, most magic users tend to be careful about what they wear-- it takes a while to recover astral power even after armour is unequipped.\"";
        next;
        mes "[Auldsbel the Wizard]";
        mes "\"The worst offenders are metal items, particularly shields and body armour.  Helmets and gloves get in the way, too.  Still, a few special or enchanted items may even increase magical power.\"";
        next;
        goto L_about_spells_minimenu;

L_about_catalysts:
        mes "[Auldsbel the Wizard]";
        mes "\"A catalyst is a material prerequisite to a spell that is not consumed as part of the spell.  For example, the Transmuter's Tablet is required to properly perform many of the more powerful transmutation spells, but it is never consumed.\"";
        next;
        goto L_about_spells_minimenu;

L_about_components:
        mes "[Auldsbel the Wizard]";
        mes "\"A material component is an item that is consumed as part of the spell's magic.  For example, when transmuting wood into arrows, you must consume a raw log to shape the arrows out of it.\"";
        next;
        goto L_about_spells_minimenu;

L_about_other_prerequisites:
        mes "[Auldsbel the Wizard]";
        mes "\"Some spells have additional requirements-- they can only be cast underground, or when you are standing very close to the person you are casting them on, or only when you are wearing a particular enchanted item.  Spells are quirky, so read their descriptions carefully-- if you do find a description.\"";
        next;
        goto L_about_spells_minimenu;

L_about_speed:
        mes "[Auldsbel the Wizard]";
        mes "\"Most spells are effective immediately, unless they require some complex astral connectoin-- summoning or teleporting can take a while to take effect, for example.  Still, after casting a spell you usually need a moment to recover before casting the next.\"";
        next;
        goto L_about_spells_minimenu;


L_about_auldsbel:
        mes "[Auldsbel the Wizard]";
        mes "\"Well, there is no harm in giving you the general picture, I suppose.\"";
        mes "He sighs, as if he had been forced to repeat something one time too many.";
        mes "\"I am from the Council of Transmuters, the head organ of organized Transmutation magic in the known world.\"";
        next;
        mes "[Auldsbel the Wizard]";
        mes "\"The council oversees recruitment, education, accreditation, and, if necessary, disciplining of Transmuters.";
        mes "It ensures that Transmuter conduct is according to its statutes and acts as representative and point of contact towards other entities.\"";
        next;
        mes "[Auldsbel the Wizard]";
        mes "\"Of all the schools of magic, the School of Transmutation the most organised by far, and held in high esteem by rulers all across the world.  Of course this is not only due to the outstanding and rigid structure of the school, but also because of the exceptional services that its members provide.\"";
        next;
        mes "[Auldsbel the Wizard]";
        mes "\"The Council, presently overseen by Lord Transmogrifier Pontorias the Plaid (May His Shape Reflect His Soul Forever), consists of fourty-nine members and is situated in the citadel of Dorngard, in the northern mountains near the Crimson Cascade.\"";
        next;
        mes "[Auldsbel the Wizard]";
        mes "\"The Council is souvereign over three hundred acres of land and nearby farming communities and has been ever since Yorick the Younger shaped Dorngard out of a mountain by sheer power of will, to build a home for the school his father had founded.\"";
        next;
        mes "[Auldsbel the Wizard]";
        mes "\"The Council has held peaceful relations with nearby realms for almost two centruries now and is widely regarded as a reliable partner in shaping civilization to allow it to evolve towards its next stage.\"";
        next;

        menu
		"All right, but what about you?.", L_about_auldsbel_2,
		"Never mind.", -;
        goto L_main_menu;

L_about_auldsbel_2:
        mes "[Auldsbel the Wizard]";
        mes "\"Oh, myself?  I am just vacationing in the area.  Very relaxed and peaceful place, the Hurnscald area.  And plenty of splendid specimen for experimentation.\"";
        next;
        goto L_main_menu;

L_learn_magic:
        mes "[Auldsbel the Wizard]";
        mes "Auldsbel laughs heartily.";
        mes "\"Hah, if only it were so easy!  No, my young friend, I fear that 'learning magic' here is not an option.  Either you are born with it, or without.\"";
        next;
        mes "[Auldsbel the Wizard]";
        mes "\"Sure, there are a few entities that may grant you magic-- Ether Spirits or Mana Seeds or Great Dragons-- but those are the stuff of legends, so I suggest that you don't waste your life trying to find one of them.\"";
        next;
        goto L_main_menu;

L_question:
        set @QQ_ELANORE, 1;
        set @QQ_MANASEED, 2;
        set @QQ_WYARA, 3;
        set @QQ_SAGATHA, 4;

        setarray @choice$, "", "", "", "", "";
        set @choices_nr, 0;
        setarray @choice_idx, 0, 0, 0, 0, 0;

        set @choice$[@choices_nr], "...Elanore the Healer?";
        set @choice_idx[@choices_nr], @QQ_ELANORE;
        set @choices_nr, @choices_nr + 1;

        if (!(MAGIC_FLAGS & MFLAG_KNOWS_MANASEED))
        	goto L_Q_post_manaseed;
        set @choice$[@choices_nr], "...the Mana Seed?";
        set @choice_idx[@choices_nr], @QQ_MANASEED;
        set @choices_nr, @choices_nr + 1;
L_Q_post_manaseed:

        if (!(MAGIC_FLAGS & MFLAG_KNOWS_WYARA))
        	goto L_Q_post_wyara;
        set @choice$[@choices_nr], "...Wyara the Witch?";
        set @choice_idx[@choices_nr], @QQ_WYARA;
        set @choices_nr, @choices_nr + 1;
L_Q_post_wyara:

        if (!(MAGIC_FLAGS & MFLAG_KNOWS_SAGATHA))
        	goto L_Q_post_sagatha;
        set @choice$[@choices_nr], "...Sagatha the Witch?";
        set @choice_idx[@choices_nr], @QQ_SAGATHA;
        set @choices_nr, @choices_nr + 1;
L_Q_post_sagatha:

        set @choice$[@choices_nr], "...never mind.";
        set @choice_idx[@choices_nr], 0;
        set @choices_nr, @choices_nr + 1;

        menu @choice$[0], -,
             @choice$[1], -,
             @choice$[2], -,
             @choice$[3], -,
             @choice$[4], -;

        set @menu, @menu - 1;

        if (@menu >= @choices_nr)
		set @menu, 0;
	
        set @c, @choice_idx[@menu];

//        mes "menu = " + @menu + ", c =  " + @c + " nr=" + @choices_nr + ", ids = " + @choice_idx[0];
//        next;

        if (@c == 0)		goto L_main_menu;
        if (@c == @QQ_ELANORE)	goto L_Q_elanore;
        if (@c == @QQ_MANASEED)	goto L_Q_manaseed;
        if (@c == @QQ_WYARA)	goto L_Q_wyara;
        if (@c == @QQ_SAGATHA)	goto L_Q_sagatha;
        close;

L_Q_elanore:
        mes "[Auldsbel the Wizard]";
        mes "\"Ah, Elanore.  A kind little woman.  Also a very proficient healer, from what I have been told, though we have interacted little.  If you are interested in Life magic, you might want to talk to her.\"";
        next;
        goto L_main_menu;

L_Q_wyara:
        mes "[Auldsbel the Wizard]";
        mes "\"The village witch?  Not exactly the brightest person, but she has managed to figure out how to brew potions.  I doubt that she can do any real magic, though.\"";
        next;
        goto L_main_menu;

L_Q_sagatha:
        mes "[Auldsbel the Wizard]";
        mes "Auldsbel frowns.";
        mes "\"That witch, hmm?  She's a well-known trouble-maker, and quite a clever one at that; she once prevented us from cutting down a forest near Dorngaard that the villages wanted to use for farming, using some ingenious magic.\"";
        next;
        mes "[Auldsbel the Wizard]";
        mes "\"That woman has spent too much time alone in the forests... I have the strong suspicion that she is more loyal towards her animals than towards us humans!  I recommend that you stay away from her, if you value your well-being.\"";
        next;
        goto L_main_menu;

L_Q_manaseed:
        mes "[Auldsbel the Wizard]";
        if (@has_magic)
        	goto L_Q_manaseed_withmagic;
        if (MAGIC_FLAGS & MFLAG_DRANK_POTION)
		goto L_Q_manaseed_prepared;
        if (MAGIC_FLAGS & MFLAG_TOUCHED_MANASEED)
		goto L_Q_manaseed_touched;
        mes "\"You have found an acual Mana Seed?  That's impossible!  Well, very unlikely...  Then again, some others have told me similar rumours.  I find it hard to believe that...\"";
        next;
        mes "[Auldsbel the Wizard]";
        mes "\"Well, if it's true, then try touching it.  It should do you no harm, but if you are very, very lucky, it just might grant you some minuscle magical power.\"";
        next;
        goto L_main_menu;

L_Q_manaseed_touched:
        if (MAGIC_FLAGS & MFLAG_DRANK_POTION)
		goto L_Q_manaseed_prepared;
        mes "\"So you touched the Mana Seed, and its power flowed right through you?  You are lucky-- it is willing to share-- but you are also unlucky, in that you lack the discipline and control needed to contain this power.\"";
        next;

        if (MAGIC_FLAGS & MFLAG_KNOWS_MANAPOTION)
		goto L_Q_manaseed_touched_short;

        mes "[Auldsbel the Wizard]";
        mes "\"Legend has it that you can substitute for such control by imbibing a Mana Potion.  I am not sure whether that legend is true, but it might be worth trying out for you.\"";
        next;

        set MAGIC_FLAGS, MAGIC_FLAGS | MFLAG_KNOWS_MANAPOTION;
L_Q_manaseed_touched_short:
        menu
		"Where can I get a Mana Potion?", L_where_mana_potion,
		"Can you make a Mana Potion?", L_make_mana_potion,
		"Thank you.", -;
        goto L_main_menu;

L_where_mana_potion:
        mes "[Auldsbel the Wizard]";
        mes "\"Well, quite a few alchemists should be able to brew one for you.  Or maybe the village witch, even, though I personally would recommend seeing an alchemist.\"";
        next;
        goto L_Q_manaseed_touched_short;

L_make_mana_potion:
        mes "[Auldsbel the Wizard]";
        mes "\"Certainly, I can transmute some components into a Mana Potion for you.  Let's see... I will need one pearl, 10,000 GP, about twenty Mauve leaves, and some Gamboge ones... ten should do, I think.  Oh, and a bottle of water, of course.\"";
        next;
        menu
		"Here you are.", -,
		"I will look for those items.", L_main_menu,
		"I'm not interested.", L_Q_manaseed_touched_short;

        if (zeny < 10000)
		goto L_make_mana_potion_missing;
        if (countitem(@YELLOW) < 10)
		goto L_make_mana_potion_missing;
        if (countitem(@MAUVE) < 20)
		goto L_make_mana_potion_missing;
        if (countitem(@PEARL) < 1)
		goto L_make_mana_potion_missing;
        if (countitem(@BOTTLE_WATER) < 1)
		goto L_make_mana_potion_missing;

        set zeny, zeny - 10000;
        delitem @YELLOW, 10;
        delitem @MAUVE, 20;
        delitem @PEARL, 1;
        delitem @BOTTLE_WATER, 1;
        getitem @MANA_POTION, 1;

        mes "[Auldsbel the Wizard]";
        mes "Auldsbel pockets your GP and the pearl, then stuffs the leaves into the bottle.  Holding the bottle between his hands, he focusses briefly.  The water and leaves flash bright red, then the leaves dissolve.";
        next;

        mes "[Auldsbel the Wizard]";
        mes "The wizard pours the resultant mixture into a different bottle.  \"It will lose its power quickly if left in a glass bottle\", he explains.";
        mes "He hands you the final result, which feels surprisingly heavy.";
        next;

        menu
		"Thank you!", L_main_menu,
		"What about the pearl and GP?", -;

        mes "[Auldsbel the Wizard]";
        mes "Auldsbel raises his eyebrows in surprise.";
        mes "\"Those were payment.  You don't expect me to work for free, now do you?\"";
        next;

        goto L_main_menu;

L_make_mana_potion_missing:
        mes "[Auldsbel the Wizard]";
        mes "\"No, I need one pearl, 10,000 GP, 20 Mauve leaves, 10 Gamboge leaves, and one bottle of water.\"";
        next;
        goto L_Q_manaseed_touched_short;

L_Q_manaseed_prepared:
        mes "\"So you found a Mana Seed and preprared yourself by drinking a mana potion?  I recommend that you visit the seed again and see if that actually works...\"";
        next;
        goto L_main_menu;

L_Q_manaseed_withmagic:
        if (MAGIC_FLAGS & MFLAG_MANASEED_MAXEDOUT)
		goto L_Q_manaseed_maxedout;
        mes "\"I still find it hard to believe that you have found an actual Mana Seed here, in the middle of nowhere...  Well, I suggest that you keep visiting it.  As your control over magic grows, it may grant you additional power.\"";
        next;
        goto L_main_menu;

L_Q_manaseed_maxedout:
        mes "\"So the mana seed isn't giving you any more power?  You might want to try again later; normally those seeds grow in power, over time.\"";
        next;
        goto L_main_menu;

L_quest:
        if (@Q_component_quest == 0)
		goto L_component_quest_0;
        if (@Q_component_quest == 1)
		goto L_component_quest_1;
        if (@Q_component_quest == 2)
		goto L_component_quest_2;
        if (@Q_component_quest == 3)
		goto L_component_quest_3;
        if (@Q_component_quest == 4)
		goto L_component_quest_4;
        if (@Q_component_quest == 5)
		goto L_component_quest_5;
        mes "[Auldsbel the Wizard]";
        mes "\"You have been very helpful, but at this point I have everything I need.  Except perhaps for a Wumpus Egg, though I have no idea where you could find one... If you ever come across one, I will give you a special reward for it, though.\"";
        next;
        goto L_main_menu;

L_component_quest_0:
        mes "[Auldsbel the Wizard]";
        mes "Auldsbel is visibly delighted.";
        mes "\"Ah, indeed, indeed!  I can often use help with my experiments, and you just happen to be arriving at a particularly opportune time.  See, I found this...\"";
        mes "He pulls something from his pocket and shows it to you.";
        next;
        mes "[Auldsbel the Wizard]";
        mes "\"It's a silk cocoon.  This area has been virtually infested with silkworms, from what I have seen.  This is splendid!  I will try to... do something very special with this one.  But for that I will need twenty Mauve leaves.\"";
        next;
        mes "[Auldsbel the Wizard]";
        mes "\"There are plenty of Mauve plants around, so I'm sure that you won't have a hard time finding the leaves.\"";
        next;
        menu
		"I have them here.", -,
                "Sure, I will look for them.", L_main_menu;

        if (countitem (@MAUVE) < 20)
		goto L_component_quest_missing;

        delitem @MAUVE, 20;
        set zeny, zeny + 2500;
        mes "[Auldsbel the Wizard]";
        mes "\"Well done, my young friend!  Here are 2,500 GP to compensate you for your efforts.\"";
	mes "[You gain 250 experience points]";
	getexp 250, 0;
        set @Q_component_quest, 1;
        callsub S_update_var;
        next;

        goto L_main_menu;

L_component_quest_1:
        mes "[Auldsbel the Wizard]";
        mes "\"Good, good... I am trying to come up with a way to best use the Mauve leaves you brought me, but it seems that I will need further components.  I am not sure about the exact composition yet, but I will need a few potions.\"";
        next;
        mes "[Auldsbel the Wizard]";
        mes "\"Please be a good " + @address$ + " and get me an iron potion, a concentration potion, and three small and three medium healing potions.\"";
        next;
        menu
		"Here you are.", -,
                "I'm not your 'good " + @address$ + "'!", L_main_menu,
                "I'll see what I can do.", L_main_menu;

        if (countitem (@SMALL_HEALING_POTION) < 3)
		goto L_component_quest_missing;
        if (countitem (@MEDIUM_HEALING_POTION) < 3)
		goto L_component_quest_missing;
        if (countitem (@IRON_POTION) < 1)
		goto L_component_quest_missing;
        if (countitem (@CONCENTRATION_POTION) < 1)
		goto L_component_quest_missing;

        delitem @SMALL_HEALING_POTION, 3;
        delitem @MEDIUM_HEALING_POTION, 3;
        delitem @IRON_POTION, 1;
        delitem @CONCENTRATION_POTION, 1;
        set zeny, zeny + 2500;
        mes "[Auldsbel the Wizard]";
        mes "\"Ah, excellent, excellent!  These are precisely what I needed.  Here are another 2,500 GP to compensate you for your efforts.\"";
	mes "[You gain 500 experience points]";
	getexp 500, 0;
        set @Q_component_quest, 2;
        callsub S_update_var;
        next;

        goto L_main_menu;


L_component_quest_2:
        mes "[Auldsbel the Wizard]";
        mes "\"Ah!  Excellent!  Yes, yes, indeed I need help.  I have managed to transmute the components you brought me into a liquid that I believe to be a demetamorphosis stock, but it seems that the details still need some fine-tuning, and I am out of silk cocoons...\"";
        next;
        mes "[Auldsbel the Wizard]";
        mes "\"I would like to run the next batch of experiments on a larger scale, though.  Would you be so kind as to fetch me some one hundred silk cocoons, please?\"";
        next;

        menu
		"One hundred cocoons, here you are.", -,
                "That's a lot; I'll see what I can do.", L_main_menu;

        if (countitem (@COCOON) < 100)
		goto L_component_quest_missing;

        delitem @COCOON, 20;
        set zeny, zeny + 5000;
        mes "[Auldsbel the Wizard]";
        mes "\"Splendid, splendid!  Here are 5,000 GP for you.\"";
        mes "Auldsbel attempts to cram the cocoons into his pockets, with little success.  Finally he gives up and takes them into his hut.";
	mes "[You gain 2,000 experience points]";
	getexp 2000, 0;
        set @Q_component_quest, 3;
        callsub S_update_var;
        next;

        goto L_main_menu;

L_component_quest_3:
        mes "[Auldsbel the Wizard]";
        mes "\"Yes... I'm actually not certain that my demetamorphosis stock will not drain the life out of these little creatures.  Perhaps an alchemical revitalization tincture would be called for.  Fortunately this one is easy, I can make it myself.\"";
        next;
        mes "[Auldsbel the Wizard]";
        mes "\"I'm still not sure how to integrate it into the spell...  but leave that to me.  Can you get me twenty-five red scorpion stingers and twenty-five lumps of maggot slime?  Those should be just what I need.\"";
        next;
        menu
		"Here are your stingers and slimes.", -,
                "I will get back to you once I have them.", L_main_menu;

        if (countitem (@RED_STINGER) < 25)
		goto L_component_quest_missing;
        if (countitem (@MAGGOT_SLIME) < 25)
		goto L_component_quest_missing;

        delitem @RED_STINGER, 25;
        delitem @MAGGOT_SLIME, 25;
        set zeny, zeny + 5000;
        mes "[Auldsbel the Wizard]";
        mes "\"Good " + @address$ + "!  Another 5,000 GP for you.\"";
        mes "\"I believe that I have figured out one possible way to integrate the tincture into the spell... I will let you know how that goes.\"";
	mes "[You gain 10,000 experience points]";
	getexp 10000, 0;
        set @Q_component_quest, 4;
        callsub S_update_var;
        next;

        goto L_main_menu;

L_component_quest_4:
        mes "[Auldsbel the Wizard]";
        mes "\"Hmmyes... See, the thing is that transmuting living beings is not normally something that transmutation magic can do.  It seems that the beings' life force must be overcome to transmute them, but that in turn kills them.\"";
        next;
        mes "[Auldsbel the Wizard]";
        mes "\"But I was wondering whether creatures that already can auto-transmute-- or metamorphose, as some people call it-- might not allow themselves to be subjected to magical transmutation more easily... Still, all of my demetamorphosis attempts so far have failed.\"";
        next;
        mes "[Auldsbel the Wizard]";
        mes "\"I am thinking of injecting the life force of another creature, perhaps using some astral channelling.  Snakes sound most promising, as they have a similar physical shape but a strong life force.\"";
        next;
        mes "[Auldsbel the Wizard]";
        mes "\"Could you get me twenty regular, cave, and mountain snake tongues, please?  So a total of sixty tongues.  This is where most of their life force is concentrated, incidentally.\"";
        next;
        menu
		"Here are your tongues.", -,
                "I will hunt some snakes for you.", L_main_menu;

        if (countitem (@SNAKE_TONGUE) < 20)
		goto L_component_quest_missing;
        if (countitem (@CAVE_SNAKE_TONGUE) < 20)
		goto L_component_quest_missing;
        if (countitem (@MOUNTAIN_SNAKE_TONGUE) < 20)
		goto L_component_quest_missing;

        delitem @SNAKE_TONGUE, 20;
        delitem @CAVE_SNAKE_TONGUE, 20;
        delitem @MOUNTAIN_SNAKE_TONGUE, 20;
        set zeny, zeny + 8000;
        mes "[Auldsbel the Wizard]";
        mes "\"8,000 GP should cover your efforts, I think.\"";
        mes "\"Now let's see if this works...\"";
	mes "[You gain 40,000 experience points]";
	getexp 40000, 0;
        set @Q_component_quest, 5;
        callsub S_update_var;
        next;

        mes "[Auldsbel the Wizard]";
        mes "Auldsbel focuses on the bundle of snake tongues, which begin to assume a bright red colour, then start to glow.  Yellow sparks drop to the ground, as Auldsbel rolls the tongues into a ball.";
        next;
        mes "[Auldsbel the Wizard]";
        mes "He tosses in a cocoon, then squeezes everything together.  A bright red flash blinds you momentarily.";
        next;
        mes "[Auldsbel the Wizard]";
        mes "As Auldsbel opens his hands, there is nothing there.";
        next;
        mes "[Auldsbel the Wizard]";
        mes "He frowns.  \"Their life force was still not strong enough.  Hmm.\"";
        next;

        goto L_main_menu;

L_component_quest_5:
        mes "[Auldsbel the Wizard]";
        mes "\"I do have another assignment for you, but this one will be tricky.  I will need fifty grass snake tongues.  I believe that this may be just enough life force to return the silkworm back to its original shape.\"";
        next;
        menu
		"Here they are.", -,
                "That's quite a challenge.", L_main_menu;

        if (countitem (@GRASS_SNAKE_TONGUE) < 50)
		goto L_component_quest_missing;

        delitem @GRASS_SNAKE_TONGUE, 50;
        set zeny, zeny + 10000;
        mes "[Auldsbel the Wizard]";
        mes "\"Excellent!  Here are 10,000 GP for you, and now let's see how this goes.\"";
	mes "[You gain 100,000 experience points]";
	getexp 100000, 0;
        set @Q_component_quest, 6;
        callsub S_update_var;
        next;

        mes "[Auldsbel the Wizard]";
        mes "Auldsbel rolls the snake tongues into a ball again, whilst chanting some words that you fail to discern.  As the ball begins to glow, he tosses in a silkworm cocoon.";
        mes "He then presses his hands together; you are blinded by a blue flash.";
        next;

        mes "[Auldsbel the Wizard]";
        mes "The wizard steps back in horror.";
        mes "\"Oh my... I should have known.  I have stepped into Astral spell territory here.  This is bad...\"";
        mes "He mumbles a brief spell invocation.";
        mes "\"I suggest that you run.\"";
        next;

        monster "this", 50, 68, "Grass Snake", 1034, 4;
        close;

L_component_quest_missing:
        mes "[Auldsbel the Wizard]";
        mes "\"No, you are missing some items.  Come back later when you have everything!\"";
        next;
        goto L_main_menu;

L_learn_spell:
        mes "[Auldsbel the Wizard]";
        next;
        goto L_main_menu;

S_update_var:
        set @Q_status, @Q_main_status | (@Q_component_quest << 5);
	set QUEST_MAGIC,
		(QUEST_MAGIC & ~(@Q_MASK)
		| (@Q_status << @Q_SHIFT));
	return;
}
