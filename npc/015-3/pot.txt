// The pot where the player can deposit stuff for the cat.

015-3.gat,37,29,0	script	Pot	127, {

    if (Katze > 0)
        mes "It's that old pot again.";
    if (Katze == 0)
        mes "It's a pot.";
    next;

    if (Katze == 1 && KatzeBeenOutside == 1) goto L_NeedsFood;
    if (Katze == 1) goto L_HasMilk;

    if (Katze == 2 && KatzeBeenOutside == 1) goto L_NeedsFur;
    if (Katze == 2) goto L_HasFood;

    if (Katze == 3) goto L_NeedsWood;

    if (Katze == 4 && KatzeBeenOutside == 0) goto L_HasWood;
    if (Katze >= 4) goto L_Finished;

L_NeedsMilk:
    if (countitem(527) > 0)
        menu
            "Pour in some milk", L_GiveMilk,
            "Leave it alone", -;
    close;

L_GiveMilk:
    delitem 527, 1;
    set Katze, 1;
    set KatzeBeenOutside, 0;
    close;

L_HasMilk:
    mes "There is milk in it.";
    next;
    close;

L_NeedsFood:
    mes "The milk is gone!";
    next;

    if (countitem(562) > 0 && countitem(676) > 0)
        menu
            "Put in a chicken leg", L_GiveChicken,
            "Put in a steak", L_GiveSteak,
            "Leave it alone", -;
    if (countitem(562) > 0 && countitem(676) == 0)
        menu
            "Put in a chicken leg", L_GiveChicken,
            "Leave it alone", -;
    if (countitem(562) == 0 && countitem(676) > 0)
        menu
            "Put in a steak", L_GiveSteak,
            "Leave it alone", -;
    close;

L_GiveChicken:
    delitem 562, 1;
    set Katze, 2;
    set KatzeBeenOutside, 0;
    close;

L_GiveSteak:
    delitem 676, 1;
    set Katze, 2;
    set KatzeBeenOutside, 0;
    close;

L_HasFood:
    mes "There is some food in it.";
    next;
    close;

L_NeedsFur:
    mes "And it's empty!";
    next;

    if (countitem(611) > 0)  // White Fur
        menu "Put a white fur next to the pot", L_GiveFur,
             "Leave it alone", -;
    close;

L_GiveFur:
    mes "You put down the fur, but the cat doesn't seem to take any notice. Maybe there's something else you could do. You pick the fur up again.";
    next;
    close;

L_NeedsWood:
    if (countitem(569) > 0)   // Raw log
        menu "Put a wooden log next to the pot", L_GiveWood,
             "Leave it alone", -;
    close;

L_GiveWood:
    delitem 569, 1;
    set Katze, 4;
    set KatzeBeenOutside, 0;
    mes "You put the wooden log next to the pot. The cat eyes it suspiciously, but remains on her spot.";
    next;
    close;

L_HasWood:
    mes "A wooden log is patiently lying next to it.";
    next;
    close;

L_Finished:
    close;
}
