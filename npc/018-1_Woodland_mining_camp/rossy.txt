// author: Dark_Mage
// reviewed by: Ali-G

018-1.gat,139,61,0	script	Rossy	114,{ 

  if (Rossy_Quest >= 13) goto L_Best;
  if (Rossy_Quest >= 11) goto L_Allergic;
  if (Rossy_Quest >= 8 && Rossy_Quest < 11) goto L_Donet;
  if (Rossy_Quest == 7) goto L_BT;
  if (Rossy_Quest == 6) goto L_Check2;
  if (Rossy_Quest == 5) goto L_Let;
  if (Rossy_Quest >= 2 && Rossy_Quest < 5) goto L_Done;
  if (Rossy_Quest == 1) goto L_Check;

  mes "[Rossy]";
  mes "The young girl looks at you in tears.";
  menu
    "Please stop crying and tell me what's wrong.", -,
    "I don't like people who cry. Bye.", L_No;
  next;
  mes "[Rossy]";
  mes "\"Those sea slimes! They ate my delicious sweets!\"";
  if (baselevel < 50) goto L_LowLevel;
  menu
    "Oh, I presume that you want some. Would you like me to get some sweets for you?", L_Set,
    "Oh well, life isn't fair.", L_No;

L_LowLevel:
  menu
    "I can't do anything about it, sorry.", L_No;

L_No:
  close;

L_Set:
  mes "[Rossy]";
  set Rossy_Quest, 1;
  mes "\"Really? Can you get 20 [Candy]s for me?\"";
  next;
  menu
    "Sure, I'll be back with your candies.", L_Get,
    "No.", L_No;

L_Get:
  mes "[Rossy]";
  mes "\"Please, get 20 [Candy]s for me.\"";
  close;

L_Check:
  if(countitem("Candy") < 20) goto L_Get;
  mes "[Rossy]";
  set Rossy_Quest, 2;
  delitem "Candy", 20;
  getexp 1000, 0;
  mes "\"I can't believe my eyes, you got them! Thanks a lot!\"";
  next;
  mes "\"Could you go and talk to my mother? She is in one of the buildings in the town south from here.\"";
  close;

L_Done:
  mes "[Rossy]";
  mes "\"Thank you "+ strcharinfo(0) +"! Please go and see my mum. She might need help.\"";
  close;

L_Let:
  mes "[Rossy]";
  mes "\"Hello "+ strcharinfo(0) +", nice to see you again. What are you holding behind in your hands? Is that a letter?\"";
  menu
    "Your mother asked me to hand you this letter. She told me it was from your teacher.", L_Let2,
    "Not at all... I need to go now, see you next time.", L_No;
    
L_Let2:
  mes "[Rossy]";
  mes "\"Oh, let me read it...\"";
  next;
  mes "*The little girl grabs the piece of paper and reads it. Her face turns pale.*";
  menu
    "What does it say?", -;
  mes "[Rossy]";
  mes "\"Oh no, I completely forgot! I need to make a potion for my exam but I don't have any of the ingredients required. Could you get them for me?\"";
  menu
    "Of course I can, what do you need?", L_Sett,
    "Well, I'm already really busy right now, sorry. Bye.", L_No;
    
L_Sett:
  mes "[Rossy]";
  set Rossy_Quest, 6;
  mes "\"Great! I need 24 [Cactus Drink]s, 32 [Maggot Slime]s, 50 [Gamboge Herb]s, 43 [Alizarin Herb]s and 30 [Bug Leg]s.\"";
  mes "\"If I don't get them soon enough, I will fail my exam...\"";
  close;
  
L_NE:
  mes "[Rossy]";
  mes "\"Please bring me 24 [Cactus Drink]s, 32 [Maggot Slime]s, 50 [Gamboge Herb]s, 43 [Alizarin Herb]s and 30 [Bug Leg]s.\"";
  mes "\"It seems you miss some of the ingredients I've asked you. Please hurry!\"";
  close;
  
L_Check2:
  if(countitem("CactusDrink") < 24) goto L_NE;
  if(countitem("MaggotSlime") < 32) goto L_NE;
  if(countitem("GambogeHerb") < 50) goto L_NE;
  if(countitem("AlizarinHerb") < 43) goto L_NE;
  if(countitem("BugLeg") < 30) goto L_NE;
  mes "[Rossy]";
  set Rossy_Quest, 7;
  mes "\"Oh, thanks you "+ strcharinfo(0) +" so much! If you don't mind, please bring this potion to my teacher and don't lose it!\"";
  delitem "CactusDrink", 24;
  delitem "MaggotSlime", 32;
  delitem "GambogeHerb", 50;
  delitem "AlizarinHerb", 43;
  delitem "BugLeg", 30;
  getexp 3000, 0;
  next;
  mes "\"His name is David, he is a magician and lives near magic school in Tulimshar.\"";
  mes "\"Please, give it to him!\"";
  close;
  
L_BT:
  mes "[Rossy]";
  mes "\"Great. Now you just should bring it to my teacher. His name is David, he is my magic teacher and lives near the magic school in the north of Tulimshar.\"";
  mes "\"You should take the ferry, that will be faster.\"";
  close;
  
L_Donet:
  mes "[Rossy]";
  mes "\"Cool, I've passed my exam! Hurray!!!\"";
  if (Rossy_Quest == 10 && countitem("redrose") == 15) goto L_RG;
  close;
  
L_RG:
  menu
    "Your mother asked me to bring you these flowers.", -;
  mes "[Rossy]";
  set Rossy_Quest, 11;
  mes "\"Let me see...\"";
  next;
  mes "*Rossy takes the flowers from your hands and suddenly throws them on the ground. She begins sneezing.*";
  delitem "redrose", 15;
  menu
    "I presume that you don't like roses, right?", -;
  mes "[Rossy]";
  mes "\"I am allergic to roses, my mother should already know that!\"";
  close;
  
L_Allergic:
  mes "*Girl sneezes a lot*";
  if (Rossy_Quest == 12 && countitem("redtulip") == 15) goto L_Sorry;
  close;
  
L_Sorry:
  menu
    "Your mother is really sorry about the roses. Here are some beautiful red tulips instead.", L_See;
    
L_See:
  mes "[Rossy]";
  set Rossy_Quest, 13;
  delitem "redtulip", 15;
  mes "\"How nice, thank you "+ strcharinfo(0) +"! Tell my mother that she is the nicest mother in the world. You are the best gentleman I've ever met!\"";
  getexp 1000, 0;
  close;
  
L_Best:
  mes "[Rossy]";
  mes "\"Thank you "+ strcharinfo(0) +", you helped me so much!\"";
  close;
  
}

009-2.gat,123,45,0	script	Olana	108,{ 

  if (Rossy_Quest == 13) goto L_Best;
  if (Rossy_Quest == 12) goto L_Give;
  if (Rossy_Quest == 11) goto L_Allergic;
  if (Rossy_Quest == 10) goto L_BringRose;
  if (Rossy_Quest == 9) goto L_RC;
  if (Rossy_Quest == 8) goto L_RL;
  if (Rossy_Quest >= 6 && Rossy_Quest < 8) goto L_BL;
  if (Rossy_Quest == 5) goto L_Bring;
  if (Rossy_Quest == 4) goto L_Cont;
  if (Rossy_Quest == 3) goto L_See;
  if (Rossy_Quest == 2) goto L_Daughter;

  mes "[Olana]";
  mes "\"Oh my dear daughter went into the forest and didn't come back!\"";
  mes "*Olana starts crying...*";
  close;

L_Daughter:
  mes "[Olana]";
  mes "\"Oh my dear daughter went into the forest and didn't come back!\"";
  mes "*Olana begins to sobb...*";
  next;
  menu
    "Wait, I saw your daughter, she is fine.", L_Help,
    "Wait a bit, I'm sure she'll get back soon.", L_No;

L_No:
  close;

L_Help:
  mes "[Olana]";
  mes "\"Yes? Everything is okay with her? That's good. I'm so relieved.\"";
  next;
  mes "\"Oh by the way, I would need your help myself too. My water supply ran out and I can't go down the stairs anymore, my knees hurt a lot.\"";
  menu
    "Sure I can help you, how much do you need?", L_Set,
    "Sorry, I'm too busy.", L_No;

L_Set:
  mes "[Olana]";
  set Rossy_Quest, 3;
  mes "\"I'd like the size of a [Bottle of Water] and I would need about 5 of those. Please, bring them to me.\"";
  close;

L_Get:
  mes "[Olana]";
  mes "\"Please, get 5 containers of water for me, around the size of a [Bottle of Water].\"";
  close;

L_See:
  if(countitem("bottleofwater") < 5) goto L_Get;
  mes "[Olana]";
  set Rossy_Quest, 4;
  mes "\"You got them for me! Thank you! Here you are... a small reward from me for your precious help!\"";
  delitem "bottleofwater", 5;
  getexp 500, 0;
  set zeny, zeny + 1000;
  goto L_Task;

L_Task:
  mes "[Olana]";
  mes "\"Thank you very much. I have another task for you, would you like to help me again?\"";
  menu
    "Tell me about it.", L_Cont,
    "No, sorry, I'm busy.", L_No;

L_Cont:
  mes "[Olana]";
  mes "\"Can you give this letter to my little daughter please? Her teacher asked me to do it, but I can't walk up to my daughter, my knees...\"";
  menu
    "Of course I can. Hand me the letter, I'll give it to Rossy as soon as I see her.", L_Let,
    "No, sorry, I'm not interested in that sort of task. Hand it yourself.", L_No;
    
L_Let:
  mes "[Olana]";
  set Rossy_Quest, 5;
  mes "\"Please bring it to her as fast as you can, she really needs to know this news!\"";
  close;
  
L_Bring:
  mes "[Olana]";
  mes "\"Bring this letter to my daughter as fast as you can, please!\"";
  close;
  
L_BL:
  mes "[Olana]";
  mes "\"Good, now she remembers it! Please, "+ strcharinfo(0) +", help Rossy, she will tell you what to do.\"";
  close;
  
L_Done:
  mes "[Olana]";
  mes "\"Thanks you "+ strcharinfo(0) +", I and Rossy will never forget what you did for us!\"";
  goto L_Rose;
  
L_Rose:
  mes "[Olana]";
  mes "\"I have another task for you, would you help me?\"";
  menu
    "Tell me what to do.", L_RL,
    "No, I'm busy, maybe later.", L_No;
    
L_RL:
  mes "[Olana]";
  set Rossy_Quest, 9;
  mes "\"Can you bring me 15 [Red Rose]s? I would like to give them to my daughter because she succesfully passed her magic exam!\"";
  menu
    "Ok, I will be back with these.", L_RG;
    
L_RG:
  mes "[Olana]";
  mes "\"Please, bring me 15 [Red Rose]s.\"";
  close;

L_RC:
  if(countitem("redrose") < 15) goto L_RG;
  mes "[Olana]";
  set Rossy_Quest, 10;
  mes "\"Great job! Please hand them to my brilliant daughter, Rossy.\"";
  close;
  
L_BringRose:
  mes "[Olana]";
  mes "\"Now, please hand them to my brilliant daughter, Rossy.\"";
  close;
  
L_Allergic:
  mes "[Olana]";
  set Rossy_Quest, 12;
  mes "\"Did she liked my present?\"";
  menu
    "Well, you should know that your daughter is allergic to roses.", -;
  mes "[Olana]";
  mes "\"How stupid I am! Here, take some of my money and buy [Red Tulip]s instead. The roses are for you sweety.\"";
  set zeny, zeny + 1500;
  next;
  mes "[Olana]";
  mes "\"I want Rossy to be happy and to forgive me for my stupidity.\"";
  menu
    "Ok, I will.", -;
  mes "[Olana]";
  mes "\"And don't forget to tell her that I'm really sorry.\"";
  close;
  
L_Give:
  mes "[Olana]";
  mes "\""+ strcharinfo(0) +", please, get 15 [Red Tulip]s and hand them to Rossy. Don't forget to tell her that I'm really sorry for the mistake I did.\"";
  close;
  
L_Best:
  mes "[Olana]";
  mes "\""+ strcharinfo(0) +", thank you for your help!\"";
  close;
  
}

024-1.gat,70,70,0	script	David	120,{ 
  
  if (Rossy_Quest >= 8) goto L_Done;
  
  mes "[David]";
  mes "\"Learning magic is hard...\"";
  if (Rossy_Quest == 7) goto L_Help;
  close;
  
L_Help:
  menu
    "Do you know Rossy?", L_Set,
    "I can tell, it is hard.", L_No;
    
L_Set:
  mes "[David]";
  set Rossy_Quest, 8;
  mes "\"Yes, I do. I am her teacher. That makes me remember she didn't hand me her potion yet. Time is running out.\"";
  menu
    "A potion? Oh, uhm... never heard about it.", L_No,
    "Right, I have her potion. She asked me to bring it to you for her.", -;
  mes "[David]";
  mes "\"Oh, that's great! Let me look at it...\"";
  mes "\"Great! This potion is perfect, tell Rossy that she passed her exam! And here is a small reward for your help.\"";
  getexp 2000, 0;
  set zeny, zeny + 500;
  close;
  
L_Done:
  mes "[David]";
  mes "\"I am really happy that my best student succesfully passed her exam! Thank you "+ strcharinfo(0) +"!\"";
  close;
}
