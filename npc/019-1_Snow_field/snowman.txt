//

019-1.gat,77,44,0	script	Snowman	129,{
	if( ChristmasQuest == 1) goto L_Done;

	mes "[Snowman]";
	mes "\"Hello there young man.";
	mes "Would you like a Christmas hat?\"";
	next;

L_Menu:
	menu
		"Yes", L_Sure,
		"No", -,
		"What do you need?", L_Need;
	mes "[Snowman]";
	mes "\"Well, thats too bad, but make sure to have the holiday spirit!\"";
	close;

L_Sure:
	mes "[Snowman]";
	mes "\"Hmm, let me see what you have.\"";
	next;
	if(countitem("Candy") < 15) goto L_NoItem;
	if(countitem("ChocolateBar") < 10) goto L_NoItem;
	if(countitem("CactusPotion") < 5) goto L_NoItem;
	getinventorylist;
	if (@inventorylist_count > 99) goto L_TooMany;
	delitem "Candy", 15;
	delitem "ChocolateBar", 10;
	delitem "CactusPotion", 5;
	mes "[Snowman]";
	mes "\"Here you go, enjoy your new hat!\"";
	getitem "SantaHat", 1;
	set ChristmasQuest, 1;
	close;

L_Need:
	mes "[Snowman]";
	mes "\"For this special hat, I need a pint of magic and a little help.";
	mes "Just kidding. I would like to get some food:";
	mes "15 pieces of [Candy]";
	mes "10 [Chocolate bar]s";
	mes "5 [Cactus potion]s\"";
	next;
	goto L_Menu;

L_NoItem:
	mes "[Snowman]";
	mes "\"Well, I am interested in some food and you don't have enought to get a hat.\"";
	close;

L_Done:
	mes "[Snowman]";
	mes "\"Thank you for your help!\"";
	close;

L_TooMany:
	mes "[Snowman]";
	mes "\"It doesn't look like you have room for this. Come back later.\"";
	close;
}
