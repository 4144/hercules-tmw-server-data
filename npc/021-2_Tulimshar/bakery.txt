// The bakery

// The chef
021-2.gat,95,21,0	script	Riskim	148,{
	mes "[Riskim]";
	mes "\"Welcome to our bakery!\"";
	callfunc "KadiyaSubquestConsts";
        if ((@Q_kadiya_status == @Q_STATUS_WANTS_CHOCOCAKE) && (countitem ("MopoxCurePotion") > 0))
        	goto L_lace_chococake;

        if ((@Q_kadiya_status == @Q_STATUS_WANTS_ORANGECUPCAKE) && (countitem ("MopoxCurePotion") > 0))
        	goto L_lace_cupcake;

	close;

L_lace_chococake:
        next;

        menu
		"Thanks!  Let me have a look around...", L_end,
                "Can you put this potion into a [Chocolate Cake]?", -,
		"Goodbye!", L_end;

	mes "[Riskim]";
        mes "As you show Riskim the potion, he leans back, away from it.";
        mes "\"Ooooh, I remember that smell...\"";
        mes "\"Elanore's Mopox potion, right?  She made me drink that, too...\"";
        mes "He chuckles.";
        mes "\"Yes, well, you do want to hide that somehow.\"";
        next;

	mes "[Riskim]";
        mes "\"We'll need extra chocolate for that one.  If you can give me ten pieces of chocolate and that potion, plus 400 GP, I will bake you a special [Chocolate Cake] that hides the taste.\"";
        next;

        menu
		"I will get them for you.", L_end,
                "Here you are!", -,
                "Maybe later.", L_end;

        if (countitem("ChocolateBar") < 10)
		goto L_lacking_ingredients;
        if (countitem("MopoxCurePotion") < 1)
		goto L_lacking_ingredients;
        if (zeny < 400)
		goto L_lacking_zeny;
        delitem "MopoxCurePotion", 1;
        delitem "ChocolateBar", 10;
        getitem "LacedChocolateCake", 1;
        set zeny, zeny - 400;

	mes "[Riskim]";
        mes "\"Please have a seat!\"";
        mes "You watch as Riskim melts the chocolate and stirs in the potion, then glazes a cake with the mixture.";
        mes "\"It's a bit more chocolatey than your average chocolate cake, of course...\"";
        next;

	mes "[Riskim]";
        mes "Riskim hands you the cake.";
        mes "\"Here you are.  I hope that this works.\"";
        close;

L_lacking_zeny:
	mes "[Riskim]";
        mes "\"I'm sorry, but I have to charge 400 GP.\"";
        close;

L_lacking_ingredients:
	mes "[Riskim]";
        mes "\"Sorry, but I need ten [Chocolate Bar]s AND your cure potion for this.  Please come back later when you have everything.\"";
        close;

L_lace_cupcake:
        next;

        menu
		"Thanks!  Let me have a look around...", L_end,
                "Can you put this potion into an [Orange Cupcake]?", -,
		"Goodbye!", L_end;

	mes "[Riskim]";
        mes "\"[Orange Cupcake]s?  Oh dear... I'm sorry, but I have a very bad orange allergy.\"";
        next;
	mes "[Riskim]";
        mes "\"Perhaps the Chef at Dimond's Cove can help you?  We get all our cupcakes from him.\"";
        next;

L_end:
        close;

}

// Server?
021-2.gat,82,23,0	script	Sara	108,{
	mes "[Sara]";
	mes "\"Hello!\"";
	close;
}

// The cashier
021-2.gat,89,21,0	shop	Drabur	112,WhiteCake :-1,ChocolateCake :-1,OrangeCake :-1,AppleCake :-1,Cake :-1,CherryCake :-1,OrangeCupcake :-3,Milk :-5,XmasCake :-1

// Stockroom boy
021-2.gat,133,21,0	script	Iormo	160,{
	mes "[Iormo]";
	mes "\"This stock room can get messy.\"";
	close;
}

// An old couple eating at the bakery
021-2.gat,71,22,0	script	Rynoh	159,{
	mes "[Rynoh]";
	mes "\"The cakes may be expensive, but they sure are delicious!\"";
	close;
}

021-2.gat,73,22,0	script	Ontas	154,{
	mes "[Ontas]";
	mes "\"We come here every week.  This place has some of the best bread I've ever eaten!\"";
	close;
}
