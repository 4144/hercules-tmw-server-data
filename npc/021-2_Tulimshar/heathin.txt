//Items for NPC are incomplete.  Makes "simple ring" which can be crafted into a useful ring by Inya and terranite armor.

021-2.gat,29,55,0	script	Heathin	147,{
	
	if (HEATHIN_QUEST == 14) && (baselevel >= 80) goto L_Heathin_Terranite_Arrows_Trade;
	if (HEATHIN_QUEST == 13) && (baselevel >= 80) goto L_Heathin_Terranite_Arrows_Ready;
	if (HEATHIN_QUEST == 12) && (baselevel >= 80) goto L_Heathin_Terranite_Arrows_Forge;
	if (HEATHIN_QUEST == 11) && (baselevel >= 80) goto L_Heathin_Terranite_Arrows;
	if (HEATHIN_QUEST == 10) && (baselevel >= 80) goto L_Heathin_Terranite_Chest_Armor_Ready;
	if (HEATHIN_QUEST == 9) && (baselevel >= 80) goto L_Heathin_Terranite_Chest_Armor_Forge;
	if (HEATHIN_QUEST == 8) && (baselevel >= 80) goto L_Heathin_Terranite_Chest_Armor;
	if (HEATHIN_QUEST == 7) && (baselevel >= 80) goto L_Heathin_Terranite_Legs_Ready;
	if (HEATHIN_QUEST == 6) && (baselevel >= 80) goto L_Heathin_Terranite_Legs_Forge;
	if (HEATHIN_QUEST == 5) && (baselevel >= 80) goto L_Heathin_Terranite_Legs;
	if (HEATHIN_QUEST == 4) && (baselevel >= 80) goto L_Heathin_Terranite_Hood_Ready;
	if (HEATHIN_QUEST == 3) && (baselevel >= 80) goto L_Heathin_Terranite_Hood_Forge;
	if (HEATHIN_QUEST == 2) && (baselevel >= 80) goto L_Heathin_Terranite_Hood;
	if (HEATHIN_QUEST == 2) && (baselevel >= 60)goto L_Heathin_Ring_Thanks;
	if (HEATHIN_QUEST == 1) && (baselevel >= 60) goto L_Heathin_Ring_Award;
	if (baselevel >= 60) goto L_Heathin_Start;

	mes "[Heathin]";
	mes "\"Hello friend.  I am Heathin, a smith of exotic metals. \"";
	close;

L_Heathin_Start:
	mes "[Heathin]";
	mes "\"Hello friend.  I am Heathin, a smith of exotic metals. \"";
	next;
	mes "\"By the order of the Wizard's Council, I've been commissioned to forge gold ingots from gold pieces.  I need to collect as many gold pieces as I can manage, forge them and give the ingots to the Council.  However, I am but a simple smith and can't do this effort by myself.\"";
	next;
	mes "\"Forging gold ingots is not just a simple matter of gold falling from the sky.  I need individuals that can gather not just the gold, but also help collect coal to heat my forge.\"";
	next;
	mes "\"Of course it would make little sense for me to gather gold pieces, just to pass them back out, but I am willing to create an award for any who would put an effort in towards completing my goal.\"";
	next;
	mes "\"Perhaps you would be interested in helping me out?  Would you be willing to help me gather coal and gold to fulfill my commission?  I'm asking any and all willing souls to get me 50 coal and 100,000 gold pieces as that would put a significant dint in my goal.\"";
	menu
		"Sure, I'll help.", L_Heathin_Help,
		"I'd rather keep my gold for my own purposes, thank you very much", L_Heathin_No,
		"Wait, what kind of award are we talking about?", L_Heathin_Interested;

L_Heathin_Help:
	set HEATHIN_QUEST, 1;
	mes "[Heathin]";
	mes "\"Excellent! I'm always glad some one can be of service not just to me, but to Tulimshar.  Please get me 50 coal and 100,000 gold pieces and you will be compensated for your efforts.\"";
	close;

L_Heathin_No:
	mes "[Heathin]";
	mes "\"That is too bad.  If you change your mind, I'll still be here attempting to get my commission done.\"";
	close;

L_Heathin_Interested:
	mes "[Heathin]";
	mes "\"Well, when I am crafting these ingots, there are always stray metals left over.  Surely I could forge them into ingots again, but I'd rather ensure that my commission be done.  I am willing to forge a simple ring for you, which could be made more beautiful by a skilled jeweler.\"";
	next;
	mes "\"What you do with the ring after I'm done forging it is your own business, but it could come in handy.\"";
	next;
	mes "\"Does this sound like something you might be interested in?\"";
	menu
		"Sure, I'll help.", L_Heathin_Help,
		"I'd rather keep my gold.", L_Heathin_No,
		"Can you repeat that again?", L_Heathin_Interested;
	close;

L_Heathin_Ring_Award:
	if ( (zeny < 100000) | (countitem ("Coal") < 50) ) goto L_Heathin_Ring_Wait;
	set HEATHIN_QUEST, 2;
	set zeny, zeny - 100000;
	delitem "Coal", 50;
	getitem "RingSimple", 1;
	mes "[Heathin]";
	mes "\"You brought the coal and gold pieces.  Most excellent!  As promised, I have crafted a ring for you.  Hopefully you will find it useful.\"";
	next;
	mes "\"Time to smelt these gold pieces into ingots.  The Council will be pleased.  Thank you for your efforts!\"";
	close;

L_Heathin_Ring_Wait:
	mes "[Heathin]";
	mes "\"Hello again, making golden ingots has got me pretty busy.  I sure hope I can fulfil my commission soon so I can move on to bigger and better things.\"";
	next;
	mes "\"Sure the pay is good, but I'm a far better smith than this.  If you can bring me 100,000 gold pieces and 50 coal, it would surely get me to my goal faster.\"";
	next;
	mes "\"I'll be sure to provide an apt award once you are done.\"";
	close;

L_Heathin_Ring_Thanks:
	mes "[Heathin]";
	mes "\"Thank you again for helping me fulfil my commission.  It has made a signficant dint in what the Council requires of me.  Perhaps I may be of service to you again one day\"";
	close;

L_Heathin_Terranite_Hood:
	mes "[Heathin]";
	mes "\"Hello again friend, I just found out about this great metal, called terranite.\"";
	next;
	mes "\"This metal is almost as strong as steel, but is also very ductile.\"";
	next; 
	mes "\"With some effort, I can make this metal form rings tighter than any chain mail.  It isn't the prettiest, but its uses can be many.\"";
	next;
	mes "\"Unfortunately, it seems to be difficult to gather.  Most that have searched for terranite ore either can't find it or they have never returned.\"";
	next;
	mes "\"I may be an excellent smith, but I'm no combatant.  However, I would enjoy turning this ore into some useful, perhaps some type of armor to help those that may be.\"";
	next;
	mes "\"So what do you say?  Are you interested?\"";
	menu
		"No thank you.  Maybe later", L_Heathin_Terranite_No,
		"Armor you say?  Definitely!", L_Heathin_Terranite_Yes;
	close;

L_Heathin_Terranite_No:
	mes "[Heathin]";
	mes "\"Ah, that is too bad.  Come by again anytime.\"";
	close;

L_Heathin_Terranite_Yes:
	set HEATHIN_QUEST, 3;
	mes "[Heathin]";
	mes "\"Great, this is going to be fun.  Because I haven't had much practice with this armor, I'd like to get started soon.\"";
	next; 
	mes "\"*Thinks for a moment*\"";
	next;
	mes "\"Ah, I know.  I'll make a helm..well more like a hood, since it will woven.\"";
	next;
	mes "\"It will require a great amount of heat, or so the rumor goes.  For this I will need 100 coal to keep my forge burning hot.  I will also need 10 terranite ore to make the armor.  Of course, doing such work doesn't come cheap.  While I may enjoy my work, my time and effort is very valuable.  To compensate my efforts I will need 200,000 gold pieces.  Please come back with the gold, coal and ore and I'll make this hood for you.\"";
	close;

L_Heathin_Terranite_Hood_Forge:
	if ( (zeny < 200000) | (countitem("Coal") < 100) | (countitem("TerraniteOre") < 10) ) goto L_Heathin_Terranite_Hood_Wait;
	set HEATHIN_QUEST, 4;
	delitem "Coal", 100;
	delitem "TerraniteOre", 10;
	set zeny, zeny - 200000;
	mes "[Heathin]";
	mes "\"Great, you brought everything!  Come back later and I'll have the helm ready for you.\"";
	close;

L_Heathin_Terranite_Hood_Wait:
	mes "[Heathin]";
	mes "\"I'm very busy right now.  If you could bring me 10 terranite ore, 100 coal and 200,000 gold pieces, I'll make that terranite hood for you.\"";
	close;

L_Heathin_Terranite_Hood_Ready:
	set HEATHIN_QUEST, 5;
	getitem "TerraniteHelmet", 1;
	mes "[Heathin]";
	mes "\"All done with your terranite hood.  Hope you enjoy it.  I've got some work to wrap up for the Council, but come back later and I might be able to assist you again.\"";
	close;

L_Heathin_Terranite_Legs:
	set HEATHIN_QUEST, 6;
	mes "[Heathin]";
	mes "\"Glad you are back.  Just finished up some more of my commission.  Now I can get back to making terranite armor for you.  This time I'll make some pants, but they need more ore, more coal to heat my forge and of course, more gold for my efforts.\"";
	next;
	mes "\"To make terranite pants, I need 200 coal, 30 terranite ore and 400,000 gold pieces.  The faster you bring this to me, the faster I can have your terranite pants ready.\"";
	close;

L_Heathin_Terranite_Legs_Forge:
	if ( (zeny < 400000) | (countitem("Coal") < 200) | (countitem("TerraniteOre") < 30) ) goto L_Heathin_Terranite_Legs_Wait;
	set HEATHIN_QUEST, 7;
	delitem "Coal", 200;
	delitem "TerraniteOre", 30;
	set zeny, zeny - 400000;
	mes "[Heathin]";
	mes "\"Great, you brought everything!  Come back later and I'll have those terranite pants ready for you.\"";
	close;

L_Heathin_Terranite_Legs_Wait:
	mes "[Heathin]";
	mes "\"I'm very busy right now.  If you could bring me 30 terranite ore, 200 coal and 400,000 gold pieces, I'll make those terranite pants for you.\"";
	close;

L_Heathin_Terranite_Legs_Ready:
	set HEATHIN_QUEST, 8;
	getitem "TerraniteLegsArmor", 1;
	mes "[Heathin]";
	mes "\"All done with your terranite pants.  Hope you enjoy it.  I've got some work to wrap up for the Council.  Come back later and I might be able to assist you again.\"";
	close;
	
L_Heathin_Terranite_Chest_Armor:
	set HEATHIN_QUEST, 9;
	mes "[Heathin]";
	mes "\"Glad you are back.  Just finished up some more of my commission.  Now I can get back to making terranite armor for you.  This time I'll make some terranite chest armor, but they need more ore, more coal to heat my forge and of course, more gold for my efforts.\"";
	next;
	mes "\"To make terranite chest armor, I need 250 coal, 40 terranite ore and 500,000 gold pieces.  The faster you bring this to me, the faster I can have your terranite pants ready.\"";
	close;

L_Heathin_Terranite_Chest_Armor_Forge:
	if ( (zeny < 500000) | (countitem("Coal") < 250) | (countitem("TerraniteOre") < 40) ) goto L_Heathin_Terranite_Chest_Armor_Wait;
	set HEATHIN_QUEST, 10;
	delitem "Coal", 250;
	delitem "TerraniteOre", 40;
	set zeny, zeny - 500000;
	mes "[Heathin]";
	mes "\"Great, you brought everything!  Come back later and I'll have the terranite chest armor ready for you.\"";
	close;

L_Heathin_Terranite_Chest_Armor_Wait:
	mes "[Heathin]";
	mes "\"I'm very busy right now.  If you could bring me 40 terranite ore, 250 coal and 500,000 gold pieces, I'll make that chest armor for you.\"";
	close;

L_Heathin_Terranite_Chest_Armor_Ready:
	set HEATHIN_QUEST, 11;
	getitem "TerraniteChestArmor", 1;
	mes "[Heathin]";
	mes "\"All done with your terranite chest armor.  Hope you enjoy it.  I've got some work to wrap up for the Council, but come back later and I might be able to assist you again.\"";
	close;

L_Heathin_Terranite_Arrows:
	mes "[Heathin]";
	mes "\"Glad you are back.  I've experimented a little with terranite ore to see what else can be done with it.  It seems to have some weapon qualities, but I've only tested it with arrows.  If you'd like, I can make terranite arrows, but as as with the armor, I need coal, terranite ore and payment for my efforts.\"";
	next;
	mes "\"Also, it might be a while until I have time to see what else I can make with terranite other than arrows, the Council is keeping me rather busy with my commission.  So would you like some terranite arrows?\"";
	menu
		"No thank you.", L_Heathin_Terranite_Arrows_No,
		"Sure, I could use some arrows.", L_Heathin_Terranite_Arrows_Yes;
	close;

L_Heathin_Terranite_Arrows_No:
	mes "[Heathin]";
	mes "\"Well, if you change your mind, I'm not going anywhere.\"";
	close;

L_Heathin_Terranite_Arrows_Yes:
	set HEATHIN_QUEST, 12;
	mes "[Heathin]";
	mes "\"Good, good.  I'll begin crafting these arrows as soon as you come back.  However, I don't just make small handfuls because it isn't worth my time and effort to have my concentration interrupted for small tasks.  I'll make 1,000 terranite arrows per request, but I'll need 10 terranite ore, 20 coal and 10,000 gold pieces to begin.\"";
	next;
	mes "\"See you soon!\"";
	close;

L_Heathin_Terranite_Arrows_Forge:
	if ( (zeny < 10000) | (countitem("Coal") < 20) | (countitem("TerraniteOre") < 10) ) goto L_Heathin_Terranite_Arrows_Wait;
	set HEATHIN_QUEST, 13;
	delitem "Coal", 20;
	delitem "TerraniteOre", 10;
	set zeny, zeny - 10000;
	mes "[Heathin]";
	mes "\"All right! You got everything I asked for.  I'll start working on them right away.\"";
	close;

L_Heathin_Terranite_Arrows_Wait:
	mes "[Heathin]";
	mes "\"If you want terranite arrows, please get me 10 terranite ore, 20 coal and 10,000 gold pieces.\"";
	close;

L_Heathin_Terranite_Arrows_Ready:
	getitem "TerraniteArrow", 1000;
	set HEATHIN_QUEST, 14;
	mes "[Heathin]";
	mes "\"All done.  Here you go.  Hope you like them.  I am still working on my commission, but if you ever need anymore terranite arrows, just come on by with 20 coal, 10 terranite ore and 10,000 gold pieces and I'll make another 1,000 arrows for you.  I have enough terranite ore in reserve that you can just trade me my requirements to keep my resources up.  Thank you for all your efforts.\"";
	close;

L_Heathin_Terranite_Arrows_Trade:
	mes "[Heathin]";
	mes "\"Hi there.  If you have 20 coal, 10 terranite ore and 10,000 gold pieces I'll trade you 1,000 terranite arrows.  Would you like some terranite arrows?\"";
	menu
		"No.", L_Heathin_Terranite_Arrows_Trade_No,
		"Yes.", L_Heathin_Terranite_Arrows_Trade_Yes;
	close;

L_Heathin_Terranite_Arrows_Trade_No:
	mes "[Heathin]";
	mes "\"Come back again if you'd like to get terranite arrows.\"";
	close;

L_Heathin_Terranite_Arrows_Trade_Yes:
	if ( (zeny < 10000) | (countitem("Coal") < 20) | (countitem("TerraniteOre") < 10) ) goto L_Heathin_Terranite_Arrows_Trade_Wait;
	delitem "Coal", 20;
	delitem "TerraniteOre", 10;
	set zeny, zeny - 10000;
	getitem "TerraniteArrow", 1000;
	mes "[Heathin]";
	mes "\"Here you go.  Come back again if you want more terranite arrows\"";
	close;

L_Heathin_Terranite_Arrows_Trade_Wait:
	mes "[Heathin]";
	mes "\"Doesn't seem like you have enough for me to trade you.  Please come back with 20 coal, 10 terranite ore and 10,000 gold pieces if you want another 1,000 terranite arrows.\"";
	close;

}
