025-1.gat,80,88,0	script	Rossy	114,{ 

  if (FLAGS & FLAG_ROSSI_COMPLETED) goto L_FinalEnd;
  if (Rossy_Quest == 17 || Rossy_Quest == 18) goto L_End;
  if (Rossy_Quest >= 14 && Rossy_Quest <= 16) goto L_Julia;
  if (Rossy_Quest >= 13) goto L_Best;
  if (Rossy_Quest >= 11) goto L_Allergic;
  if (Rossy_Quest >= 8 && Rossy_Quest < 11) goto L_Donet;
  if (Rossy_Quest == 7) goto L_BT;
  if (Rossy_Quest == 6) goto L_Check2;
  if (Rossy_Quest == 5) goto L_Let;
  if (Rossy_Quest >= 2 && Rossy_Quest < 5) goto L_Done;
  if (Rossy_Quest == 1) goto L_Check;

  mes "[Rossy]";
  mes "*The young girl looks at you in tears.*";
  menu
    "Please stop crying and tell me what's wrong.", -,
    "I don't like people who cry. Bye.", L_No;
  next;
  mes "[Rossy]";
  mes "\"I dropped a basket of fruits on the ground and these evil snails ate them all!\"";
  if (baselevel < 50) goto L_LowLevel;
  menu
    "Oh, I presume that you want some. Would you like me to get some fruits for you?", L_Set,
    "Oh well, life isn't fair.", L_No;

L_LowLevel:
  menu
    "I can't do anything about it, sorry.", L_No;

L_No:
  close;

L_Set:
  mes "[Rossy]";
  mes "\"That would be great! Can you get 10 Cherries for me? These are the fruits I like best.\"";
  next;
  menu
    "Sure, I'll be back with your cherries.", L_Get,
    "No, get some yourself.", L_No;

L_Get:
  mes "[Rossy]";
  set Rossy_Quest, 1;
  mes "\"Please, get 10 Cherries for me.\"";
  close;

L_Check:
  if(countitem("Cherry") < 10) goto L_Get;
  mes "[Rossy]";
  set Rossy_Quest, 2;
  getexp 2000, 0;
  mes "\"I can't believe my eyes, you got them all! Please give them to my mother when she will ask for them.\"";
  next;
  menu
    "No problem, I'm in a hurry, bye.", -,
    "Certainly, I'm always here to help, do you need anything else?", L_Done;
  mes "[Rossy]";
  mes "\"I might need your help later. Bye.\"";

L_Done:
  mes "[Rossy]";
  mes "\"My mother might need help. She is in one of the buildings in the town north east from here.\"";
  close;

L_Let:
  mes "[Rossy]";
  mes "\"Hello "+ strcharinfo(0) +", nice to see you again. Hey, what are you hiding behind your back? Is that a... letter?\"";
  next;
  menu
    "Your mother asked me to hand you this letter. She told me it was from your teacher.", L_Let2,
    "Not at all... I need to go now, see you next time.", L_No;
    
L_Let2:
  mes "[Rossy]";
  mes "*The little girl grabs the piece of paper and reads it with an authomatic gesture. Her face turns pale.*";
  menu
    "What does it say?", -;
  mes "[Rossy]";
  mes "\"Oh no, I completely forgot! I need to make a potion but don't have any of the ingredients required. Could you get them for me?\"";
  next;
  menu
    "Of course I can, what do you need?", L_Sett,
    "Well, I'm already really busy right now, sorry. Bye.", L_No;
    
L_Sett:
  mes "[Rossy]";
  set Rossy_Quest, 6;
  mes "\"I need 24 [Cactus Drink]s, 32 [Maggot Slime]s, 50 [Gamboge Herb]s, 43 [Alizarin Herb]s and 30 [Bug Leg]s.\"";
  next;
  mes "\"Please collect all of that and come back to see me. I know the recipe by heart.\"";
  close;
  
L_NE:
  mes "[Rossy]";
  mes "\"Please bring me 24 [Cactus Drink]s, 32 [Maggot Slime]s, 50 [Gamboge Herb]s, 43 [Alizarin Herb]s and 30 [Bug Leg]s.\"";
  next;
  mes "\"It seems you miss some of the ingredients I've asked you. Please hurry!\"";
  close;
  
L_Check2:
  if(countitem("CactusDrink") < 24) goto L_NE;
  if(countitem("MaggotSlime") < 32) goto L_NE;
  if(countitem("GambogeHerb") < 50) goto L_NE;
  if(countitem("AlizarinHerb") < 43) goto L_NE;
  if(countitem("BugLeg") < 30) goto L_NE;
  mes "[Rossy]";
  set Rossy_Quest, 7;
  mes "\"Oh, thank you "+ strcharinfo(0) +" so much! If you don't mind, please bring this potion to my teacher and don't lose it!\"";
  delitem "CactusDrink", 24;
  delitem "MaggotSlime", 32;
  delitem "GambogeHerb", 50;
  delitem "AlizarinHerb", 43;
  delitem "BugLeg", 30;
  getexp 3000, 0;
  next;
  mes "\"His name is David, he is a very young magician and lives near the magic school in the north of Tulimshar.\"";
  next;
  mes "\"Please, give it to him as soon as you can!\"";
  close;
  
L_BT:
  mes "[Rossy]";
  mes "\"Great. Now you just should bring it to my teacher. His name is David, he is a very young magician and lives near the magic school in the north of Tulimshar.\"";
  next;
  mes "\"You should take the ferry, that'll be faster than swimming!\"";
  close;
  
L_Donet:
  if (Rossy_Quest == 10 && countitem("redrose") >= 15) goto L_RG;
  menu
    "David took your potion and he tested it in front of me. It perfectly worked!", -;
  mes "[Rossy]";
  mes "\"Hurray! You might want to tell that to my mother now.\"";
  close;
  
L_RG:
  menu
    "Your mother asked me to bring you these flowers because you succesfully made your potion.", -;
  mes "[Rossy]";
  set Rossy_Quest, 11;
  mes "\"How nice!\"";
  next;
  mes "*Rossy takes the flowers from your hands and suddenly throws them on the ground. She begins sneezing.*";
  delitem "redrose", 15;
  menu
    "I presume that you don't like roses, right?", -;
  mes "[Rossy]";
  mes "\"I am allergic to roses, my mother should already know that!\"";
  close;
  
L_Allergic:
  mes "*Rossy sneezes a lot*";
  if (Rossy_Quest == 12 && countitem("redtulip") >= 15) goto L_Sorry;
  close;
  
L_Sorry:
  menu
    "Your mother is sorry about the roses. Here are some beautiful red tulips instead.", L_See;
    
L_See:
  mes "[Rossy]";
  set Rossy_Quest, 13;
  delitem "redtulip", 15;
  mes "\"That's better! Go and tell my mother that she is the nicest mother in the world.\"";
  next;
  mes "\"Come back and see me afterwards.\"";
  getexp 2500, 0;
  close;
  
L_Best:
  mes "[Rossy]";
  set Rossy_Quest, 14;
  mes "\"Thank you "+ strcharinfo(0) +", you helped me so much!\"";
  next;
  mes "\"I now need to find my sister before my mum gets worried... Please find her!\"";
  close;

L_Julia:
  mes "[Rossy]";
  mes "\"I saw my twin, Julia! She was forced to enter a cave not far from here. I heard her scream. Please enter that cave and save her now!\"";
  close;

L_End:
  mes "[Rossy]";
  mes "\"I saw my twin, Julia! She was forced to enter a cave not far from here. I heard her scream. Did you saved her yet?\"";
  menu
    "She'll be back soon, trust me, I saved her.", L_Found,
    "I didn't saw her yet, sorry.", L_No;

L_Found:
  mes "[Rossy]";
  if (Rossy_Quest == 17) set Rossy_Quest, 18;
  if (Rossy_Quest == 18) set Rossy_Quest, 19;
  mes "\"Oh "+ strcharinfo(0) +", thank you very much! Take this, you deserve it!\"";
  next;
  mes "\"*Rossy hands you a huge bag full of coins.*\"";
  getexp 5000, 0;
  set zeny, zeny + 5000;
  if (Rossy_Quest == 19) goto L_Clear;
  close;

L_Clear:
  set Rossy_Quest, 0;
  set cavefights, 0;
  set FLAGS, FLAGS | FLAG_ROSSI_COMPLETED;
  close;


L_FinalEnd:
  mes "[Rossy]";
  mes "\"Julia and I are playing hide and seek right now, it is funny in the forest!\"";
  mes "*The young girl laughs quietly.*";
  close;

}
