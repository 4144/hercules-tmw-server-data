// Cemetary Caretaker
// TODO: add dialog/code for when first letter is delivered
//       Airlia supports receiving an infinate number of letters

027-2.gat,24,25,0	script	Caretaker	159,{
	if (QUEST_Graveyard_Caretaker == 1) goto L_Return_Dialog;
	if (QUEST_Graveyard_Caretaker == 2) goto L_Has_Letter;
	if (QUEST_Graveyard_Caretaker == 3) goto L_Delivered_Letter;
	if (QUEST_Graveyard_Caretaker == 4) goto L_Has_No_New_Letter;
	if (QUEST_Graveyard_Caretaker == 5) goto L_Has_New_Letter;

	mes "[Caretaker]";
	mes "\"...Another adventurer comes to slay the already dead.\"";
	next;
	menu
		"And what are you?", L_Who,
		"Thats right, so move and let me kill that demon behind you.", -;
	close;

L_Who:
	mes "[Caretaker]";
	mes "\"I was once the caretaker to these cemetery grounds, but look at what they have become.";
	mes "I now care only for my wife. I watch over her... make sure that nobody ever does her harm.\"";
	next;

	menu
		"You guard her corpse day and night?", -;

	mes "[Caretaker]";
	mes "\"I am bound to this place by the very same spell that was cast to keep the undead from leaving. Until the day that this cemetery is cured of its curse, I remain within these gates.\"";
	set QUEST_Graveyard_Caretaker, 1;
	close;

L_Return_Dialog:
	mes "[Caretaker]";
	mes "\"I am somewhat fortunate that adventurers like yourself frequent this place... despite your intentions.";
	mes "Perhaps you can do me a favor?\"";
	next;

	menu
		"What is the favor?", L_Favor,
		"I'm sure I can, for a price.", L_Favor,
		"Sorry, I am busy.", -;
	close;

L_Favor:
	mes "[Caretaker]";
	mes "\"I have not seen my daughter since I was cursed to stay in this cemetery 25 years ago.";
	mes "We communicate now through letters. Would you please take this to her?";
	mes "I am sure she will compensate you for your trouble.\"";
	next;

	menu
		"I will take it to her.", L_Receive,
		"I will help you. Where can I find your daughter?", L_Receive,
		"Sorry, but I am too busy.", -;
	close;

L_Receive:
	set QUEST_Graveyard_Caretaker, 2;
	mes "You receive the letter and put it in a special place so it doesn't get lost among your inventory.";
	if (@menu == 2) goto L_Location;
	close;

L_Location:
	next;

L_Location_Real:
	mes "[Caretaker]";
	mes "\"Her name is Airlia. She works at the city hall in Hurnscald. Thank you for your help.\"";
	close;

L_Has_Letter:
	mes "[Caretaker]";
	mes "\"Have you found my daughter?\"";
	next;

	menu
		"Not Yet.", -,
		"I'll get there when I get there.", -,
		"Where did you say I could find her?", L_Location_Real;
	close;

L_Delivered_Letter:
	// TODO: Thank player for delivering the initial letter
	close;

L_Has_No_New_Letter:
	// TODO: Give player a new letter; add time limit
	close;

L_Has_New_Letter:
	// TODO: The player has to deliver the current letter first
	close;

OnTimer3000:
	monster "027-2.gat", 29, 21, "Caretaker's Wife", 1044, 1, "Caretaker::OnWifeDeath";
	end;

OnWifeDeath:
	mapannounce "027-2.gat", "NO! YOU KILLED MY WIFE!", 0;
	gmcommand strcharinfo(0) + " : @l I killed the caretaker's wife!";
	gmcommand strcharinfo(0) + " : @ban +5m " + strcharinfo(0);
	setnpctimer 0;
	end;

OnInit:
	initnpctimer;
	end;
}
