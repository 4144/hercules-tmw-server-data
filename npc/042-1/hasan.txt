042-1.gat,111,68,0	script	Hasan	189,2,2{

	set @toll, 10000;
	set @x, getx(0);
	set @y, gety(0);

	if ($@scorp) goto L_Fear;
	if (FLAGS & FLAG_TUTORIAL_DONE) goto L_Tut_Done;
	if ( (@y < 64)&&(@x < 110) || (@x < 107) ) goto L_Cliff_Up;
	if (hasan == 4) goto L_Thank;
	if (hasan == 3) goto L_Trick;
	if (hasan > 0) goto L_Toll;

	mes "[Unfriendly Guy]";
	mes "\"Hey! You can't pass here.\"";
	next;
	menu
		"Why not?",-,
		"Who says that?",-;
	mes "[Unfriendly Guy]";
	mes "\"Me, Hasan the mighty, decided to put a toll on this way.\"";
	next;
L_Toll:
	mes "[Hasan]";
	mes "\"Give me " + @toll + "GP, and I may let you pass.\"";
	if (hasan == 0)
		set hasan, 1;
	next;
	if (zeny >= @toll)
		menu
			"Ok, here you go.",L_Pay,
			"What? I don't have so much money!",L_NoPay,
			"We'll see if you can stop me!",L_Fight;
	menu
		"What? I don't have so much money!",L_NoPay,
		"We'll see if you can stop me!",L_Fight;
	close;

L_NoPay:
	mes "[Hasan]";
	mes "\"That's not my problem. You have to stay here then.\"";
	close;

L_Fight:
	mes "He laughs derisively.";
	mes "[Hasan]";
	mes "\"Do you really think you can beat me?\"";
	mes "He pulls out a nasty looking dagger and stabs it in your direction.";
	set hp, 1;
	mes "That hurt! You begin to think searching for a different solution might be healthier.";
	close;

L_Pay:
	if (zeny < @toll)
		goto L_Cheat;
	set zeny, zeny - @toll;
	mes "He grins gloatingly.";
	mes "[Hasan]";
	mes "\"Very well. You may pass.\"";
	set hasan, 0;
	set kaan, 0;
	set sorfina, 0;
	set tanisha, 0;
	set maggot, 0;
	set FLAGS, FLAGS | FLAG_TUTORIAL_DONE;
	close;

L_Cheat:
	mes "[Hasan]";
	mes "\"Don't dare to cheat on me!\"";
	close;

L_Trick:
	mes "[Hasan]";
	mes "\"Are you going to pay now? " + @toll + "GP, and I may let you pass.\"";
	next;
	menu
		"I don't have so much money!",L_NoPay,
		"No. (Scratch your head.)",-;
	mes "While you're scratching your head, you see Kaan coming nearer to the cliff above you.";
	close2;
	monster "042-1.gat",110,70,"Scorpion",1003,1, "Hasan::onScorpionDeath";
	set $@scorp, 1;
	end;

L_Thank:
	mes "[Hasan]";
	mes "\"You - you saved me!";
	mes "I tried to get your money and you saved me!\"";
	next;
	mes "\"Hey, you're a really cool person.";
	mes "I mean, how you finished that ghastly scorpion!";
	mes "Amazing!\"";
	next;
	mes "\"I tell you something: Cool persons are allowed to pass for free.";
	mes "And you can my Sharp Knife.\"";
	getitem "SharpKnife", 1;
	next;
	set hasan, 0;
	set kaan, 0;
	set sorfina, 0;
	set tanisha, 0;
	set maggot, 0;
	set FLAGS, FLAGS | FLAG_TUTORIAL_DONE;
	mes "\"You want to go to the bazaar, right? Shall I bring you there?\"";
	next;
	menu
		"Sure, thanks.",-,
		"I'll try to find the way myself.",L_Explore,
		"Could you just explain me the way?",L_Directions;
	warp "001-1.gat", 44, 40;
	close;

L_Explore:
	mes "[Hasan]";
	mes "\"Alright. Take care!\"";
	close;

L_Directions:
	mes "[Hasan]";
	mes "\"Ok, when you leave here follow the road past the docks until you reach the old city gates.\"";
	next;
	mes "\"Pass through them and at the crossway, just go west and then south until you reach some other gateway.\"";
	next;
	mes "\"Behind it there is the bazaar of Tulimshar.\"";
	close;

L_Fear:
	mes "[Hasan]";
	mes "\"Sc-sc-sco-scorpion! Pl-please k-kill it.\"";
	mes "He seems close to tears.";
	close;

L_Tut_Done:
	mes "[Hasan]";
	if (baselevel >= 40)
		mes "\"Welcome to the suburbs of Tulimshar. I wonder what an experienced adventurer like you is doing here.\"";
	if (baselevel < 40)
		mes "\"Bah, nothing interesting is happening here.\"";
	close;

L_Distance:
	mes "There is a person in some distance. He doesn't seem to notice you, maybe you should go closer.";
	close;

L_Cliff_Up:
	mes "You see a guy in shaby clothes when you look down the cliff.";
	close;

OnTouch:
	if ((FLAGS & FLAG_TUTORIAL_DONE) || $@scorp)
		end;
	warp "042-1.gat", 112, 64;
	message strcharinfo(0), "Hasan: \"Stop!\"";
	end;

onScorpionDeath:
	set $@scorp, 0;
	if (hasan != 3)
		close;
	set hasan, 4;
	message strcharinfo(0), "You saved Hasan.";
	end;
}

042-1.gat,117,76,0	script	#barrier	127,0,2{
	if (FLAGS & FLAG_TUTORIAL_DONE)
		goto L_Tut;
	warp "042-1.gat", 115, 76;
	if ($@scorp)
		message strcharinfo(0), "Hasan: \"Please help me!\"";
	if (!$@scorp)
		message strcharinfo(0), "Hasan: \"Please wait a moment!\"";
	end;
L_Tut:
	warp "022-1.gat", 23, 38;
	end;
}
