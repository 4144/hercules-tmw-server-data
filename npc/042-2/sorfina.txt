042-2.gat,28,26,0	script	Sorfina	154,{

	if (sorfina == 2) goto L_Again;
	if (sorfina == 1) goto L_Clothes;

	mes "[Old Woman]";
	mes "\"Ah, you're waking up. It seems you collapsed after you left the boat that brought you to Tulimshar.";
	mes "A group of ragtags immediatly took advantage of that and stole all your stuff!\"";
	next;
	mes "\"My little granddaughter Tanisha saw that, but couldn't do something to stop them.";
	mes "She hurried to get someone to help. It was too late to catch the thieves, but at least they brought you here to recover.\"";
	next;
	mes "\"This area of the town is full of shady people, so I don't think there's a chance to get back your stuff.\"";
	mes "She has a sad look on her face, but then she shakes her head and smiles.";
	next;
	mes "[Old Woman]";
	mes "\"I'm glad you're feeling better now. You're not used to the hot weather, right?";
	mes "Are you here to look for some work? Many young people come to our Tulimshar for this reason.\"";
	next;
	mes "\"I can tell you some useful things, if you to hear.\"";
	mes "\"Oh, and by the way my name is Sorfina.\"";
	menu
		"That would be great!",L_Tut,
		"No, thanks. I already know how things are going.",L_SkipTut;

L_Tut:
	mes "[Server]";
	mes "The explanations in this tutorial are based on the official client version 0.5.2";
	mes "If you're using another client, settings and controls may be different.";
	next;
	mes "She smiles at you.";
	mes "[Sorfina]";
	mes "\"A wise decision.\"";
	next;
	mes "\"Ok, I hope you didn't forget how to walk. Just use the arrow keys or click at the place you want to go to.\"";
	next;
	mes "\"And you really should get dressed again. I gave you an old shirt of my son.\"";
	next;
	set sorfina, 1;
L_Naked:
	mes "\"Press F3 or click at the button for your equipment in the bar at the upper right corner to open your inventory.\"";
	next;
	mes "\"Then click on the shirt and press the equip button.\"";
	next;
	mes "\"Talk to me again when you got dressed. You can either click on me, or press N to focus on me and T to talk.\"";
	next;
	mes "\"If you want to talk to other adventurers, it's a bit different. In that case, press Enter, type your message and press Enter again.\"";
	close;

L_Clothes:
	if (getequipid(equip_torso) < 0) goto L_Naked;
	mes "[Sorfina]";
	mes "\"That's much better, don't you think so?\"";
	next;
	mes "\"You know, there are different kind of items. You can also use usable items in your inventory.\"";
	next;
	mes "\"But you might find it more useful to put them on a shortcut. Press F8 to open your shortcut window.\"";
	next;
	mes "\"You can select the item you want to put on a shortcut with your mouse and then click on the position in the shortcut window you want to place it.\"";
	next;
	mes "\"I think this will help you a bit. To get more informations, press the button settings and look at the controls. You can also change them as you like.\"";
	next;
	mes "\"Now you should go to see my granddaughter, she is very worried. She's in the storehouse, that's the building just a few steps southeast from here.\"";
	next;
	mes "\"Oh, and if you're looking for work, you should talk to Aidan when you go to main Tulimshar. They're giving rewards for monster hunting.";
	mes "You can find him near the south gate. Well, good luck.\"";
	set sorfina, 2;
	close;

L_Again:
	mes "[Sorfina]";
	mes "\"Welcome back! Do you have any questions?\"";

	if (hasan)
		menu
			"A guy named Hasan doesn't let me enter main Tulimshar.",L_Hasan,
			"Yes, could you explain again about...",L_Explain,
			"No, thanks.",L_Close;
	menu
		"Yes, could you explain again about...",L_Explain,
		"No, thanks.",L_Close;
	close;

L_Explain:
	menu
		"... walking?",L_Walk,
		"... talking?",L_Talk,
		"... the inventory and items?",L_Items,
		"... shortcuts?",L_Shortcut,
		"... what to do in main Tulimshar?",L_Aidan,
		"... nevermind.",L_Close;
	close;

L_Walk:
	mes "[Sorfina]";
	mes "\"You can use the arrow keys to walk around.";
	mes "Or just click at the place you want to go to.";
	mes "But this only works if that place is reachable and walkable.\"";
	close;

L_Talk:
	mes "[Sorfina]";
	mes "\"If you want to talk to a normal person, you can click at him or her.";
	mes "Or you can press N to focus on the nearest person and then press T to talk.\"";
	next;
	mes "\"If you want to talk to other adventurers, press Enter to open your chat window.";
	mes "Then type your message and press Enter to send.\"";
	next;
	mes "\"You may want to talk privately to another adventuerer sometimes. In that case you can whisper.";
	mes "After pressing Enter, type '/whisper [name] [message]'.";
	mes "Or you can right click on someone and choose the whisper option.\"";
	next;
	mes "\"Press F7 to show and hide your chat window.\"";
	close;

L_Items:
	mes "[Sorfina]";
	mes "\"There are three types of items.";
	mes "They can be Consumables, Equipment, or Miscellaneous\"";
	next;
	mes "\"Consumable items such as Potions, can be used only once.";
	mes "After use, they will disappear from your inventory.\"";
	next;
	mes "\"Equipment items like Armors, Weapons, Accessories";
	mes "can be equipped for fashionable purposes or to raise your status.\"";
	next;
	mes "\"Miscellaneous items such as maggot slime, are used";
	mes "in creating other items, or just to trade and sell.\"";
	next;
	mes "\"Press F3 to open your inventory.";
	mes "When placing your cursor over an item there, you can see a box with some informations about that item.\"";
	next;
	mes "\"To use or equip an item, select it with clicking on it.";
	mes "Then press Use or Equip.\"";
	close;

L_Shortcut:
	mes "[Sorfina]";
	mes "\"Press F8 to open your shortcut window.\"";
	next;
	mes "\"You can select the item you want to put on a shortcut with your mouse and then click on the position in the shortcut window you want to place it.\"";
	next;
	mes "\"Now you can use or equip/unequip it with pressing the number of the shortcut.\"";
	close;

L_Aidan:
	mes "[Sorfina]";
	mes "\"I suggest to go to the bazaar near the south gate.";
	mes "We're in the north of Tulimshar here, so you have to walk across the town.\"";
	next;
	mes "\"When you leave this area to the Southeast, you'll come to the docks.";
	mes "Follow the street to the south to pass the old towen gates.\"";
	next;
	mes "\"Turn West at the next intersection. You'll pass by the gates of Tulimshar's famos magic school there.";
	mes "Just follow the street to the south and you'll reach the bazaar.\"";
	next;
	mes "\"I heard Aidan is looking for brave adventurers, you might want to talk to him.";
	mes "Usually the bazaar is bustling with activity, there are many persons who might know something useful.\"";
	close;

L_Hasan:
	mes "[Sorfina]";
	mes "\"Ah, Hasan. He's causing trouble again?\"";
	mes "She shakes her head.";
	next;
	mes "[Sorfina]";
	mes "\This guy was a plague ever since. TODO: add story\"";
	// some backstory, hasan living here as a child, always making trouble
	// but unfortunately very strong
	// player question: weak point
	// answer: frightened of scropions
	if (hasan == 1)
		set hasan, 2;
	close;

L_SkipTut:
	mes "[Sorfina]";
	mes "\"Really? You seem quite unexperienced. Listening to some advices won't hurt.\"";
	menu
		"Thinking about it twice... please tell me.",L_Tut,
		"Yeah, but thanks for the offer. Bye",-;
	mes "[Sorfina]";
	mes "\"If you think so. Good luck.\"";
	//TODO:set flag
	close;

L_Close:
	close;
}

042-2.gat,45,31,0	script	#DoorOut	127,2,2,{
//TODO: check also flag for having finished the tutorial
	if (sorfina < 2) goto L_Block;
	warp "042-1.gat", 62, 63;
	end;
L_Block:
	message strcharinfo(0), "Sorfina: Wait a moment! You're not ready to leave.";
	end;
}
