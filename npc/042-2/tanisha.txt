//TODO: remove knife from beginning items
042-2.gat,37,90,0	script	Tanisha	114,{

	if (tanisha == 3) goto L_Again;
	if (tanisha == 2) goto L_Stats;
	if (tanisha == 1) goto L_Maggots;

	mes "[Tanisha]";
	mes "\"Hey! You're up again!\"";
	emotion 3;
	next;
	mes "\"Are you feeling better?\"";
	menu
		"Yes, thank you.",-,
		"Yeah, but all my stuff is gone.",-;
	mes "[Tanisha]";
	mes "\"I see. It was really bad luck what happened to you.";
	mes "Hey, I have an idea.\"";
	next;
L_Maggots:
	set tanisha, 1;
	mes "\"There are some maggots eating the goods and I shall get rid of them.";
	mes "But they're so yucky!\"";
	next;
	mes "\"If you kill them, I'll give you my knife.";
	mes "What do you say?\"";
	menu
		"Sure!",-,
		"That's your job.",L_Close;
	mes "[Tanisha]";
	mes "\"Wonderful!\"";
	if ($@TUT_MAGGOT) goto L_Blocked;
	set $@TUT_MAGGOT, 1;
	set $@TUT_COUNTER, 0;
	getitem "knife", 1;
	next;
	mes "\"Ok, listen what to do.";
	mes "Equip the knife and kill the maggots.\"";
	next;
	mes "\"Press A to focus on a monster. With X you can focus and attack.";
	mes "But it also works to click on the monster with your mouse.\"";
	next;
	mes "\"If you press Shift, you can abort your attack.\"";
	next;
	mes "\"Maggots aren't aggressive, so they won't do you harm you until you attacked them first. Take them out one by one.";
	mes "Sometimes dead monsters leave some useful things. You can pick them up with pressing Z or clicking with your mouse.\"";
	close2;
	warp "042-2.gat", 33, 87;
	areamonster "042-2.gat", 31, 85, 35, 89, "", 1002, 5, "Tanisha::onMaggotDeath";
	startnpctimer;
	end;

L_Stats:
	mes "[Tanisha]";
	mes "\"That was great! Like a professional exterminator!\"";
	next;
	mes "\"And it seems you became stronger and more experienced.";
	mes "That means you got some status points which you can use to increase your abilities.\"";
	next;
	mes "\"Press F2 to see your status window. There you can distribute your points on six different properties.\"";
	next;
L_StatsRe:
	mes "\"Strength makes you hit harder and you can carry more weight.\"";
	next;
	mes "\"Agility increases your attack speed and your ability to dodge attacks.\"";
	next;
	mes "\"Vitality gives you more Hit Points and more defense against physical attacks.\"";
	next;
	mes "\"Intelligence is useful for magic attack and defense.\"";
	next;
	mes "\"Dexterity is needed to hit a monster. It's also very important for the damage if you use a ranged weapon.\"";
	next;
	mes "\"Luck gives you the chance to do critical hits and lowers the chance to receive a critical hit from your opponent.\"";
	next;
	mes "\"Think about how you want to play before putting the points.";
	next;
	mes "Also I heard rumors about a strange guy lurking around in the desert south of Tulimshar, who can help you to forget your abilites!\"";
	mes "Tanisha looks at you with wide opened eyes.";
	next;
	mes "[Tanisha]";
	mes "\"But I don't think he'd do that without a service in return.\"";

	if (tanisha == 2) goto L_Advice;
	close;

L_Advice:
	next;
	mes "[Tanisha]";
	mes "\"When you go to main Tulmishar, you could see Bernard. He can make a delicious soup!";
	mes "Good luck!\"";
	emotion 3;
	if (tanisha == 2)
		set tanisha, 3;
	close;

L_Again:
	mes "[Tanisha]";
	mes "\"Hi there! Is everything alright? Do you have questions?\"";

	if (hasan)
		menu
			"A guy named Hasan doesn't let me enter main Tulimshar.",L_Hasan,
			"Yes, could you explain again about...",L_Explain,
			"No, thanks.",L_Close;
	menu
		"Yes, could you explain again about...",L_Explain,
		"No, thanks.",L_Close;
	close;

L_Explain:
	menu
		"... how to fight?",L_Fight,
		"... monsters?",L_Mob,
		"... status points?",L_StatsRe,
		"... what to do in main Tulimshar",L_Advice,
		"... nevermind.",L_Close;

L_Fight:
	mes "[Tanisha]";
	mes "\"Press A to focus on a monster. With X you can focus and attack.";
	mes "But it also works to click on the monster with your mouse.\"";
	next;
	mes "\"If you press Shift, you can abort your attack.\"";
	next;
	mes "\"Sometimes dead monsters leave some useful things. You can pick them up with pressing Z or clicking with your mouse.\"";
	close;

L_Mob:
	mes "[Tanisha]";
	mes "\"I don't know much about monsters, I'm just a little girl. But I know a few things.";
	mes "There a several types of monsters, Aggressive, Neutral, and Assistants.\"";
	next;
	mes "\"Aggressive monsters will attack you as soon as they see you.\"";
	next;
	mes "\"Neutral monsters are peacefully until they get attacked.\"";
	next;
	mes "\"Assistants are monsters who help each other.\"";
	close;

L_Hasan:
	mes "[Tanisha]";
	mes "\"Bah, Hasan. He's a jerk. But he's very strong.\"";
	next;
	mes "\"The only thing he's afraid of are scropions.";
	mes "I'm not sure why, some bad experience when he was a child.";
	mes "That was before I was born.\"";
	if (hasan == 1)
		set hasan, 2;
	close;

L_Blocked:
	mes "[Tanisha]";
	//TODO: make this more plausible
	mes "\"Oh, someone else is already in there. Let's wait a short while.\"";
	close;

OnTimer1000:
	setnpctimer 0;
	if (getareausers("042-2.gat", 29, 85, 36, 90) < 1)
		goto L_CleanUp;
	areatimer "042-2.gat", 29, 85, 36, 90, 10, "Tanisha::onTick";
	end;

L_CleanUp:
	set $@TUT_MAGGOT, 0;
	killmonster "042-2.gat", "Tanisha::onMaggotDeath";
	stopnpctimer;
	setnpctimer 0;
	end;

onTick:
	if (isdead(0)) end;
	if (hp > 20)
		goto L_NoHeal;
	heal MaxHp, 0;
	message strcharinfo(0), "Tanisha: You're doing good!";
L_NoHeal:
	if ($@TUT_COUNTER < 5)
		end;
	warp "042-2.gat", 36, 90;
	set tanisha, 2;
	goto L_CleanUp;
	close;

onMaggotDeath:
	set $@TUT_COUNTER, $@TUT_COUNTER + 1;
	end;

L_Close:
	close;
}
