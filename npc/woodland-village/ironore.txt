new_20-1.gat,185,55,0	script	Nicholas	135,{
mes "[Nicholas]";
mes "Hello there,";
mes "I'm an expert blacksmith.";
mes "If you get me some Iron Ore";
mes "I could make you a very valuable helmet.";
next;
L_M:
menu "I have some Iron Ore!",L_Check,"Where can I get this Iron Ore?",L_Info,"I'm okay, thanks.",L_Pass;
	L_Check:
		mes "[Nicholas]";
		mes "Let me take at look at how much you have...";
		next;
		if(countitem(640)<5) goto NoItem;
		if(countitem(640)<10) goto StageA;
		if(countitem(640)<15) goto StageB;
		goto StageC;
	L_Info:
		mes "[Nicholas]";
		mes "You can find Iron Ore in mines.";
		mes "It is quite hard to come by though.";
		close;
	L_Pass:
		mes "[Nicholas]";
		mes "Oh, okay";
		mes "Come back any time.";
		close;
	StageA:
		mes "[Nicholas]";
		mes "That's just enough for me to make you";
		mes "a winged Knight's Helmet.";
		mes "But it'll cost you 10,000GP";
		mes "and 5 lumps of Iron Ore.";
		next;
		menu "Deal!",L_YesKnight,"I'll pass, thanks.",ComeBack;
	StageB:
		mes "[Nicholas]";
		mes "Ahh, with that much Iron Ore I can";
		mes "make you one of two helmets,";
		mes "for only 10,000GP.";
		mes "";
		mes "Which one do you want?";
		next;
		menu "Crusade Helmet. (10 Iron Ore's)",L_YesCrusade,"Knight's Helmet. (5 Iron Ore's)",L_YesKnight,"I'll pass, thanks.",ComeBack;
	StageC:
		mes "[Nicholas]";
		mes "Excellent, that's enough to make";
		mes "three different types of helmet.";
		mes "My fee is 10,000GP.";
		mes "";
		mes "Which helmet do you want?";
		next;
		menu "Warlord Helmet. (15 Iron Ore's)",L_YesWarlord,"Crusade Helmet. (10 Iron Ore's)",L_YesCrusade,"Knight's Helmet. (5 Iron Ore's)",L_YesKnight,"I'll pass, thanks.",ComeBack;
	NoItem:
		mes "[Nicholas]";
		mes "It appears you don't have enough Iron Ore for me to work with.";
		mes "Please do come back when you have more though.";
		close;
	ComeBack:
		mes "[Nicholas]";
		mes "Come back any time.";
		close;
	NoMoney:
		mes "[Nicholas]";
		mes "Oh dear, it seems you don't have enough money.";
		close;
	L_YesKnight:
		if (zeny < 10000) goto NoMoney;
		set zeny, zeny-10000;
		delitem 640,5;
		getitem 637,1;
		mes "[Nicholas]";
		mes "There you go!";
		mes "";
		mes "Come back any time.";
		close;
	L_YesCrusade:
		if (zeny < 10000) goto NoMoney;
		set zeny, zeny-10000;
		delitem 640,10;
		getitem 639,1;
		mes "[Nicholas]";
		mes "Hope you like it!";
		mes "";
		mes "Come back any time.";
		close;
	L_YesWarlord:
		if (zeny < 10000) goto NoMoney;
		set zeny, zeny-10000;
		delitem 640,15;
		getitem 636,1;
		mes "[Nicholas]";
		mes "Here you go!";
		mes "";
		mes "Come back any time.";
		close;
}