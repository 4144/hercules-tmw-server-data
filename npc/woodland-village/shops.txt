//#################################################################################
//#                                                                               #
//# This script file contains all shops and other NPCs with important functions.  #
//#                                                                               #
//#################################################################################
//Bartender
// sells beer and cake
new_20-1.gat,65,49,0	shop	Barkeeper	112,539:175,513:20,519

//Receptionist
// Offers the player to rest at the inn for 100gp
new_20-1.gat,50,48,0	script	Receptionist	108,{
    if (zeny < 100) goto L_NoMoney;
    mes "[Receptionist]";
    mes "Would you like to rest? It's only 100 gp.";
    next;

    menu "Yes",-,"No",L_No;
        mes "Sleep well!";
        next;

        set zeny,zeny-100;
        heal 10000,10000;
        close;

    L_No:
        mes "See you.";
        close;

    L_NoMoney:
        mes "Would you like to rest? Come back if you have at least 100 gp.";
        close;
}

//Archer Shop
// sells bow, short bow, arrows and iron arrows
new_20-1.gat,97,24,0	shop	Apprentice	120,1199:3,529:5,1200:1000,530:3000

//Potion Shop
// sells cactus drink, cactus potion, iron potion and concentration potion
new_20-1.gat,121,26,0	shop	Witch	103,501:50,502:70,567:500,568:500

//Nurse
// heals player below level 10 for free and tells players about the doctor on the 2nd floor.
new_20-1.gat,147,65,0	script	Nurse	119,{

	mes "[Nurse]";
	mes "Can I help you?";
	next;

	menu "Oooh, these wounds! They hurts so much!",L_Heal,"I don't feel so well, I might be sick.",L_Doctor,"No, I'm fine.",-;
		mes "Then I would ask you to leave. There are people who really need our help.";
		close;

	L_Doctor:
		mes "Then you should better see the doctor. He is usually in his office on the 2nd floor.";
		close;

	L_Heal:
		if (baselevel > 10) goto L_NoHeal;
        mes "Here, let me heal you.";
        next;
        heal 10000,10000;
        close;

    L_NoHeal:
    	mes "I'm sorry but I'm here only to help young people.";
		mes "Your level is already higher than 10.";
		mes "You can get some rest in the inn near here.";
		close;
}


//Blacksmith
// Sells all knives, chainmail armor, light platemail
new_20-1.gat,183,57,0	shop	Blacksmith	135,625:20000,626:50000,1201:25,522:100,521:1000;