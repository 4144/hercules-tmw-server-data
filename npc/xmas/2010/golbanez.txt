//#################################################################################
//#
//#    Golbanez for Christmas 2010
//#
//#    Authors: alastrim, PjotrOrial
//#
//#    reviewed by:
//#
//#    This is Golbanez, he took Santa Claus as a hostage.
//#    Santa will be freed on Dec24, so players need to pass a labyrinth
//#    if the players bring Golbanez stuff, santa will donate some other stuff to the players.
//#    
//#################################################################################

019-1.gat,93,41,0	script	#Golbenez#_M	204,{
	set $Golbenez_Santa_Free, gettime(5);
	
	if (gettime(7) > 2010)  set $Golbenez_Santa_Free, 25;

	if ($Golbenez_Santa_Free >= 24 || gettime(7) > 2010) goto Glb_Inn_Enough;

	mes "[Golbenez]";
	mes "\"Mortal!  I am Golbenez!  I have broken through the barriers of space and time!\"";
	next;
	mes "\"Well to let you know, I have taken Santa Claus, Rudolph and his fellow raindeer hostage.\"";
	next;
	mes "\"This may sound hard to you ... But I need lots of stuff for building the way to place of leisure.";
	next;
	mes "\"So if you want to have donations at Christmas... we should make a deal: Bring me money or presents.";
	next;
	mes "\"Maybe I will release Santa Claus and the raindeer then.\"";
	next;
	mes "\"But do you really expect a promise to let him free on Christmas?\"";
	next;
	mes "\"Uhm well, then you should get away!\"";
	next;
	mes "\"What do you like to give?\"";

	menu
		"I'd like to spend some money.", Glb_money,
		"I'd like to give some yellow presents.", -, 
		"I'd like to give some blue presents.", -,
		"I'd like to give some green presents.", -,
		"I'd like to give some purple presents.", -,
		"Why do you want present boxes?",Glb_whypresents,
		"Can I bring other items?", Glb_otheritems,
		"Nevermind.", Glb_nvm;

	// in this array the items should be filled in
	setarray @menuitem[2], 4021, 516, 538, 515;
	setarray @menuvalue[2], 100000, 20000, 50000, 10000;

	mes "\"How many presents do you want to give?\"";

	input @count;

	if (countitem(@menuitem[@menu]) < @count ) goto Glb_noitems;
	delitem @menuitem[@menu], @count;
	set @Give, (@count * @menuvalue[@menu]);
	//mes ""+@Give+"";
	set @Gave_Presents, 1;
	goto Glb_checkGlAn;

Glb_money:
	input @Give;

	set @Give, @Give;

	if (@Give <= 0) close;
	if (zeny < @Give) goto Glb_Inn_NotEnoughZeny;

Glb_checkGlAn:
	// if a player gave boxes, at least 20 yellows are needed, or lots of others.
	if(@Gave_Presents == 1 && @Give<2000000 )goto Golbenez_pay;
	// if the @Give amount is below 30% of the players money, just let him pay
	if( @Give<100000 || ( 100 * @Give / (zeny+1) )<30 ) goto Golbenez_pay;

	set @glob_an$ , "Golbenez: " + strcharinfo(0) + " accounted to free santa";

	// depending on the absolute amount setup another global announcement.
	if( @Give >10000000 ) goto Golbenez_an_5;
	if( @Give > 5000000 ) goto Golbenez_an_4;
	if( @Give > 1000000 ) goto Golbenez_an_3;
	if( @Give >  720000 ) goto Golbenez_an_2;
	if( @Give >  355000 ) goto Golbenez_an_1;

	goto Golbenez_an;

Golbenez_an_1:
	set @glob_an$, @glob_an$ + " with a great effort.";
	goto Golbenez_an;

Golbenez_an_2:
	set @glob_an$, @glob_an$ + " with a honest contribution.";
	goto Golbenez_an;

Golbenez_an_3:
	set @glob_an$, @glob_an$ + " with a great effort. Such a generous person.";
	goto Golbenez_an;

Golbenez_an_4:
	set @glob_an$, @glob_an$ + " with a remarkable effort. Impressive!";
	goto Golbenez_an;

Golbenez_an_5:
	set @glob_an$, @glob_an$ + " with a huge effort. What a honorable donation!";
	goto Golbenez_an;

Golbenez_an:
	announce @glob_an$, 0;

Golbenez_pay:
	set Golbenez_Inn_Cost, Golbenez_Inn_Cost + @Give;
	set $Golbenez_Inn_Cost, $Golbenez_Inn_Cost + @Give;
	if (@Gave_Presents == 1) close;
	set zeny, zeny - @Give;
	close;

Glb_Inn_NotEnoughZeny:
	mes "[Golbenez]";
	mes "\"You don't have as much as you say you do.  Come back when you can give the gold pieces you say you can.\"";
	close;

Glb_noitems:
	mes "[Golbenez]";
	mes "\"You don't have as many as you say you do.  Come back when you have enough.\"";
	close;

Glb_nvm:
	mes "[Golbenez]";
	mes "\"That's too bad.\"";
	close;

Glb_whypresents:
	mes "[Golbenez]";
	mes "\"The presents contain some sort of spiritual attachment that aids in the creation of dimensional connections to this plane of existence. The green ones contain the stronger spiritual attachment, followed by the blue and then purple present boxes. But the yellow ones, they have amazing spiritual attachment.\"";
	close;

Glb_otheritems:
	mes "[Golbenez]";
	mes "\"No you cannot!\"";
	next;
	mes "\"I will not discuss any further, mortal!\"";
	next;
	mes "\"So bring me the needed stuff and maybe you get some stuff as a reward, little coward!\"";
	close;

Glb_Inn_Enough:
	mes "[Golbenez]";
	mes "\"Ok mortal!\"";
	next;
	mes "\"I have collected enough for now.\"";
	next;
	mes "\"This helps me finish the place of leisure.\"";
	next;
	mes "\"Although I did not promise I let Santa be free again!\"";
	next;
	mes "\"He can be found in some cave around here.\"";
	close;
}


