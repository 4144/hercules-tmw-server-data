// Emote NPC allows for players to learn the emote skill

001-1.gat,30,45,0|script|Entertainer|165,{
    set @EMOTE_SKILL, 1;
    if (getskilllv(@EMOTE_SKILL) > 0) goto L_Has;

    mes "[Entertainer]";
    mes "\"Yes how can I help you?\"";
    menu
        "What are those emotions above your head?", L_Learn,
        "Nothing I guess", -;
    goto L_Close;

L_Learn:
    mes "[Entertainer]";
    mes "\"They are called emotes, I use them to express how I am feeling";
    mes "I can teach you to do them too\"";
    menu
        "Ok sure", L_Learn2,
        "Why would I need to that", -;
    goto L_Close;

L_Learn2:
    setskill @EMOTE_SKILL, 1;
    mes "[Entertainer]";
    mes "\"All you have to do is press alt and a number";
    mes "The number determines what emotion will be showned";
    mes "Some clients will also show an emote shortcut-bar with the F12 button\"";
    goto L_Close;

L_Has:
    mes "[Entertainer]";
    mes "\"The entertainment life is a hard life...\" *sigh*";
    goto L_Close;

L_Close:
    set @EMOTE_SKILL, 0;
    close;

OnTimer3000:
    emotion rand(0,11);
    setnpctimer 0;

OnInit:
    initnpctimer;

}
