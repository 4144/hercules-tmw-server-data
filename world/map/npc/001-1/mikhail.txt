//

001-1.gat,50,46,0|script|Mikhail|120,{
    if (TMW_Quest >= 6) goto L_Done;
    if (TMW_Quest == 5) goto L_Progress;
    if (TMW_Quest == 4) goto L_Start;
    if (TMW_Quest < 4) goto L_Convince_Mikhail_First;

    // If this happens, something is wrong with the above case handling.
    mes "Mikhail looks confused.";
    close;

L_Convince_Mikhail_First:
    mes "[Mikhail]";
    mes "\"Hello...\"";
    mes "";
    mes "It seems that the boy would like to say more, but does not dare to open up to you. Maybe you should help someone else first, so that he sees your intentions are good.";
    close;

L_Start:
    mes "[Mikhail]";
    mes "\"My grandma sent me to get five Maggot Slimes from the maggots in the desert, but they scare me so bad!\"";
    next;
    mes "\"You look like a nice person.  Would you go get them for me?\"";
    menu
        "Of course, I'll go get them for you.", L_accept,
        "I've got other things to do right now.", L_close;
    close;

L_close:
    close;

L_accept:
    mes "[Mikhail]";
    mes "\"Thank you so much!  I'll wait for you here.\"";
    set TMW_Quest, 5;
    close;

L_Progress:
    mes "[Mikhail]";
    mes "\"Did you bring me the five Maggot Slimes i need?\"";
    next;
    menu
        "Yes, here they are, kiddo!", L_try,
        "Not yet, but I'll be back soon.", L_close;
    close;

L_try:
    if countitem("MaggotSlime") >= 5 goto L_get;
    mes "[Mikhail]";
    mes "\"It doesn't look like you have them all...\"";
    close;

L_get:
    delitem "MaggotSlime", 5;
    set TMW_Quest, 6;
    mes "[Mikhail]";
    mes "\"Ooh!  Thank you so much!  I can get back to my grandma now!\"";
    next;
    getexp 100, 0;
    mes "You get 100 xp.";
    close;

L_Done:
    mes "[Mikhail]";
    mes "\"Thanks again for helping me get those Maggot Slimes!\"";
    close;
}
