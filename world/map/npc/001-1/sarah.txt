//

001-1.gat,137,78,0|script|Sarah|106,{
    if (TMW_Quest >= 8) goto L_Done;
    if (TMW_Quest == 7) goto L_Progress;
    if (TMW_Quest == 6) goto L_Start;
    if (TMW_Quest < 6) goto L_Convince_Sarah_First;

    // If this is shown, something is wrong with the above case handling.
    mes "Sarah looks confused.";
    goto L_Close;

L_Convince_Sarah_First:
    mes "[Sarah]";
    mes "\"Boy, am I hungry or what!\"";
    next;
    mes "\"The girl suddenly looks at you suspiciously.\"";
    next;
    mes "\"My mommy says not to talk to strangers!\"";
    goto L_Close;

L_Start:
    set @TEMP, rand(2);
    if(@TEMP == 0) goto L_Opening0;
    if(@TEMP == 1) goto L_Opening1;

L_Opening0:
    mes "[Sarah]";
    mes "\"Cherry Cake is the best!\"";
    next;
    goto L_Ask;

L_Opening1:
    mes "[Sarah]";
    mes "\"Mmm, Cherry Cake...  I love it!\"";
    next;
    goto L_Ask;

L_Ask:
    mes "\"Can you bring me a piece of Cherry Cake?  Pretty please?\"";
    menu
        "Yes.", L_Req0,
        "No.", L_Close;

L_Req0:
    mes "[Sarah]";
    mes "\"Yippee!  Bring me a piece of Cherry Cake, and I'll give you a nice hat!\"";
    next;
    goto L_Set;

L_Set:
    set TMW_Quest,7;
    mes "[Sarah]";
    mes "\"Please bring it to me!\"";
    goto L_Close;

L_Progress:
    if (countitem("CherryCake") < 1) goto L_NotEnough;
    mes "[Sarah]";
    mes "\"Whee!\"";
    next;
    mes "You brought me Cherry Cake!  Here is your new hat, as promised.\"";
    emotion EMOTE_TONGUE;
    getinventorylist;
    if ((@inventorylist_count - (countitem("CherryCake") == 1)) > 99) goto L_TooMany;
    delitem "CherryCake", 1;
    getitem "serfhat", 1;
    set TMW_Quest, 8;
    goto L_Close;

L_NotEnough:
    mes "[Sarah]";
    mes "\"Oh, I'm starving!  Please bring me Cherry Cake!\"";
    goto L_Close;

L_Done:
    mes "[Sarah]";
    mes "\"It was so tasty, I can't eat anything more...  Thank you!\"";
    emotion EMOTE_TONGUE;
    goto L_Close;

L_Close:
    set @TEMP, 0;
    close;

L_TooMany:
    next;
    mes "[Sarah]";
    mes "\"You don't have room for my reward.  I'll wait until you do.\"";
    goto L_Close;
}
