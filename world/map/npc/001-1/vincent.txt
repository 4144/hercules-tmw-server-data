//

001-1.gat,135,42,0|script|Vincent|113,{
    set @hw2011_npc_id, $@hw2011_npc_vincent;
    if (gettime(7) == $@hw2011_year && gettime(6) == 10 && gettime(5) >= $@hw2011_start_day)
        goto L_TrickOrTreat;

L_Begin:
    if (TMW_Quest >= 10) goto L_Done;
    if (TMW_Quest == 9) goto L_Progress;
    if (TMW_Quest == 8) goto L_Start;
    if (TMW_Quest < 8) goto L_Convince_Vincent_First;

    // If this happens, something is wrong with the above code.
    mes "Vincent looks confused.";
    goto L_Close;

L_Convince_Vincent_First:
    mes "[Vincent]";
    mes "\"I'm making an action figure.  I'm almost done with it.\"";
    mes "";
    mes "Vincent seems distracted with his near-completed figurine for now.";
    goto L_Close;

L_Start:
    set @TEMP, rand(4);
    if(@TEMP == 0) goto L_Opening1;
    if(@TEMP == 1) goto L_Opening2;
    if(@TEMP == 2) goto L_Opening3;
    if(@TEMP == 3) goto L_Opening4;

L_Opening1:
    mes "[Vincent]";
    mes "\"I just need 10 more Bug Legs to finish my action figure!\"";
    next;
    goto L_Ask;

L_Opening2:
    mes "[Vincent]";
    mes "\"This maggot action figure is awesome! I just need to attach 10 Bug Legs.\"";
    next;
    goto L_Ask;

L_Opening3:
    mes "[Vincent]";
    mes "\"This is a great action figure! A must have! All I need is a few parts...\"";
    next;
    goto L_Ask;

L_Opening4:
    mes "[Vincent]";
    mes "\"Can you get me 10 Bug Legs? I need them to replace the action figure parts.\"";
    next;
    goto L_Ask;

L_Ask:
    mes "[Vincent]";
    mes "\"Will you help me find 10 Bug Legs?\"";
    next;
    menu
        "Yes", L_Sure,
        "No", -;
    goto L_Close;

L_Sure:
    set TMW_Quest, 9;
    set @TEMP,rand(4);
    if(@TEMP == 0) goto L_Req1;
    if(@TEMP == 1) goto L_Req2;
    if(@TEMP == 2) goto L_Req3;
    if(@TEMP == 3) goto L_Req4;

L_Req1:
    mes "[Vincent]";
    mes "\"Thank you!\"";
    next;
    goto L_Wait;

L_Req2:
    mes "[Vincent]";
    mes "\"I don't know how to thank you enough!\"";
    next;
    goto L_Wait;

L_Req3:
    mes "[Vincent]";
    mes "\"I will thank you when I get them!\"";
    next;
    goto L_Wait;

L_Req4:
    mes "[Vincent]";
    mes "\"I'm sure I will give a small reward. :D\"";
    next;
    goto L_Wait;

L_Wait:
    mes "[Vincent]";
    mes "\"Now please go get me 10 Bug Legs.\"";
    goto L_Close;

L_Progress:
    if(countitem("BugLeg") >= 10) goto L_Have;
    mes "[Vincent]";
    mes "\"Please help me collect 10 Bug Legs!\"";
    goto L_Close;

L_Have:
    mes "[Vincent]";
    mes "\"Excellent! Finally I can complete the model!!\"";
    next;
    if(countitem("BugLeg") < 10) goto L_Progress;
    delitem "BugLeg", 10;
    set Zeny, Zeny + 1000;
    set TMW_Quest, 10;

    mes "[Vincent]";
    mes "\"Here you go, a little of my appreciation!\"";
    mes "";
    mes "[1000 gold]";
    goto L_Close;

L_Done:
    mes "[Vincent]";
    mes "\"Thanks for your help!\"";
    goto L_Close;

L_Close:
    set @TEMP, 0;
    close;

L_TrickOrTreat:
    callfunc "TrickOrTreat2011";
    goto L_Begin;
}
