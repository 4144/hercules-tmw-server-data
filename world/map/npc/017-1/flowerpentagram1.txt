// Author: Jenalya

017-1.gat,95,42,0|script|#FlowerPentagram1|400,{

    if (OrumQuest >= 31) goto L_PlacedFifthFlower;

    if (OrumQuest > 17) goto L_PlacedFirstFlower;
    if (OrumQuest == 17) goto L_FirstFlower;

    message strcharinfo(0), "Something is odd with this place.";
    end;

L_FirstFlower:
    mes "This looks like the place Orum described.";
    menu
        "Place the flower.", -,
        "Leave.", L_Close;

    if (isin("017-1.gat", 94, 41, 96, 43))
        goto L_Place;

    mes "You're too far away.";
    goto L_Close;

L_Place:
//    if (countitem("TODO:name") < 1)
//        goto L_NoFlower;
//    delitem "TODO:name", 1;
    mes "You carefully place the magic flower on the marked spot. You feel some tension in the air around you.";
    set OrumQuest, 18;
    close2;
    areamonster "017-1.gat", 91, 37, 101, 46, "", 1105, 2, "#FlowerPentagram1::OnSquirrelDeath";
    areamonster "017-1.gat", 91, 37, 101, 46, "", 1003, 3, "#FlowerPentagram1::OnScorpionDeath";
    set @value, 15;
    callfunc "QuestSagathaAnnoy";
    set @value, 0;
    end;

OnSquirrelDeath:
    set @mobID, 1105;
    callfunc "MobPoints";
    end;

OnScorpionDeath:
    set @mobID, 1003;
    callfunc "MobPoints";
    end;

L_NoFlower:
    mes "You don't have the flower with you. Where did you put it?";
    mes "If you can't find it again, you'll have to tell Orum.";
    goto L_Close;

L_PlacedFirstFlower:
    mes "This is the place where you put the first of Orum's magical flowers.";
    mes "You still feel a slight glowing of magic power around the place, waiting to be unleashed.";
    goto L_Close;

L_PlacedFifthFlower:
    mes "This is the place where you put the first of Orum's magical flowers.";
    mes "But after the pentagram was finished and the summoning happened, everything is back to normal with this spot.";
    goto L_Close;

L_Close:
    close;
}
