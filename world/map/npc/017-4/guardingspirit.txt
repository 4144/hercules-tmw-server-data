// Author: Jenalya

017-4.gat,173,47,0|script|#Guarding Spirit#_M|204,0,0{

L_Talk:
    if (OrumQuest < 14)
        goto L_Block;
    if (OrumQuest > 14)
        goto L_Chat;

    mes "[Guarding Spirit]";
    mes "\"Hold on! My master doesn't wish to get disturbed! I can't let you enter.\"";
    menu
        "Who is your master?", L_Master,
        "But I have important things I need to discuss with your master!", L_Helped,
        "Oh, so he's finally back?", L_Back,
        "Ok, ok.", L_Close;

L_Back:
    mes "[Guarding Spirit]";
    mes "\"Yeah, finally... He was gone for quite a while, and now he came back with this scary and quainty guy...\"";
    next;
    mes "\"Ehem, I mean. Yes, mortal. He came back into his residence and doesn't wish to interact with those below his dignity. Leave.\"";
    goto L_Close;

L_Master:
    mes "[Guarding Spirit]";
    mes "\"He's a powerful wizard. He can force the elements to obey his will. He talks to demons and higher beings from the astral plane, such as I am.\"";
    next;
    mes "\"Mankind knows him with under the name 'Orum'.\"";
    menu
        "Hey, I helped him out some while ago!", L_Helped,
        "Orum? That guy who feels weary due to some transmution spells?", L_Disrespectful,
        "Then I better respect his wish and leave!", L_Close;

L_Disrespectful:
    mes "[Guarding Spirit]";
    mes "\"WHAT are you saying? Are you making fun of my master?! You'll regret that!\"";
    set Hp, 1 + Hp/5;
    goto L_Close;

L_Helped:
    mes "[Guarding Spirit]";
    mes "\"Really? That sounds unplausible to me...\"";
    next;
    mes "\"Why should my master mess around with such a puny mortal as you are?\"";
    menu
        "I'd like to offer him my humble services...", L_Pass,
        "Listen, he'll get angry if you don't let me pass. ", L_Pass,
        "You're right, I was just bluffing.", L_Close;

L_Pass:
    mes "[Guarding Spirit]";
    mes "\"Mh. Mh... All right, I let you pass.\"";
    next;
    mes "\"Remember to talk to him with befitting respect.\"";
    set OrumQuest, 15;
    warp "017-4.gat", 201, 26;
    goto L_Close;

L_Block:
    mes "[Guarding Spirit]";
    mes "\"Stop, mortal! I can't let you pass!\"";
    menu
        "What? Who are you?", -,
        "Why?", -,
        "Ok...", L_Close;
    mes "[Guarding Spirit]";
    mes "\"My master summoned me for the sole purpose to prevent his refuge being entered during his absence.";
    mes "I'll guard this cave with all my power!\"";
    next;
    mes "\"Because... otherwise he'll punish me. That hurts.\"";
    goto L_Close;

L_Chat:
    mes "[Guarding Spirit]";
    mes "Oh, it's you. You may enter.";
    // TODO: more chat, maybe depending on the quest progress?
    goto L_Close;

L_Close:
    close;

OnTouch:
    if (OrumQuest < 15)
        goto L_Talk;
    warp "017-4.gat", 201, 26;
    end;
}
