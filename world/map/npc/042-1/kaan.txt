// This file is part of the Tutorial
// Author: Jenalya

042-1.gat,84,65,0|script|Kaan|160,
{
    if (FLAGS & FLAG_TUTORIAL_DONE) goto L_Tut_Done;
    if (hasan == 4) goto L_Hasan_Saved;
    if ($@ScorpionFighter == getcharid(3)) goto L_Run;
    if (kaan == 4) goto L_Plan;
    if (kaan == 3) goto L_Trick;
    if (kaan == 2) goto L_Out;
    if (kaan == 1) goto L_Tanisha;

    mes "[Young Man]";
    mes "\"You're the person Tanisha brought here, right?";
    mes "My name is Kaan, I'm her brother.\"";
    next;
    mes "\"She was babbling at me about it all the morning until I sent her to take care of the maggots in the storehouse.\"";
    next;
    mes "Kaan grins mischieviously.";
    mes "[Kaan]";
    mes "\"She hates maggots.\"";
    next;
    mes "\"Maybe you should go and see her...she was really upset about what happened to you.\"";
    set kaan, 1;
    if (tanisha >= 3)
        goto L_Tanisha2;
    goto L_Close;

L_Tanisha2:
    menu
        "I already did and helped her with the maggots.",L_Tanisha3,
        "I talked to her, and she's done with the maggots.",L_Tanisha3;

L_Tanisha:
    mes "[Kaan]";
    mes "\"Did you already talk to Tanisha?\"";
    if (tanisha < 3)
        goto L_Close;
    next;
    menu
        "Yes, I helped her with the maggots.",-,
        "Yeah, and she's done with the maggots.",-;
L_Tanisha3:
    mes "[Kaan]";
    mes "\"Ah, that is good news.";
    mes "You know, we have a lot of problems with bugs here.\"";
    next;
    mes "\"If we don't watch out the maggots will eat all of our stocks.";
    mes "And the desert is crawling with scorpions.";
    mes "They also come here and you need to watch out not to step on them.\"";
    next;
    mes "\"But I took care of that problem.\"";
    mes "He looks very proud.";
    next;
    mes "\"I built the fence down south of here to keep them out.\"";
    next;
    menu
        "Impressive! Just a bit... skewed.",-,
        "Wow, it looks very - ahm, unconventional.",-;
    mes "[Kaan]";
    mes "\"Well, it keeps the scorpions out. That's all that matters.\"";
    next;
    mes "\"Are you going to visit Tulimshar?";
    mes "The bazaar there is a wondeful place! Traders, entertainers, adventurers - it's very exciting.\"";
    next;
    mes "\"Ah, I forgot. Without some money the bazaar is less fun I suppose.";
    mes "Here, take this.\"";
    set Zeny, Zeny + 15;
    next;
    mes "\"And Ian the trainer is hanging around there often. You should talk to him, he's a smart guy.\"";
    set kaan, 2;
    if (hasan)
        menu
            "A guy named Hasan won't let me enter Tulimshar.",L_Hasan,
            "Thanks for the advice.",L_Close;

L_Out:
    mes "[Kaan]";
    mes "\"Hello! How are you? Did you already visit Tulimshar?\"";
    next;
    if (hasan)
        menu
            "A guy named Hasan won't let me enter Tulimshar.",L_Hasan,
            "Not yet.",L_Close;
    menu
        "Not yet.",L_Close;

L_Hasan:
    mes "[Kaan]";
    mes "\"Hasan! That's crazy. He becomes more cheeky with every day.";
    mes "I'd like to help you, but - I won't risk an open confrontation with him.\"";
    next;
    mes "\"Sometimes he just freaks out and then he's dangerous.";
    mes "He might listen to one of the adult men, but they're all off to work in Tulimshar during the day.\"";
    next;
    mes "\"We should think about some kind of trick. Ah, if only I knew his weak point.\"";
    set kaan, 3;
    next;

L_Trick:
    mes "[Kaan]";
    mes "\"Do you have any idea what Hasan's weakness could be?";
    mes "I wonder if Sorfina or Tanisha know something about him that could help us.\"";
    if (hasan < 2)
        goto L_Close;
    next;
    menu
        "He's afraid of scorpions.",-,
        "No idea.",L_Close;
    mes "[Kaan]";
    mes "\"Scorpions?! Right! How could I forget?";
    mes "When he was a child, he nearly died because he fell into a scorpion nest.\"";
    next;

L_Explain:
    mes "\"Ok, then here's the plan:";
    mes "I will catch one of the scorpions that's coming to the fence from the desert.\"";
    next;
    mes "\"You'll go to Hasan and give me a sign when you're ready.";
    mes "Lets say the sign is that you're scratching your head.";
    mes "I'll watch you from this point.\"";
    next;
    mes "\"When you give me the sign, I'll throw the scorpion down on him.";
    mes "While he's distracted with the scorpion, you can pass.\"";
    next;
    mes "\"How's that? We can start as soon as you're ready.\"";
    if (kaan == 3)
        set kaan, 4;
    set hasan, 3;
    goto L_Close;

L_Plan:
    mes "[Kaan]";
    mes "\"I'm ready. Just give me the sign when you're down there.";
    mes "Or do you want me to explain the plan again?\"";
    next;
    menu
        "Yes please.",L_Explain,
        "No, everything's alright.",L_Close;

L_Hasan_Saved:
    mes "[Kaan]";
    mes "\"I told you to pass when he's distracted, not to fight the scorpion!";
    mes "But maybe Hasan is thankful now?\"";
    next;
    mes "\"Why don't you talk to him again and see what he says.\"";
    goto L_Close;

L_Tut_Done:
    mes "[Kaan]";
    mes "\"Hi there. I hope you enjoy your time in Tulimshar.";
    mes "Hasan can be a real troublemaker, but it seems like he has calmed down for now.\"";
    goto L_Close;

L_Run:
    mes "[Kaan]";
    mes "\"He's distracted! What are you waiting for?\"";
    goto L_Close;

L_Close:
    close;
}
