// Uncle Bunny
008-1.gat,45,23,0|script|Uncle Bunny|336,{

    if ((BaseLevel < 30) || (gettimetick(2)-TUT_var < 3*7*86400)) // player must be created at least three weeks ago
        goto L_TooYoung;

    if (!(gettime(6) == 3 && gettime(5) >= $@easter2012_start_day) && !(gettime(6) == 4 && gettime(5) < $@easter2012_reward_start_day))
        goto L_Easter2012Over;

    if (QUEST_Easter12 == 1) goto L_Return_Death_Threat;
    if (QUEST_Easter12 > 1 && QUEST_Easter12 < 5) goto L_Return_Frozenbeard;
    if (QUEST_Easter12 == 5) goto L_Return_Easter_Bunny;
    if (QUEST_Easter12 > 5) goto L_Easter2012Over;

    mes "You see a big, funny-looking bunny wearing a bowler hat. He gazes at the horizon with a very sad face.";
    menu
        "Hey you, can I help you with anything?", -,
        "Leave the bunny alone with his problems.", L_Close;

    mes "[Uncle Bunny]";
    mes "\"Oh, hi! Sorry, I didn't notice you were here. I am so worried about my nephew and his current situation that I can't barely keep a normal conversation.\"";
    menu
        "Sorry, but I don't want to be involved in your family troubles right now.", L_Close,
        "Who is your nephew? What happened to him?", -;

    if (QUEST_Easter11 != 14)
        goto L_Dont_Know_Easter11;

    mes "[Uncle Bunny]";
    mes "\"I believe you know him already. He is the Easter Bunny, the one you helped last year with a very nice Easter basket. But this year he can't even work on his things...\"";
    goto L_Ask_For_Help;

L_Dont_Know_Easter11:
    mes "[Uncle Bunny]";
    mes "\"I don't think you know him personally, but he is very famous. He is the Easter Bunny. He is widely known for his beautiful baskets full of candies and chocolates but also about his memory problems.\"";

L_Ask_For_Help:
    next;
    mes "\"The problem is that some pirates kidnapped him and now they are asking for a lot of expensive things to set him free! They just sent me a terrible message! They are asking for items just to keep him alive!\"";
    next;
    mes "\"These criminals are probably inspired by that evil man who kidnapped Santa a couple years ago... They must be thinking they are going to get away with the ransom, just like the other guy. But we have to do something about it! Would you help me with this problem?\"";
    next;
    menu
        "Of course I will. What do you need me to do?", -,
        "Ehh... I am really busy right now, maybe later.", L_Close;

    set QUEST_Easter12, 1;
    mes "[Uncle Bunny]";
    mes "\"Great! We need to be fast. They said they need " + $@easter2012_pearl_amount + " pearls, " + $@easter2012_orangecupcake_amount + " orange cupcakes and " + $@easter2012_pinkpetal_amount + " pink petals to keep him alive. Please, bring it to me as fast as you can.\"";
    goto L_Close;

L_Return_Death_Threat:
    mes "[Uncle Bunny]";
    mes "\"You are back!\"";
    next;
    if (countitem("Pearl") >= $@easter2012_pearl_amount && countitem("OrangeCupcake") >= $@easter2012_orangecupcake_amount && countitem("PinkPetal") >= $@easter2012_pinkpetal_amount)
        goto L_Return_Death_Threat_With;

    mes "\"Oh, but you don't have the items... Remember, we need " + $@easter2012_pearl_amount + " pearls, " + $@easter2012_orangecupcake_amount + " orange cupcakes and " + $@easter2012_pinkpetal_amount + " pink petals to keep him alive.";
    mes "Please hurry, the Easter Bunny's life depends on you!\"";
    goto L_Close;

L_Return_Death_Threat_With:
    mes "\"And you have the items! Thank you! Now we have time to think about something.\"";
    next;
    mes "\"While you were collecting the items, I heard there is a pirate called Frozenbeard who is not one of the kidnappers. Maybe he knows something about them or where they are hiding my nephew.\"";
    next;
    if (countitem("Pearl") < $@easter2012_pearl_amount || countitem("OrangeCupcake") < $@easter2012_orangecupcake_amount || countitem("PinkPetal") < $@easter2012_pinkpetal_amount)
        goto L_Noitems;
    delitem "Pearl", $@easter2012_pearl_amount;
    delitem "OrangeCupcake", $@easter2012_orangecupcake_amount;
    delitem "PinkPetal", $@easter2012_pinkpetal_amount;
    set QUEST_Easter12, 2;
    getitem "BlackPearl", 1;
    mes "\"Please, any information you get from him will be extremely valuable! And here, take this black pearl. I heard he is a greedy man, so you can offer this item in case he shows any sign of fear or decides to put a price to his knowledge.\"";
    goto L_Close;

L_Return_Frozenbeard:
    mes "[Uncle Bunny]";
    mes "\"So, what did you find out?\"";
    next;
    if (QUEST_Easter12 == 2)
        menu
            "Nothing yet, I still need to find this man.", L_Return_Frozenbeard_Close;

    if (QUEST_Easter12 == 3)
        menu
            "I found Frozenbeard, but he only referred me to another person.", L_Return_Frozenbeard_George;

    if (QUEST_Easter12 == 4)
        menu
            "I know where he is. I am going to save him!", L_Return_Frozenbeard_Save;

L_Return_Frozenbeard_Close:
    mes "[Uncle Bunny]";
    mes "\"Please, don't waste too much time or this is going to be a tragedy!\"";
    goto L_Close;

L_Return_Frozenbeard_George:
    mes "[Uncle Bunny]";
    mes "\"Then go after this person!\"";
    goto L_Close;

L_Return_Frozenbeard_Save:
    mes "[Uncle Bunny]";
    mes "\"Oh, Really? Are you sure about this information? If I were younger I would go with you and take my nephew from the hands of the pirates myself, but I am old and weak... All I can do is wish you good luck!\"";
    goto L_Close;

L_Return_Easter_Bunny:
    mes "[Uncle Bunny]";
    mes "\"" + strcharinfo(0) + "! You won't believe it! My nephew escaped! He just showed up as if nothing had happened... He told me he woke up in a strange house and realized he didn't know how he got there. Can you believe that?\"";
    next;
    mes "\"My nephew forgot he had been kidnapped! He thought it was just one of his blackouts... So, since the door was locked, he just broke the lock and escaped! I guess the pirates were not counting on such a bold move from his part, hahah!\"";
    next;
    mes "\"But don't worry, I told him all about the pirates and your efforts to save him! Now he wants to talk to you, he says you deserve a special gift. He went to the Tulimshar workshop where he gets some help with the Easter issues and is waiting for you there.\"";
    goto L_Close;

L_Easter2012Over:
    mes "[Uncle Bunny]";
    mes "\"Ah, I am so glad my nephew was found! Now the nightmare is over! I hope his memory gets better one day.\"";
    goto L_Close;

L_Noitems:
    mes "[Uncle Bunny]";
    mes "\"Wait, I could swear you had the items, but they disappeared. Please, come back with the items!\"";
    goto L_Close;

L_TooYoung:
    mes "[Uncle Bunny]";
    mes "\"Hello, young one. This world is harsh, so be careful out there.\"";
    goto L_Close;

L_Close:
    close;
}
