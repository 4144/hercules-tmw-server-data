function|script|EvilObelisk|{
    mes "[Evil Obelisk]";
    mes "(A mystical aura surrounds this stone.  It seems to crave money.)";
    next;

    menu
        "Pay it 200,000 gold", L_JACKO,
        "Pay it 100,000 gold", L_GRAVEYARD1,
        "Pay it 75,000 gold", L_GRAVEYARD2,
        "Pay it 50,000 gold", L_SKULL,
        "Pay it 25,000 gold", L_SNAKE,
        "Don't pay it anything.", -;
    close;
    return;

L_JACKO:
    if (Zeny < 200000)
        goto L_NotEnough;
    set Zeny, Zeny - 200000;
    set @mob_id, 1022;
    set @mob_count, rand(2) + 1;
    goto L_Summon;

L_GRAVEYARD1:
    if (Zeny < 100000)
        goto L_NotEnough;
    set @temp, rand(2);
    if(@temp == 0)
        set @mob_id, 1036;  // Zombie
    if(@temp == 1)
        set @mob_id, 1045;  // Fallen
    set @mob_count, rand(2) + 1;
    set Zeny, Zeny - 100000;
    goto L_Summon;

L_GRAVEYARD2:
    if (Zeny < 75000)
        goto L_NotEnough;
    set @temp, rand(2);
    if(@temp == 0)
        set @mob_id, 1044;  // Lady Skelly
    if(@temp == 1)
        set @mob_id, 1043;  // Normal Skelly
    set @mob_count, rand(2) + 1;
    set Zeny, Zeny - 75000;
    goto L_Summon;

L_SKULL:
    if (Zeny < 50000)
        goto L_NotEnough;
    set @temp, rand(2);
    if(@temp == 0)
        set @mob_id, 1024;  // Poison
    if(@temp == 1)
        set @mob_id, 1023;  // Fire
    set @mob_count, rand(4) + 1;
    set Zeny, Zeny - 50000;
    goto L_Summon;

L_SNAKE:
    if (Zeny < 25000)
        goto L_NotEnough;
    set @temp, rand(4);
    if(@temp == 0)
        set @mob_id, 1034;  // Grass
    if(@temp == 1)
        set @mob_id, 1026;  // Mnt.
    if(@temp == 2)
        set @mob_id, 1010;  // Normal
    if(@temp == 3)
        set @mob_id, 1021;  // Cave
    set @mob_count, rand(4) + 1;
    set Zeny, Zeny - 25000;
    goto L_Summon;

L_Summon:
    monster @map$, @x, @y, "Evil", @mob_id, @mob_count;
    close;
    return;

L_NotEnough:
    mes "You don't have that much money";
    close;
    return;
}
