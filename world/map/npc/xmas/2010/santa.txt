//#################################################################################
//#
//#    Santa for Christmas 2010
//#
//#    Authors: alastrim, PjotrOrial
//#
//#    reviewed by:
//#
//#    This is Santa Claus.
//#    He was taken as hostage by golbanez, and now that he is free, he would like to
//#    give a reward for those who helped him.
//#
//#################################################################################


031-3.gat,122,51,0	script	Santa	105,{

        if (Sex) set @honorific$, "young hero";
        if (Sex) set @gender$, "boy";
        if (!Sex) set @honorific$, "young heroine";
        if (!Sex) set @gender$, "girl";
        if (Xmas2010 == 2) goto L_Returning;
        if (Xmas2010 == 3) goto L_Stocking_Prize;
        if (Xmas2010 == 4) goto L_Event_Done;

L_start:
        mes "The old man's eyes open wide as he sees you.";
        next;
        mes "[Santa Claus]";
        mes "\"Ho-Ho-ho! It is so good to see you here " +@honorific$+ "!\"";
        next;
        menu
                "Im " + strcharinfo(0) + " and I am here to save you from Golbenez.",-;
        mes "\"I cant believe it... are you really " +strcharinfo(0)+ "? I remember all the comments that... 'Thing' was making about building his way to a 'Leisure Place' and how your donations would really help him.\"";
        next;
        mes "\"What is his name again? Erh... Golbinos? Galvanis? Ho-hum... All I know is that it was keeping me hostage in this cold labyrinth. And you proved yourself a good " +@gender$+ " by helping me with all those donations!\"";
        next;
        if (gettime(6) != 12 || gettime(5) >= 25) goto L_Skip_Dialogue;
        mes "\"Well, I am really late! In a few hours I have to start my usual present giving routine... But not before I give you a special gift for all you did.\"";
        next;
L_Skip_Dialogue:
        mes "\"Let me see... From what I can remember, Galvenio said that the only way someone could enter this cave is by giving him at least 5 Million Gold Pieces! It means you really did a good job!\"";
        next;

L_Calculate_Prizes:
        getinventorylist;
        if (@inventorylist_count == 100) goto L_full;
        setarray @prizes, 1216, 629, 630, 634, 1214, 633, 628, 627, 621;
        mes "\"You are so generous! I believe I can give you one of the hats from this bag. I know you like these silly hats. Yes, Santa knows this kind of thing... Ho-Ho-Ho! Put your hand here and pick one hat... No, you cant look, it MUST be a surprise...\"";
        getitem @prizes[rand(8)], 1;
        set Xmas2010, 2;
        set Golbenez_Inn_Cost, 0;
        next;

L_Returning:
        mes "[Santa Claus]";
        mes "\"I hope you liked your gift.\"";
        next;
        menu
                "Yes Santa, you are the best!",L_Close,
                "Ehh, it is not what I expected.",L_Bad,
                "I loved it!",-;

L_Stocking_Prize:
        set Xmas2010, 3;
        getinventorylist;
        if (@inventorylist_count == 100) goto L_full;
        mes "[Santa Claus]";
        mes "\"Good to know you liked it! Now I would like to give you a personal item. It is not a hat, but something that will always make you remember how grateful I am.\"";
        next;
	menu
		"Wonderful! Santa, You are great! Is it another hat?",-;
	next;
        mes "\"No, not a hat. Here, take this. I have been using it for the last three months... It is my favorite and it means a lot to me.\"";
        getitem "RedChristmasStocking", 1;
        set Xmas2010, 4;
        close2;
        percentheal -20, 0;
        message strcharinfo(0), "You suddenly get dizzy... Something is not right with you. Maybe you shouldnt have put the Red Stocking so close to your face!";
        end;

L_full:
        mes "[Santa Claus]";
        mes "\"Your backpack seems to be full. You should get rid of some unused stuff before you get my gifts, or maybe try one super resistant red bag, my personal favorite! Hoh-hoh!\"";
        close;

L_Close:
        mes "[Santa Claus]";
        mes "\"Ho, Ho... Ok, see you later " +@honorific$+ "!\"";
        close;

L_Bad:
        mes "[Santa Claus]";
        mes "\"Hmm... and I was thinking you were a good " +@gender$+ "! Dont be ungrateful and next time, think before you get a gift.\"";
        close;

L_Event_Done:
        mes "[Santa Claus]";
        mes "\"Hello again " +strcharinfo(0)+ "! I cant chat with you right now, because we are really late for Christmas, but, when it is over, you will always be welcome in my house for a hot chocolate.\"";
        close;

}
