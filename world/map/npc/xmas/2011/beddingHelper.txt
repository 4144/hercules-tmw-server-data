// This file is part of Christmas Event 2011
// author: Jenalya
// Osmo asks for some White Fur which is used for bedding some presents in the boxes
// can be sabotaged by given Cloth instead
// 00 not talked
// 10 asked for Bedding
// 01 done in good way
// 11 done in bad way

030-2.gat,37,77,0|script|Osmo|328,{

    if (!(gettime(7) == $@xmas2011_year && gettime(6) == 12 && gettime(5) >= $@xmas2011_start_day)
        && !(gettime(7) == ($@xmas2011_year + 1) && gettime(6) == 1 && gettime(5) <= $@xmas2011_reward_end_day))
        goto L_NoEvent;
    if ((gettime(7) == $@xmas2011_year && gettime(6) == 12 && gettime(5) >= $@xmas2011_reward_start_day)
        || (gettime(7) == ($@xmas2011_year + 1) && gettime(6) == 1 && gettime(5) <= $@xmas2011_reward_end_day))
        goto L_RewardTime;

    set @bedding_amount, 10;

    if (xmas11 & $@xmas11_BeddingDone) goto L_Done;
    if (xmas11 & $@xmas11_Bedding) goto L_Bedding;
    if (xmas11 & $@xmas11_talkedToChief) goto L_Helping;

    mes "[Busy Helper]";
    mes "\"What? Uh, excuse me, I have some work to do.\"";
    goto L_Close;

L_Helping:
    mes "[Osmo]";
    mes "\"You came in at just the right moment. I was informed that our bedding material for fragile presents is nearly depleted.";
    mes "Could you bring me " + @bedding_amount + " White Furs?\"";
    next;
    mes "[Osmo]";
    mes "\"Take care that it's real White Fur.";
    mes "Other materials tend to go out of place in the present box, leaving the fragile content unprotected from any pushes.\"";
    set xmas11, xmas11 | $@xmas11_Bedding;
    goto L_Close;

L_Bedding:
    mes "[Osmo]";
    mes "\"Ah, you're back. Do you have the " + @bedding_amount + " White Fur needed as bedding for fragile presents?\"";
    if ((countitem("WhiteFur") >= @bedding_amount) && (countitem("CottonCloth") >= @bedding_amount))
        menu
            "Yeah, here it is. (Give the White Fur.)",L_Fur,
            "Sure, here it is. (Give the Cotton Cloth.)",L_Cloth,
            "No, I'm still working on that.",L_Close;
    if (countitem("WhiteFur") >= @bedding_amount)
        menu
            "Yeah, here it is. (Give the White Fur.)",L_Fur,
            "No, I'm still working on that.",L_Close;
    if (countitem("CottonCloth") >= @bedding_amount)
        menu
            "Sure, here it is. (Give the Cotton Cloth.)",L_Cloth,
            "No, I'm still working on that.",L_Close;
    goto L_Close;

L_Fur:
    if (countitem("WhiteFur") < @bedding_amount)
        goto L_NoItems;
    delitem "WhiteFur", @bedding_amount;
    set xmas11, xmas11 & ~$@xmas11_Bedding;
    set xmas11, xmas11 | $@xmas11_BeddingDone;
    mes "[Osmo]";
    mes "\"Wonderful! Thanks for your effort.\"";
    goto L_Close;

L_Cloth:
    if (countitem("CottonCloth") < @bedding_amount)
        goto L_NoItems;
    delitem "CottonCloth", @bedding_amount;
    set xmas11, xmas11 | $@xmas11_BeddingDone;
    mes "He doesn't notice that you're giving him Cotton Cloth.";
    mes "[Osmo]";
    mes "\"Wonderful! Thanks for your effort.\"";
    goto L_Close;

L_Done:
    mes "[Osmo]";
    if (xmas11 & $@xmas11_Bedding)
        mes "\"The Fur you brought me isn't working very well.\"";
    if (!(xmas11 & $@xmas11_Bedding))
        mes "\"Thanks for helping out with the bedding material.\"";
    goto L_Close;

L_NoEvent:
    mes "[Osmo]";
    mes "\"How did you come in? At this time of the year the door should be locked!\"";
    warp "030-1.gat", 99, 55;
    goto L_Close;

L_RewardTime:
    mes "[Osmo]";
    mes "\"I have to say that I'm satisfied with our work, even if it was very stressful this year.\"";
    goto L_Close;

L_NoItems:
    mes "[Osmo]";
    mes "\"Where do you have it? Is this a joke?\"";
    goto L_Close;

L_Close:
    set @bedding_amount, 0;
    close;
}
