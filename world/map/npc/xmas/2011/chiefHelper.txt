// This file is part of Christmas Event 2011
// author: Jenalya
// Warrick is the Chief of Santa's helpers, starts the quest

030-2.gat,87,77,0|script|Chief Warrick|329,{

    if (!(gettime(7) == $@xmas2011_year && gettime(6) == 12 && gettime(5) >= $@xmas2011_start_day)
        && !(gettime(7) == ($@xmas2011_year + 1) && gettime(6) == 1 && gettime(5) <= $@xmas2011_reward_end_day))
        goto L_NoEvent;
    if ((gettime(7) == $@xmas2011_year && gettime(6) == 12 && gettime(5) >= $@xmas2011_reward_start_day)
        || (gettime(7) == ($@xmas2011_year + 1) && gettime(6) == 1 && gettime(5) <= $@xmas2011_reward_end_day))
        goto L_RewardTime;
    if ((BaseLevel < 30) || (gettimetick(2)-TUT_var < 3*7*86400)) // player must be created at least three weeks ago
        goto L_NoHelp;

    if (xmas11 & $@xmas11_talkedToChief) goto L_Helping;

    mes "[Important looking Person]";
    mes "\"Welcome to the home of Santa! My name is Warrick, I'm the chief of Santa's helpers.";
    mes "As you certainly know Christmas is near. Therefore we're very very busy.\"";
    next;
    mes "\"For this reason I have to ask you to leave and come back some at some more convenient time.\"";
    menu
        "But I want to see Santa!",-,
        "I'm so excited! This is really Santa's house?",-,
        "This is not very friendly. Isn't Christmas about love and happiness and all?",-;
    mes "Warrick sighs.";
    mes "[Chief Warrick]";
    mes "\"Usually we'd be more obliging. But this year - our preparations for Christmas -";
    mes "Everything is going wrong, we're missing ingredients for the Christmas candy and material for the toys.";
    mes "The lists about the good and bad kids got mixed up, everything is in utter confusion.";
    mes "And to make matters worse some of the reinboos are missing!\"";
    next;
    mes "\"Oh, please excuse me. I didn't mean to moan.";
    mes "However, as you can see we have to deal with some serious problems here.";
    mes "So I'd appreciate if you could just leave and not interfere.\"";
    menu
        "But I can help!",-;
    mes "He hesitates.";
    mes "[Chief Warrick]";
    mes "\"I'm not sure...\"";
    menu
        "Really, I can do a lot of things.",-;
    mes "[Chief Warrick]";
    mes "\"Well, alright. I suppose it can't get worse than it already is.";
    mes "Go to the helpers all over the house and ask them what is needed.";
    mes "This will show you're hired as a co-helper.\"";
    mes "He stabs your chest with his finger and leaves a blinking mark.";
    set xmas11, xmas11 | $@xmas11_talkedToChief;
    goto L_Close;

L_Helping:
    callfunc "xmas2011points";
    mes "[Chief Warrick]";
    if ((@sabotagepoints + @helppoints) < $@xmas2011_totalpoints)
        mes "\"I hope you're doing well.\"";
    if ((@sabotagepoints + @helppoints) == $@xmas2011_totalpoints)
        mes "\"It seems we're catching up with the tasks to do, thanks to your help.\"";
    goto L_Close;

L_NoEvent:
    mes "[Chief Warrick]";
    mes "\"How did you come in? At this time of the year the door should be locked!\"";
    warp "030-1.gat", 99, 55;
    goto L_Close;

L_RewardTime:
    mes "Warrick looks really tired.";
    mes "[Chief Warrick]";
    mes "\"I'm so glad it's over for this year.";
    mes "Did you already talk to Santa?\"";
    goto L_Close;

L_NoHelp:
    mes "[Chief Warrick]";
    mes "\"Hello young one. Welcome to Santa's house.";
    mes "Have a look around and enjoy your visit, but please don't interrupt the busy helpers.\"";
    mes "He smiles at you.";
    goto L_Close;

L_Close:
    set @sabotagepoints, 0;
    set @helppoints, 0;
    close;
}

//030-2.gat,85,33,0|script|Debug|105,{
//    callfunc "xmas2011debug";
//}
