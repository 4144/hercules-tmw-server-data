// This file is part of Christmas Event 2011
// author: Jenalya
// Grombadil gives the task to search for the lost Reinboos after the player talked to the Chief helper
// Home Reinboos: Reimeles, Leimeres
// Lost Reinboos: Meireles, Meileres, Reilemes, Leiremes
// coding for a lost reinboo
// b1b0
// 00 : haven't talked yet
// 01 : talked
// 10 : Good
// 11 : Bad

030-1.gat,103,54,0|script|Grombadil|105,{

    if (!(gettime(7) == $@xmas2011_year && gettime(6) == 12 && gettime(5) >= $@xmas2011_start_day)
        && !(gettime(7) == ($@xmas2011_year + 1) && gettime(6) == 1 && gettime(5) <= $@xmas2011_reward_end_day))
        goto L_NoEvent;
    if ((gettime(7) == $@xmas2011_year && gettime(6) == 12 && gettime(5) >= $@xmas2011_reward_start_day)
        || (gettime(7) == ($@xmas2011_year + 1) && gettime(6) == 1 && gettime(5) <= $@xmas2011_reward_end_day))
        goto L_RewardTime;

    if (xmas11 & $@xmas11_startedReinbooSearch) goto L_Search;

    mes "The man is grumbling to himself and seems very worried.";
    if (xmas11 & $@xmas11_talkedToChief)
        goto L_Help;
    goto L_Close;

L_Help:
    menu
        "Chief Warrick asked me to help out a bit.",-,
        "Nevermind.",L_Close;
    mes "[Santa's Helper]";
    mes "\"He did? Well, I indeed have a problem.";
    mes "I'm Grombadil, Santa's equerry - the person in charge of the Reinboos.";
    next;
    mes "\"Those Reinboos, you know, are much more clever than a normal animal and can even talk.";
    mes "But they are more like children than an adult.\"";
    next;
    mes "\"And I fear something happened to some of my little fosterlings.";
    mes "It isn't a long time until Christmas and they all should have already arrived here.";
    mes "But look!\"";
    next;
    mes "He waves his hands to point at the area around him.";
    mes "[Grombadil]";
    mes "\"Only Reimeles and Leimeres are here.";
    mes "Meireles, Reilemes, Meileres and Leiremes are missing!";
    mes "And I have no idea where they could be!\"";
    next;
    mes "\"They could be anywhere and I can't leave here. Would you find them for me?\"";
    menu
        "Don't worry, they're as good as home.",-,
        "I'll keep my eyes open.",-,
        "I'm on my way!",-;
    mes "Grombadil looks relieved.";
    mes "[Grombadil]";
    mes "\"Oh, one thing before you leave!\"";
    next;
    mes "\"As I told you, they're a bit like children. And as children they love all kind of candy.";
    mes "But it's not good for them, they get an upset stomach from eating sweet stuff.\"";
    next;
    mes "\"So don't give them candy! You might want to take some apples with you, that's much better for them.";
    mes "Red apples. Don't give them candy. Good luck.\"";
    set xmas11, xmas11 | $@xmas11_startedReinbooSearch;
    goto L_Close;

L_Search:
    mes "[Grombadil]";
    mes "\"Welcome back! I hope you'll be able to find my fosterlings before it's too late.";
    mes "Imagine what would happen if they don't arrive in time!\"";
    next;
    mes "\"And remember, don't give them candy, but red apples. Those are the best ones.";


    cleararray @choice_idx, 0, 5;
    cleararray @choice$, "", 5;

    set @C_r1, 1;
    set @C_r2, 2;
    set @C_r3, 3;
    set @C_r4, 4;
    set @C_nevermind, 5;

    // counter of available answers
    set @choices_nr, 0;

    if (!(xmas11 & $@xmas11_firstReinbooDone))
        goto L_NoR1;
    set @choice_idx[@choices_nr], @C_r1;
    set @choice$[@choices_nr], "I found Meireles, he's about to come home.";
    set @choices_nr, @choices_nr + 1;

L_NoR1:
    if (!(xmas11 & $@xmas11_secondReinbooDone))
        goto L_NoR2;
    set @choice_idx[@choices_nr], @C_r2;
    set @choice$[@choices_nr], "Meileres is on his way home.";
    set @choices_nr, @choices_nr + 1;

L_NoR2:
    if (!(xmas11 & $@xmas11_thirdReinbooDone))
        goto L_NoR3;
    set @choice_idx[@choices_nr], @C_r3;
    set @choice$[@choices_nr], "Reilemes said he'll follow my smell back home.";
    set @choices_nr, @choices_nr + 1;

L_NoR3:
    if (!(xmas11 & $@xmas11_fourthReinbooDone))
        goto L_NoR4;
    set @choice_idx[@choices_nr], @C_r4;
    set @choice$[@choices_nr], "I found Leiremes, he should be here anytime.";
    set @choices_nr, @choices_nr + 1;

L_NoR4:
    set @choice_idx[@choices_nr], @C_nevermind;
    set @choice$[@choices_nr], "I'm still searching.";

    menu
        @choice$[0],-,
        @choice$[1],-,
        @choice$[2],-,
        @choice$[3],-,
        @choice$[4],-;

    set @menu, @menu - 1;
    if (@choice_idx[@menu] == 0)
        goto L_Close;
    if ((@choice_idx[@menu] == @C_nevermind))
        goto L_Close;
    mes "[Grombadil]";
    mes "\"Ah, that are good news!\"";
    goto L_Close;

L_NoEvent:
    mes "[Grombadil]";
    mes "\"Everything is calm and peaceful.\"";
    mes "He smiles.";
    goto L_Close;

L_RewardTime:
    mes "[Grombadil]";
    mes "\"All this stress is finally over. You might want to talk to Santa.\"";
    goto L_Close;

L_Close:
    cleararray @choice_idx, 0, 5;
    cleararray @choice$, "", 5;
    set @C_r1, 0;
    set @C_r2, 0;
    set @C_r3, 0;
    set @C_r4, 0;
    set @C_nevermind, 0;
    set @choices_nr, 0;
    close;
}

030-1.gat,96,57,0|script|Reimeles|170,{

    if (!(gettime(7) == $@xmas2011_year && gettime(6) == 12 && gettime(5) >= $@xmas2011_start_day)
        && !(gettime(7) == ($@xmas2011_year + 1) && gettime(6) == 1 && gettime(5) <= $@xmas2011_reward_end_day))
        goto L_NoEvent;
    if ((gettime(7) == $@xmas2011_year && gettime(6) == 12 && gettime(5) >= $@xmas2011_reward_start_day)
        || (gettime(7) == ($@xmas2011_year + 1) && gettime(6) == 1 && gettime(5) <= $@xmas2011_reward_end_day))
        goto L_RewardTime;

    mes "[Reimeles the Reinboo]";
    mes "\"I miss Reilemes, he's my best friend.\"";
    close;

L_NoEvent:
    mes "[Reimeles the Reinboo]";
    mes "\"Hello. Do you want to see Santa? He's not at home.\"";
    close;

L_RewardTime:
    mes "[Reimeles the Reinboo]";
    mes "\"Ah, I'm tired. But it was fun!\"";
    close;
}

030-1.gat,100,46,0|script|Leimeres|170,{

    if (!(gettime(7) == $@xmas2011_year && gettime(6) == 12 && gettime(5) >= $@xmas2011_start_day)
        && !(gettime(7) == ($@xmas2011_year + 1) && gettime(6) == 1 && gettime(5) <= $@xmas2011_reward_end_day))
        goto L_NoEvent;
    if ((gettime(7) == $@xmas2011_year && gettime(6) == 12 && gettime(5) >= $@xmas2011_reward_start_day)
        || (gettime(7) == ($@xmas2011_year + 1) && gettime(6) == 1 && gettime(5) <= $@xmas2011_reward_end_day))
        goto L_RewardTime;

    mes "[Leimeres the Reinboo]";
    mes "\"I like the snowflakes. Look, I can catch them with my mouth!\"";
    close;

L_NoEvent:
    mes "[Leimeres the Reinboo]";
    mes "\"It's nice here, isn't it?\"";
    close;

L_RewardTime:
    mes "[Leimeres the Reinboo]";
    mes "\"It was fun to ride with Santa!\"";
    close;
}

006-1.gat,126,112,0|script|Meireles the Reinboo|170,{
    set @cake_amount, 3;
    set @apple_amount, 5;

    if (!(gettime(7) == $@xmas2011_year && gettime(6) == 12 && gettime(5) >= $@xmas2011_start_day)
        && !(gettime(7) == ($@xmas2011_year + 1) && gettime(6) == 1 && gettime(5) <= $@xmas2011_reward_end_day))
        goto L_NoEvent;
    if ((gettime(7) == $@xmas2011_year && gettime(6) == 12 && gettime(5) >= $@xmas2011_reward_start_day)
        || (gettime(7) == ($@xmas2011_year + 1) && gettime(6) == 1 && gettime(5) <= $@xmas2011_reward_end_day))
        goto L_RewardTime;

    if ((xmas11 & $@xmas11_firstReinboo) && (xmas11 & $@xmas11_firstReinbooDone)) goto L_Bad;
    if (!(xmas11 & $@xmas11_firstReinboo) && (xmas11 & $@xmas11_firstReinbooDone)) goto L_Good;
    if (xmas11 & $@xmas11_firstReinboo) goto L_Food;
    if (xmas11 & $@xmas11_startedReinbooSearch) goto L_Search;

    mes "The reinboo shys away from you. It seems to be frightened.";
    goto L_Close;

L_Search:
    mes "The Reinboo comes close to you.";
    mes "[Meireles the Reinboo]";
    mes "\"Your smell is familiar!\"";
    menu
        "Grombadil sent me to find you.",-,
        "I don't talk with animals!",L_Close;
    mes "[Meireles the Reinboo]";
    mes "\"Oh! Yes, I shouldn't be here, but home.";
    mes "But I got lost.\"";
    menu
        "What happened?",-;
    mes "The Reinboo seems to feel ashamed.";
    mes "[Meireles the Reinboo]";
    mes "\"I was stupid. I was on the way home, but there was some guy with a mask.";
    mes "He gave me tasty cakes! So tasty...\"";
    next;
    mes "\"He made me go with him, he said he had more cakes. But he tricked me!";
    mes "He dashed some powder into my face and ran away.";
    mes "The powder burned in my nose and made me forget the smell of the way home.\"";
    next;
    mes "\"I tried to find the way, but I'm totally lost.";
    mes "This place is very odd.\"";
    next;
    mes "\"And all this running around made me become hungry.\"";
L_Food:
    mes "The Reinboo snuffles on you.";
    set xmas11, xmas11 | $@xmas11_firstReinboo;
    next;
    if (countitem("OrangeCake") >= @cake_amount)
        goto L_Cake;
    mes "[Meireles the Reinboo]";
    mes "\"Can you bring me some Orange Cakes? Please?";
    mes "When I have something to eat I can follow your smell back home.\"";
    if (countitem("RedApple") < @apple_amount)
        goto L_Close;
    menu
        "I'll try to find some.",L_Close,
        "Cakes aren't good for you, remember? But I have some apples.",L_GiveApple;

L_Cake:
    mes "[Meireles the Reinboo]";
    mes "\"You have some Orange Cakes! Give them to me! Please?\"";
    if (countitem("RedApple") >= @apple_amount)
        menu
            "I have something that is better for you. Apples!",L_GiveApple,
            "No, I want to keep them.",L_Close,
            "Ok, here.",L_GiveCake;
    menu
        "No, I want to keep them.",L_Close,
        "Ok, here.",L_GiveCake;

L_GiveCake:
    if (countitem("OrangeCake") < @cake_amount)
        goto L_NoItem;
    delitem "OrangeCake", @cake_amount;
    mes "[Meireles the Reinboo]";
    mes "\"Yam!\"";
    mes "The Reinboo munches the Orange Cakes.";
    set xmas11, xmas11 | $@xmas11_firstReinbooDone;
    goto L_Close;

L_GiveApple:
    if (countitem("RedApple") < @apple_amount)
        goto L_NoItem;
    delitem "RedApple", @apple_amount;
    mes "[Meireles the Reinboo]";
    mes "\"Apples, bah. Ok.\"";
    mes "The Reinboo munches the Apples.";
    set xmas11, xmas11 | $@xmas11_firstReinbooDone;
    set xmas11, xmas11 & ~$@xmas11_firstReinboo;
    goto L_Close;

L_NoItem:
    mes "[Meireles the Reinboo]";
    mes "\"Huh? It disappeared!\"";
    goto L_Close;

L_Good:
    mes "[Meireles the Reinboo]";
    mes "\"I feel better now. I'm about to follow your smell back to Grombadil.\"";
    goto L_Close;

L_Bad:
    mes "[Meireles the Reinboo]";
    mes "\"Uh, I don't feel well. My stomach hurts. I have to rest a little longer.\"";
    goto L_Close;

L_NoEvent:
    mes "The Reinboo just looks at you.";
    goto L_Close;

L_RewardTime:
    mes "[Meireles the Reinboo]";
    mes "\"Phew, that was close. I nearly didn't make it back in time.";
    mes "But everything went well and now I came back because this place made me curious.\"";
    goto L_Close;

L_Close:
    set @cake_amount, 0;
    set @apple_amount, 0;
    close;
}

012-1.gat,66,55,0|script|Meileres the Reinboo|170,{
    set @cake_amount, 3;
    set @apple_amount, 5;

    if (!(gettime(7) == $@xmas2011_year && gettime(6) == 12 && gettime(5) >= $@xmas2011_start_day)
        && !(gettime(7) == ($@xmas2011_year + 1) && gettime(6) == 1 && gettime(5) <= $@xmas2011_reward_end_day))
        goto L_NoEvent;
    if ((gettime(7) == $@xmas2011_year && gettime(6) == 12 && gettime(5) >= $@xmas2011_reward_start_day)
        || (gettime(7) == ($@xmas2011_year + 1) && gettime(6) == 1 && gettime(5) <= $@xmas2011_reward_end_day))
        goto L_RewardTime;

    if ((xmas11 & $@xmas11_secondReinboo) && (xmas11 & $@xmas11_secondReinbooDone)) goto L_Bad;
    if (!(xmas11 & $@xmas11_secondReinboo) && (xmas11 & $@xmas11_secondReinbooDone)) goto L_Good;
    if (xmas11 & $@xmas11_secondReinboo) goto L_Food;
    if (xmas11 & $@xmas11_startedReinbooSearch) goto L_Search;

    mes "The reinboo shys away from you. It seems to be frightened.";
    goto L_Close;

L_Search:
    mes "The Reinboo snuffles on your clothes.";
    mes "[Meileres the Reinboo]";
    mes "\"Mh, you smell like home.\"";
    menu
        "Grombadil sent me to find you.",-,
        "Back off from me!",L_Close;
    mes "[Meileres the Reinboo]";
    mes "\"Grombadil! He must be worried!\"";
    menu
        "He is. How did you get lost?",-,
        "What are you doing here?",-;
    mes "The Reinboo seems abashed.";
    mes "[Meileres the Reinboo]";
    mes "\"There was this guy with candy. Very tasty candy.";
    mes "He promised to give me more candy, so I followed him.\"";
    next;
    mes "\"But he didn't give me more candy! He dashed some stinging powder into my face!";
    mes "It hurt my nose and my eyes. And then he was gone and I don't know where I am.\"";
    next;
    mes "\"Usually I could find the way back with my remarkable nose.";
    mes "But because of that stinging powder I can't remember the smell of the way back.\"";
    next;
    mes "\"And I'm hungry.\"";
L_Food:
    mes "The Reinboo snuffles on you again.";
    set xmas11, xmas11 | $@xmas11_secondReinboo;
    next;
    if (countitem("ChocolateCake") >= @cake_amount)
        goto L_Cake;
    mes "[Meileres the Reinboo]";
    mes "\"Can you bring me some Chocolate Cakes? Please?";
    mes "Then I can go home, I can follow your smell to find the way.\"";
    if (countitem("RedApple") < @apple_amount)
        goto L_Close;
    menu
        "I will see what I can do.",L_Close,
        "Cakes aren't good for you! But I have some apples.",L_GiveApple;

L_Cake:
    mes "[Meileres the Reinboo]";
    mes "\"You have some Chocolate Cakes! Can I have them? Can I have them?\"";
    if (countitem("RedApple") >= @apple_amount)
        menu
            "I have something that is better for you. Apples!",L_GiveApple,
            "No, I want to keep them.",L_Close,
            "Ok, here.",L_GiveCake;
    menu
        "No, I want to keep them.",L_Close,
        "Ok, here.",L_GiveCake;

L_GiveCake:
    if (countitem("ChocolateCake") < @cake_amount)
        goto L_NoItem;
    delitem "ChocolateCake", @cake_amount;
    mes "[Meileres the Reinboo]";
    mes "\"Yay!\"";
    mes "The Reinboo munches the Chocolate Cakes.";
    set xmas11, xmas11 | $@xmas11_secondReinbooDone;
    goto L_Close;

L_GiveApple:
    if (countitem("RedApple") < @apple_amount)
        goto L_NoItem;
    delitem "RedApple", @apple_amount;
    mes "[Meileres the Reinboo]";
    mes "\"Hrm, ok, I'll eat the apples.\"";
    mes "The Reinboo munches the Apples.";
    set xmas11, xmas11 | $@xmas11_secondReinbooDone;
    set xmas11, xmas11 & ~$@xmas11_secondReinboo;
    goto L_Close;

L_NoItem:
    mes "[Meileres the Reinboo]";
    mes "\"Huh? It disappeared!\"";
    goto L_Close;

L_Good:
    mes "[Meileres the Reinboo]";
    mes "\"Hello! I'm about to follow your smell back to Grombadil.\"";
    goto L_Close;

L_Bad:
    mes "[Meileres the Reinboo]";
    mes "\"Uh, my stomach. It hurts. I have to rest a little longer.\"";
    goto L_Close;

L_NoEvent:
    mes "The Reinboo just looks at you.";
    goto L_Close;

L_RewardTime:
    mes "[Meileres the Reinboo]";
    mes "\"Phew, that was close. I nearly didn't make it back in time.";
    mes "But everything went well and now I came back because this place is interesting.\"";
    goto L_Close;

L_Close:
    set @cake_amount, 0;
    set @apple_amount, 0;
    close;
}

018-1.gat,114,41,0|script|Reilemes the Reinboo|170,{
    set @cake_amount, 3;
    set @apple_amount, 5;

    if (!(gettime(7) == $@xmas2011_year && gettime(6) == 12 && gettime(5) >= $@xmas2011_start_day)
        && !(gettime(7) == ($@xmas2011_year + 1) && gettime(6) == 1 && gettime(5) <= $@xmas2011_reward_end_day))
        goto L_NoEvent;
    if ((gettime(7) == $@xmas2011_year && gettime(6) == 12 && gettime(5) >= $@xmas2011_reward_start_day)
        || (gettime(7) == ($@xmas2011_year + 1) && gettime(6) == 1 && gettime(5) <= $@xmas2011_reward_end_day))
        goto L_RewardTime;

    if ((xmas11 & $@xmas11_thirdReinboo) && (xmas11 & $@xmas11_thirdReinbooDone)) goto L_Bad;
    if (!(xmas11 & $@xmas11_thirdReinboo) && (xmas11 & $@xmas11_thirdReinbooDone)) goto L_Good;
    if (xmas11 & $@xmas11_thirdReinboo) goto L_Food;
    if (xmas11 & $@xmas11_startedReinbooSearch) goto L_Search;

    mes "The reinboo shys away from you. It seems to be frightened.";
    goto L_Close;

L_Search:
    mes "The Reinboo comes closer and snuffles on your clothes.";
    mes "[Reilemes the Reinboo]";
    mes "\"Did you met Reimeles? He's my best friend. I miss him.\"";
    menu
        "Yes, Grombadil sent me to find you.",-,
        "Hey, stay away from me!",L_Close;
    mes "[Reilemes the Reinboo]";
    mes "\"Oh, Grombadil! He's very nervous, right? I should have been home long ago.\"";
    menu
        "He is. How did you get lost?",-,
        "Reimeles misses you too, why are you here?",-;
    mes "The Reinboo seems embarrassed.";
    mes "[Reilemes the Reinboo]";
    mes "\"I got distracted. Some man gave me tasty candy!";
    mes "He said he had more at his place and told me to come with him.\"";
    next;
    mes "\"But there wasn't more tasty candy! He dashed some stinging powder into my face!";
    mes "My eyes and nose still feel itchy.\"";
    next;
    mes "\"That's why I can't find the way back. My nose doesn't remember the smell!\"";
    next;
    mes "\"And I'm hungry.\"";
L_Food:
    mes "The Reinboo snuffles on you again.";
    set xmas11, xmas11 | $@xmas11_thirdReinboo;
    next;
    if (countitem("WhiteCake") >= @cake_amount)
        goto L_Cake;
    mes "[Reilemes the Reinboo]";
    mes "\"Can you bring me some White Cakes? I love those!";
    mes "Then I can go home, I can follow your smell to find the way.\"";
    if (countitem("RedApple") < @apple_amount)
        goto L_Close;
    menu
        "I will see what I can do.",L_Close,
        "Cakes aren't good for you! But I have some apples.",L_GiveApple;

L_Cake:
    mes "[Reilemes the Reinboo]";
    mes "\"You have some White Cakes! I want those! Please, can I have them?\"";
    if (countitem("RedApple") >= @apple_amount)
        menu
            "I have something that is better for you. Apples!",L_GiveApple,
            "No, I want to keep them.",L_Close,
            "Ok, here.",L_GiveCake;
    menu
        "No, I want to keep them.",L_Close,
        "Ok, here.",L_GiveCake;

L_GiveCake:
    if (countitem("WhiteCake") < @cake_amount)
        goto L_NoItem;
    delitem "WhiteCake", @cake_amount;
    mes "[Reilemes the Reinboo]";
    mes "\"Yay!\"";
    mes "The Reinboo munches the White Cakes.";
    set xmas11, xmas11 | $@xmas11_thirdReinbooDone;
    goto L_Close;

L_GiveApple:
    if (countitem("RedApple") < @apple_amount)
        goto L_NoItem;
    delitem "RedApple", @apple_amount;
    mes "[Reilemes the Reinboo]";
    mes "\"Hrm, ok, I'll eat the apples.\"";
    mes "The Reinboo munches the Apples.";
    set xmas11, xmas11 | $@xmas11_thirdReinbooDone;
    set xmas11, xmas11 & ~$@xmas11_thirdReinboo;
    goto L_Close;

L_NoItem:
    mes "[Reilemes the Reinboo]";
    mes "\"Huh? It disappeared!\"";
    goto L_Close;

L_Good:
    mes "[Reilemes the Reinboo]";
    mes "\"Hello! I'm about to follow your smell back to Grombadil and Reimeles.\"";
    goto L_Close;

L_Bad:
    mes "[Reilemes the Reinboo]";
    mes "\"Uh, my stomach. It hurts. I have to rest a little longer.\"";
    goto L_Close;

L_NoEvent:
    mes "The Reinboo just looks at you.";
    goto L_Close;

L_RewardTime:
    mes "[Reilemes the Reinboo]";
    mes "\"I was nearly late! But everything went well.";
    mes "Now I'm playing hide and seek with Reimeles.\"";
    goto L_Close;

L_Close:
    set @cake_amount, 0;
    set @apple_amount, 0;
    close;
}

017-1.gat,35,49,0|script|Leiremes the Reinboo|170,{
    set @cake_amount, 3;
    set @apple_amount, 5;

    if (!(gettime(7) == $@xmas2011_year && gettime(6) == 12 && gettime(5) >= $@xmas2011_start_day)
        && !(gettime(7) == ($@xmas2011_year + 1) && gettime(6) == 1 && gettime(5) <= $@xmas2011_reward_end_day))
        goto L_NoEvent;
    if ((gettime(7) == $@xmas2011_year && gettime(6) == 12 && gettime(5) >= $@xmas2011_reward_start_day)
        || (gettime(7) == ($@xmas2011_year + 1) && gettime(6) == 1 && gettime(5) <= $@xmas2011_reward_end_day))
        goto L_RewardTime;

    if ((xmas11 & $@xmas11_fourthReinboo) && (xmas11 & $@xmas11_fourthReinbooDone)) goto L_Bad;
    if (!(xmas11 & $@xmas11_fourthReinboo) && (xmas11 & $@xmas11_fourthReinbooDone)) goto L_Good;
    if (xmas11 & $@xmas11_fourthReinboo) goto L_Food;
    if (xmas11 & $@xmas11_startedReinbooSearch) goto L_Search;

    mes "The reinboo shys away from you. It seems to be frightened.";
    goto L_Close;

L_Search:
    mes "The Reinboo approaches you and snuffles on your clothes.";
    mes "[Leiremes the Reinboo]";
    mes "\"I like your smell. You smell like home. Have you been at Santa's place?\"";
    menu
        "I was, I'm searching for you.",-,
        "Wah, a talking Reinboo!",L_Close;
    mes "[Leiremes the Reinboo]";
    mes "\"Searching for me? Oh, I'm late, right? I'm so sorry. I didn't mean to cause trouble.\"";
    menu
        "How did you get lost?",-,
        "Well, now I found you. How did you come here?",-;
    mes "The Reinboo looks at you sheepishly.";
    mes "[Leiremes the Reinboo]";
    mes "\"I... I did something stupid. There was a man, he was really friendly to me. He gave me candy!";
    mes "I love candy. It's so tasty! The man said he can give me more at his place.\"";
    next;
    mes "\"So I went with him. But he was evil! He didn't give me candy! He dashed some bad itchy powder in my face.";
    mes "It made me forget the smell of the way home and now I'm lost.\"";
    next;
    mes "\"I was wandering around and searching for it. But that only made me tired. And hungry. Oh, I wish I had more candy.\"";
L_Food:
    mes "The Reinboo snuffles on you again.";
    set xmas11, xmas11 | $@xmas11_fourthReinboo;
    next;
    if (countitem("AppleCake") >= @cake_amount)
        goto L_Cake;
    mes "[Leiremes the Reinboo]";
    mes "\"Can you bring me some Apple Cakes? I like them so much.";
    mes "After that I will feel better and go home. Because I can follow your smell.\"";
    if (countitem("RedApple") < @apple_amount)
        goto L_Close;
    menu
        "Ok, hang on.",L_Close,
        "Apple Cakes? What about apples instead?",L_GiveApple;

L_Cake:
    mes "[Leiremes the Reinboo]";
    mes "\"You have some Apple Cakes! Tasty tasty Apple Cakes! Can I have them?\"";
    if (countitem("RedApple") >= @apple_amount)
        menu
            "I have something that is better for you. Apples!",L_GiveApple,
            "No, I want to keep them.",L_Close,
            "Ok, here.",L_GiveCake;
    menu
        "No, I want to keep them.",L_Close,
        "Ok, here.",L_GiveCake;

L_GiveCake:
    if (countitem("AppleCake") < @cake_amount)
        goto L_NoItem;
    delitem "AppleCake", @cake_amount;
    mes "[Leiremes the Reinboo]";
    mes "\"Hooray!\"";
    mes "The Reinboo munches the Apple Cakes.";
    set xmas11, xmas11 | $@xmas11_fourthReinbooDone;
    goto L_Close;

L_GiveApple:
    if (countitem("RedApple") < @apple_amount)
        goto L_NoItem;
    delitem "RedApple", @apple_amount;
    mes "[Leiremes the Reinboo]";
    mes "\"But - but cakes are much more tasty! Bleah, ok.\"";
    mes "The Reinboo munches the Apples.";
    set xmas11, xmas11 | $@xmas11_fourthReinbooDone;
    set xmas11, xmas11 & ~$@xmas11_fourthReinboo;
    goto L_Close;

L_NoItem:
    mes "[Leiremes the Reinboo]";
    mes "\"Huh? It disappeared!\"";
    goto L_Close;

L_Good:
    mes "[Leiremes the Reinboo]";
    mes "\"I'm not hungry anymore. Just a short break, then I'll go home.\"";
    goto L_Close;

L_Bad:
    mes "[Leiremes the Reinboo]";
    mes "\"Uh uh uh. It hurts. I need to rest a little longer.\"";
    goto L_Close;

L_NoEvent:
    mes "The Reinboo just looks at you.";
    goto L_Close;

L_RewardTime:
    mes "[Leiremes the Reinboo]";
    mes "\"Ha, it was fun to ride with Santa! I came back here because this place is nice.\"";
    goto L_Close;

L_Close:
    set @cake_amount, 0;
    set @apple_amount, 0;
    close;
}
