// This file is part of Christmas Event 2011
// author: Jenalya
// Reino asks for reed bundles and water to make wrapping paper
// also for some dyes to make them colorful
// later he can be asked for those papers to wrap boxes

030-2.gat,140,31,0|script|Reino|328,{

    if (!(gettime(7) == $@xmas2011_year && gettime(6) == 12 && gettime(5) >= $@xmas2011_start_day)
        && !(gettime(7) == ($@xmas2011_year + 1) && gettime(6) == 1 && gettime(5) <= $@xmas2011_reward_end_day))
        goto L_NoEvent;
    if ((gettime(7) == $@xmas2011_year && gettime(6) == 12 && gettime(5) >= $@xmas2011_reward_start_day)
        || (gettime(7) == ($@xmas2011_year + 1) && gettime(6) == 1 && gettime(5) <= $@xmas2011_reward_end_day))
        goto L_RewardTime;

    set @reed_amount, 2;
    set @water_amount, 1;

    if (xmas11 & $@xmas11_talkedToChief) goto L_Helping;

    mes "[Busy Helper]";
    mes "\"Excuse me, but please don't interrupt. We're all very busy.\"";
    goto L_Close;

L_Helping:
    mes "[Reino]";
    mes "\"Oh, I see you're here to help. Maybe you can assist me with a problem we have.";
    mes "We're nearly out of wrapping paper for the present boxes.";
    mes "That's very very bad. Can you imagine how it would look if the presents didn't have wrapping paper?\"";
    next;
    mes "\"So, eh, I can only give you some of the remaining wrapping paper if you bring me the needed material to make new wrapping paper.";
    mes "For making one roll of wrapping paper, I need " + @reed_amount + " Reed Bundles, " + @water_amount + " Bottle of Water and some dye to make it colored.\"";
    next;
L_Again:
    mes "\"Let's see... These are the regulations for wrapping paper colors and designs for this year. Tell me which you want.\"";
    mes "He shows you an official looking piece of paper.";
    menu
        "Red dotted (2 Red Dyes)",L_Red,
        "Yellow dotted (2 Yellow Dyes)",L_Yellow,
        "Blue dotted (2 Light Blue Dyes)",L_LightBlue,
        "Purple striped (1 Purple Dye)",L_Purple,
        "Red with golden stripes (1 Red Dye and 1 Yellow Dye)",L_RedGolden,
        "Green with red stripes (1 Green Dye and 1 Red Dye)",L_GreenRed,
        "Why do you have a regulation for wrapping papers?!",L_Regulation,
        "I'm on my way.",L_Close;

L_Red:
    if ((countitem("ReedBundle") < @reed_amount) || (countitem("BottleOfWater") < @water_amount) || (countitem("RedDye") < 1))
        goto L_NoItems;
    delitem "ReedBundle", @reed_amount;
    delitem "BottleOfWater", @water_amount;
    delitem "RedDye", 2;
    getitem "RedDottedWrap", 1;
    mes "[Reino]";
    mes "\"This one is classical.\"";
    goto L_MadePaper;

L_Yellow:
    if ((countitem("ReedBundle") < @reed_amount) || (countitem("BottleOfWater") < @water_amount) || (countitem("YellowDye") < 1))
        goto L_NoItems;
    delitem "ReedBundle", @reed_amount;
    delitem "BottleOfWater", @water_amount;
    delitem "YellowDye", 2;
    getitem "YellowDottedWrap", 1;
    mes "[Reino]";
    mes "\"Right, bright paper brings good vibes.\"";
    goto L_MadePaper;

L_LightBlue:
    if ((countitem("ReedBundle") < @reed_amount) || (countitem("BottleOfWater") < @water_amount) || (countitem("LightBlueDye") < 1))
        goto L_NoItems;
    delitem "ReedBundle", @reed_amount;
    delitem "BottleOfWater", @water_amount;
    delitem "LightBlueDye", 2;
    getitem "BlueDottedWrap", 1;
    mes "[Reino]";
    mes "\"Plain and simple. Here.\"";
    goto L_MadePaper;

L_Purple:
    if ((countitem("ReedBundle") < @reed_amount) || (countitem("BottleOfWater") < @water_amount) || (countitem("PurpleDye") < 1))
        goto L_NoItems;
    delitem "ReedBundle", @reed_amount;
    delitem "BottleOfWater", @water_amount;
    delitem "PurpleDye", 1;
    getitem "PurpleStripedWrap", 1;
    mes "[Reino]";
    mes "\"Mh, personally I don't like it that much, but here you go.\"";
    goto L_MadePaper;

L_RedGolden:
    if ((countitem("ReedBundle") < @reed_amount) || (countitem("BottleOfWater") < @water_amount) || (countitem("RedDye") < 1) || (countitem("YellowDye") < 1))
        goto L_NoItems;
    delitem "ReedBundle", @reed_amount;
    delitem "BottleOfWater", @water_amount;
    delitem "RedDye", 1;
    delitem "YellowDye", 1;
    getitem "RedGoldenStripedWrap", 1;
    mes "[Reino]";
    mes "\"Good choice, this is my favorite this year.\"";
    goto L_MadePaper;

L_GreenRed:
    if ((countitem("ReedBundle") < @reed_amount) || (countitem("BottleOfWater") < @water_amount) || (countitem("RedDye") < 1) || (countitem("GreenDye") < 1))
        goto L_NoItems;
    delitem "ReedBundle", @reed_amount;
    delitem "BottleOfWater", @water_amount;
    delitem "RedDye", 1;
    delitem "GreenDye", 1;
    getitem "GreenRedStripedWrap", 1;
    mes "[Reino]";
    mes "\"Yeah, this is really Christmas-styled.\"";
    goto L_MadePaper;

L_MadePaper:
    next;
    mes "\"Do you need more wrapping paper?\"";
    menu
        "Yes.",L_Again,
        "No, I have all I need.",L_Close;

L_Regulation:
    mes "Reino seems irritated.";
    mes "[Reino]";
    mes "\"Well, because, ah - there would be total chaos without regulations!";
    mes "You know, this Christmas business requires a lot of coordination. Things have to be well-ordered.\"";
    goto L_Close;

L_NoEvent:
    mes "[Reino]";
    mes "\"How did you come in? At this time of the year the door should be locked!\"";
    warp "030-1.gat", 99, 55;
    goto L_Close;

L_RewardTime:
    mes "[Reino]";
    mes "\"I love Christmas time. Even if it was total chaos this year.\"";
    goto L_Close;

L_NoItems:
    mes "[Reino]";
    mes "\"Oh? It seems you don't have the required items.\"";
    goto L_Close;

L_Close:
    set @reed_amount, 0;
    set @water_amount, 0;
    close;
}
