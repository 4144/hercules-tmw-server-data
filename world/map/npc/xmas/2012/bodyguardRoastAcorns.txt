// This file is part of Christmas Event 2012
// author: Jenalya

030-2.gat,89,23,0|script|Verneri|334,{
    callfunc "xmas2012time";

    set @reinboo, (xmas2012 & NIBBLE_0_MASK) >> NIBBLE_0_SHIFT;

    if (@xmas_time == $@xmas2012_no_event_time)
        goto L_NoEvent;
    if (@xmas_time == $@xmas2012_reward_time)
        goto L_RewardTime;

    if (@reinboo > 7) goto L_RoastAcornAgain;

    mes "[Verneri]";
    mes "\"Hm? I'm currently having a break. It's really nice here at the fireplace, isn't it?\"";
    next;
    mes "\"Ah, I remember when I was a child we used to roast acorns at the fireplace. We dunked them into Iron Potion and then put them over the fire for a while. That gave a tasty icing.\"";
    if (@reinboo != 7)
        goto L_Close;
    menu
        "Neiremes needs nutrient food, let's roast some.", -,
        "Children's stuff...", L_Close;

    mes "[Verneri]";
    mes "\"The little Reinboo? Ah, I heard he has some problems with flying.\"";
    next;
    mes "\"Well, if you get some Acorns and Iron Potions, we can do that.";
    mes "With one Iron Potion we can ice about " + $@xmas2012_acorn_amount + " Acorns.\"";
    next;
    mes "\"I think Neiremes will need about " + $@xmas2012_roasted_acorn_amount + " Roasted Acorns.\"";
    next;
    mes "[Verneri]";
    mes "\"Do you have everything we need? Shall we start?\"";
    menu
        "Sure!", L_Roast,
        "I need to get the ingredients.", L_Close;

L_Roast:
    if (countitem("Acorn") < $@xmas2012_acorn_amount || countitem("IronPotion") < 1)
        goto L_NoItems;

    delitem "IronPotion", 1;
    delitem "Acorn", $@xmas2012_acorn_amount;

    mes "Verneri takes the Iron Potion and the Acorns from you. He pours the potion into some bowl and adds the Acorns.";
    mes "Then he puts the bowl over the fire, stirring it every now and then. After a while it starts to smell really tasty.";
    next;
    mes "[Verneri]";
    mes "\"Here we are! Let me take one of them, ok?\"";
    mes "He carefully takes one of the roasted acorns and eats it.";
    next;

    set @roasted_acorns, $@xmas2012_acorn_amount - 1 - rand(4);
    mes "[Verneri]";
    mes "\"Ouch, hot. But tasty! Here's the rest of them for Neiremes. Some of them might be burnt, I'll throw those away.\"";
    getitem "RoastedAcorn", @roasted_acorns;
    menu
        "Let's roast some more.", L_Roast,
        "Thank you.", L_Close;

L_RoastAcornAgain:
    mes "[Verneri]";
    mes "\"Hello again. Do you want to roast some more acorns? They're delicious, right? Maybe we should do this every year.\"";
    menu
        "Not now.", L_Close,
        "Yes, I brought the ingredients.", L_Roast;

L_NoItems:
    mes "[Verneri]";
    mes "\"You need to bring at least one Iron Potion and " + $@xmas2012_acorn_amount + " Acorns.\"";
    goto L_Close;

L_RewardTime:
    mes "[Verneri]";
    mes "\"Now that Christmas is over we're going to leave this residence soon. Santa spends the rest of the year at other places on Kaizei, untiringly working on the concerns of the Sages of Kaizei. He's really impressive.\"";
    if (@reinboo < 8)
        goto L_Close;
    next;
    mes "\"Make sure to come back next year. We could roast some more acorns then.\"";
    goto L_Close;

L_NoEvent:
    mes "[Verneri]";
    mes "\"How did you come in? At this time of the year the door should be locked!\"";
    warp "030-1.gat", 99, 55;
    goto L_Close;

L_Close:
    set @reinboo, 0;
    set @xmas_time, 0;
    close;
}
